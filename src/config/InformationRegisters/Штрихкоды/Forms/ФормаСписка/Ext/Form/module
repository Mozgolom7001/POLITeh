
// Процедура - обработчик события "Нажатие" кнопки "Заполнить".
//
Процедура ДействияФормыЗаполнить(Кнопка)

	Результат = РегистрыСведений.ШтрихКоды.ПолучитьФорму("ФормаЗаполнения", ЭтаФорма).ОткрытьМодально();

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТипEAN13 = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN13;
	ПрефиксШтучногоТовара = СокрЛП(Константы.ПрефиксШтучногоТовара.Получить());
	ПрефиксВнутреннегоШтрихкода = Константы.ПрефиксВнутреннегоШтрихкода.Получить();

	НаборЗаписей = РегистрыСведений.Штрихкоды.СоздатьНаборЗаписей();

	СозданоНовых = 0;

	Выборка = Результат.Выбрать();

	Попытка
		Пока Выборка.Следующий() Цикл
			НаборЗаписей.Очистить();

			НоваяСтрока = НаборЗаписей.Добавить();
			НоваяСтрока.ТипШтрихкода               = ТипEAN13;
			НоваяСтрока.Штрихкод                   = РаботаСТорговымОборудованием.СформироватьШтрихКод(ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода);
			НоваяСтрока.Владелец                   = Выборка.Номенклатура;
			НоваяСтрока.ЕдиницаИзмерения           = Выборка.ЕдиницаИзмерения;
			НоваяСтрока.СерияНоменклатуры          = Выборка.СерияНоменклатуры;
			НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			НоваяСтрока.Качество                   = Выборка.Качество;

			НаборЗаписей.Отбор.ТипШтрихкода.Установить(НоваяСтрока.ТипШтрихкода);
			НаборЗаписей.Отбор.Штрихкод.Установить(НоваяСтрока.Штрихкод);
			НаборЗаписей.Отбор.Владелец.Установить(НоваяСтрока.Владелец);
			НаборЗаписей.Отбор.ЕдиницаИзмерения.Установить(НоваяСтрока.ЕдиницаИзмерения);
			НаборЗаписей.Отбор.СерияНоменклатуры.Установить(НоваяСтрока.СерияНоменклатуры);
			НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Установить(НоваяСтрока.ХарактеристикаНоменклатуры);
			НаборЗаписей.Отбор.Качество.Установить(НоваяСтрока.Качество);

			НаборЗаписей.Записать();

			СозданоНовых = СозданоНовых + 1;
		КонецЦикла;
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки());
	КонецПопытки;

	Предупреждение("Создано " + СокрЛП(СозданоНовых) + " новых записей.");

КонецПроцедуры // ДействияФормыЗаполнить()

// Процедура - обработчик события "ПриВыводеСтроки" ТЧ.
//
Процедура РегистрСведенийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ТипЗнч(ДанныеСтроки.Владелец) = Тип("СправочникСсылка.Номенклатура")
	   И Не ЗначениеЗаполнено(ДанныеСтроки.Качество) Тогда
		ОформлениеСтроки.Ячейки.Качество.ОтметкаНезаполненного = Истина;
	КонецЕсли;

КонецПроцедуры // РегистрСведенийСписокПриВыводеСтроки()
