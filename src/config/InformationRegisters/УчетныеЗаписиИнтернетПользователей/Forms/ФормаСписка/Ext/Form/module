
Процедура ДействияФормыПодбор(Кнопка)

	ФормаВыбораКонтрагентов = Справочники.Контрагенты.ПолучитьФормуВыбора(, ЭтаФорма);
	ФормаВыбораКонтрагентов.ПараметрВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.ГруппыИЭлементы;
	ФормаВыбораКонтрагентов.МножественныйВыбор = Ложь;
	ФормаВыбораКонтрагентов.ЗакрыватьПриВыборе = Ложь;
	ФормаВыбораКонтрагентов.Открыть();

КонецПроцедуры

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	Если ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если ЗначениеВыбора.ЭтоГруппа Тогда
			Ответ = Вопрос("Вы выбрали группу. Добавить все ее элементы? ", РежимДиалогаВопрос.ДаНет);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;

		НаборЗаписей = РегистрыСведений.УчетныеЗаписиИнтернетПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		ТЗ = НаборЗаписей.Выгрузить(); 
		ТЗ.Индексы.Добавить("Контрагент");
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("Контрагент", "");

		НужноЗаписывать = Ложь;
		Запрос = Новый Запрос();
		Запрос.Текст ="ВЫБРАТЬ
		              |	Контрагенты.Ссылка,
		              |	Контрагенты.Код
		              |ИЗ
		              |	Справочник.Контрагенты КАК Контрагенты
		              |ГДЕ
		              |	Контрагенты.Ссылка В ИЕРАРХИИ(&МассивВыбора)
		              |	И (НЕ Контрагенты.ЭтоГруппа)";
		Запрос.УстановитьПараметр("МассивВыбора", ЗначениеВыбора);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураПоиска.Контрагент = Выборка.Ссылка;
			МасивСтрок = ТЗ.НайтиСтроки(СтруктураПоиска);
			Если МасивСтрок.Количество() = 0 Тогда
				ЭлементНабора = НаборЗаписей.Добавить();
				ЭлементНабора.Логин      = Выборка.Код;
				ЭлементНабора.Пароль     = УправлениеПользователями.СгенерироватьПароль();
				ЭлементНабора.Контрагент = Выборка.Ссылка;

				НужноЗаписывать = Истина;
			Иначе
				Сообщить("Контрагент """ + СокрЛП(Выборка.Ссылка) + """ уже есть в списке 
						 | Интернет-пользователей с логином """ + СокрЛП(МасивСтрок[0].Логин) + """.", СтатусСообщения.Важное);
			КонецЕсли;
		КонецЦикла;
		
		Если НужноЗаписывать Тогда
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Отказ = Не Константы.ИспользоватьWEBЗаказы.Получить();
	Если Отказ Тогда
		Предупреждение("В настройке параметров учета запрещено использование
		               | WEB-приложения ""Управление заказами"".");
	КонецЕсли;

КонецПроцедуры
