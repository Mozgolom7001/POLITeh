
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСВызовСервера.ПриПолученииФормыРегистраСведений(
		"НастройкиОбменаЕГАИС",
		ВидФормы,
		Параметры,
		ВыбраннаяФорма,
		ДополнительнаяИнформация,
		СтандартнаяОбработка);
	
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Получает список доступных транспортных модулей ЕГАИС.
//
// Параметры:
//  Отбор - Структура, Массив - структура с ключами: Поле, Значение. Или массив таких структур.
//
// Возвращаемое значение:
//   Массив - список транспортных модулей ЕГАИС.
//
Функция ДоступныеТранспортныеМодули(Отбор = Неопределено) Экспорт

	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.ИНН, """") КАК ИНН,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.КПП, """") КАК КПП,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.Представление, """") КАК Представление,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.ПредставлениеАдреса, """") КАК Адрес,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)) КАК ОрганизацияЕГАИС,
	|	ЕСТЬNULL(ФорматыОбменаЕГАИС.ФорматОбмена, ЗНАЧЕНИЕ(Перечисление.ФорматыОбменаЕГАИС.V1)) КАК ФорматОбмена,
	|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР КАК ИдентификаторФСРАР,
	|	НастройкиОбменаЕГАИС.АдресУТМ КАК АдресУТМ,
	|	НастройкиОбменаЕГАИС.ПортУТМ КАК ПортУТМ,
	|	НастройкиОбменаЕГАИС.ОбработкаОбслуживания КАК ОбработкаОбслуживания,
	|	НастройкиОбменаЕГАИС.ЗагружатьВходящиеДокументы КАК ЗагружатьВходящиеДокументы,
	|	НастройкиОбменаЕГАИС.Организация КАК Организация,
	|	НастройкиОбменаЕГАИС.Склад КАК Склад
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО НастройкиОбменаЕГАИС.ИдентификаторФСРАР = КлассификаторОрганизацийЕГАИС.Код
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФорматыОбменаЕГАИС КАК ФорматыОбменаЕГАИС
	|		ПО НастройкиОбменаЕГАИС.ИдентификаторФСРАР = ФорматыОбменаЕГАИС.ИдентификаторФСРАР
	|";
	
	Если ТипЗнч(Отбор) = Тип("Массив") Тогда
		МассивОтборов = Отбор;
	Иначе
		МассивОтборов = Новый Массив;
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		МассивОтборов.Добавить(Отбор);
	КонецЕсли;
	
	Сч = 0;
	ТекстУсловий = "";
	Для Каждого СтруктураОтбора Из МассивОтборов Цикл
		Если НЕ ПустаяСтрока(ТекстУсловий) Тогда
			ТекстУсловий = ТекстУсловий + " И ";
		Иначе
			ТекстУсловий = " ГДЕ ";
		КонецЕсли;
		
		ТекстУсловий = ТекстУсловий + "НастройкиОбменаЕГАИС." + СтруктураОтбора.Поле + " = &Параметр_" + Сч;
		Запрос.УстановитьПараметр("Параметр_" + Сч, СтруктураОтбора.Значение);
		
		Сч = Сч + 1;
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса + ТекстУсловий;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеМодуля = Новый Структура;
		Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
			ДанныеМодуля.Вставить(Колонка.Имя);
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(ДанныеМодуля, Выборка);
		
		Если ПустаяСтрока(ДанныеМодуля.Представление) Тогда
			ДанныеМодуля.Представление = Выборка.ИдентификаторФСРАР;
		КонецЕсли;
		
		ИмяОбработкиОбслуживания = "";
		ОбработкаОбслуживания = Выборка.ОбработкаОбслуживания.Получить();
		Если ОбработкаОбслуживания <> Неопределено Тогда
			ИмяОбработкиОбслуживания = ПолучитьИмяВременногоФайла("epf");
			Попытка
				ОбработкаОбслуживания.Записать(ИмяОбработкиОбслуживания);
			Исключение
				ТекстСообщения = НСтр("ru = 'Не удалось подключить обработку обслуживания.'") + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
			КонецПопытки;
		КонецЕсли;
		
		ДанныеМодуля.ФорматОбмена = ИнтеграцияЕГАИСКлиентСервер.ФорматОбмена(Выборка.ФорматОбмена);
		ДанныеМодуля.Вставить("ИмяОбработкиОбслуживания", ИмяОбработкиОбслуживания);
		
		ИнтеграцияЕГАИСПереопределяемый.ПриПолученииНастроекУТМ(ДанныеМодуля);
		
		Результат.Добавить(ДанныеМодуля);
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецЕсли