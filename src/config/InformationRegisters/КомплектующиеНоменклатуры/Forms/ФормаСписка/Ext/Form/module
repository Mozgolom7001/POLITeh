
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура устанавливает видимость для элементов формы
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	Если ЗначениеЗаполнено(ХарактеристикаНоменклатуры) Тогда
		ЭлементыФормы.РегистрСведенийСписок.Колонки.ХарактеристикаНоменклатуры.Видимость = Ложь;
	Иначе
		ЭлементыФормы.РегистрСведенийСписок.Колонки.ХарактеристикаНоменклатуры.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	УстановитьВидимость();

КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПриИзменении" поля "ХарактеристикаНоменклатуры"
//
Процедура ХарактеристикаНоменклатурыПриИзменении(Элемент)

	Если НЕ ЗначениеЗаполнено(ХарактеристикаНоменклатуры) Тогда
		ОтборПоКомплектующим = РегистрСведенийСписок.Отбор.ХарактеристикаНоменклатуры;
		ОтборПоКомплектующим.Использование = Ложь;
	Иначе
		ОтборПоКомплектующим = РегистрСведенийСписок.Отбор.ХарактеристикаНоменклатуры;
		ОтборПоКомплектующим.ВидСравнения  = ВидСравнения.Равно;
		ОтборПоКомплектующим.Значение      = ХарактеристикаНоменклатуры;
		ОтборПоКомплектующим.Использование = Истина;
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода комплектующей
//
Процедура РегистрСведенийСписокКомплектующаяПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;

	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Комплектующая) Тогда
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.Комплектующая.ЕдиницаХраненияОстатков;
	КонецЕсли;

	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Комплектующая)
	   И СтрокаТабличнойЧасти.ХарактеристикаКомплектующей.Владелец <> СтрокаТабличнойЧасти.Комплектующая Тогда
		СтрокаТабличнойЧасти.ХарактеристикаКомплектующей = Неопределено;
	КонецЕсли;

КонецПроцедуры // РегистрСведенийСписокКомплектующаяПриИзменении()

Процедура РегистрСведенийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ЭлементыФормы.РегистрСведенийСписок.Колонки.АртикулКомплектующей.Видимость Тогда
		ОформлениеСтроки.Ячейки.АртикулКомплектующей.УстановитьТекст(ДанныеСтроки.Комплектующая.Артикул);
	КонецЕсли;
	Если ЭлементыФормы.РегистрСведенийСписок.Колонки.Артикул.Видимость Тогда
		ОформлениеСтроки.Ячейки.Артикул.УстановитьТекст(ДанныеСтроки.Номенклатура.Артикул);
	КонецЕсли;
КонецПроцедуры


