
Процедура ПриОткрытии()

	// Для новой записи заполним значение по умолчанию.
	Если НЕ Выбран() и ПараметрОбъектКопирования = Неопределено Тогда
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда // если заполнена - значит это был переход по кнопке перейти из справочника Организации.
			Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		КонецЕсли;
		
		УчетнаяПолитикаНУ = РегистрыСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(Период, Новый Структура("Организация", Организация)); 
		
		Если УчетнаяПолитикаНУ.Количество() = 0 Тогда
			
			МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке;
			СложныйУчетНДС = Ложь;
			
			НДСРежимУчетаРаспределенныхОплат = Перечисления.НДСРежимУчетаРаспределенныхОплат.Приоритет_НДСНеМожетБытьПринятКВычету;
			НДСИспользованиеОплатПокупателя_Приоритет0 = Ложь;
			НДСНалоговыйПериод = Перечисления.Периодичность.Месяц;
			ПорядокРегистрацииСчетовФактурНаАванс = Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.НаВсеАвансы;
			
			СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая;
			ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы;
			
		Иначе
			ТекущаяУчетнаяПолитикаНУ = УчетнаяПолитикаНУ.Получить(0);
			
			ОрганизацияЯвляетсяПлательщикомЕНВД = ТекущаяУчетнаяПолитикаНУ.ОрганизацияЯвляетсяПлательщикомЕНВД;
			РозничнаяТорговляОблагаетсяЕНВД = ТекущаяУчетнаяПолитикаНУ.РозничнаяТорговляОблагаетсяЕНВД;
			
			МоментОпределенияНалоговойБазыНДС = ТекущаяУчетнаяПолитикаНУ.МоментОпределенияНалоговойБазыНДС;
			СложныйУчетНДС = ТекущаяУчетнаяПолитикаНУ.СложныйУчетНДС;
			НДСРежимУчетаРаспределенныхОплат = ТекущаяУчетнаяПолитикаНУ.НДСРежимУчетаРаспределенныхОплат;
			НДСИспользованиеОплатПокупателя_Приоритет0 = ТекущаяУчетнаяПолитикаНУ.НДСИспользованиеОплатПокупателя_Приоритет0;
			НДСНалоговыйПериод = ТекущаяУчетнаяПолитикаНУ.НДСНалоговыйПериод;
			ПорядокРегистрацииСчетовФактурНаАванс = ТекущаяУчетнаяПолитикаНУ.ПорядокРегистрацииСчетовФактурНаАванс;
			
			СистемаНалогообложения = ТекущаяУчетнаяПолитикаНУ.СистемаНалогообложения;
			ОбъектНалогообложенияУСН = ТекущаяУчетнаяПолитикаНУ.ОбъектНалогообложенияУСН;
			
		КонецЕсли;
	ИначеЕсли НЕ Выбран() и не ПараметрОбъектКопирования = Неопределено Тогда
		Период = НачалоМесяца(РабочаяДата);
	КонецЕсли;

	УправлениеМоментомОпределенияНалоговойБазыПоНДС();
	УправлениеФлагами_ПартионныйУчетНДСвРазрезеСерийИХарактеристик_Складов();
	УправлениеНалоговымПериодомПоНДС();

	УправлениеВидимостьюДоступностью();

	ПроверитьКассыККМ();

КонецПроцедуры

Процедура ЗаполнитьСписокВыбораНалоговогоПериодаНДС()
	
	СписокПеречисления = Новый СписокЗначений;

	СписокПеречисления.Добавить(Перечисления.Периодичность.Месяц);
	СписокПеречисления.Добавить(Перечисления.Периодичность.Квартал);
	РаботаСДиалогами.УстановитьСписокПоляВыбора(ЭлементыФормы.НДСНалоговыйПериод, СписокПеречисления);

КонецПроцедуры // ЗаполнитьСписокВыбораНалоговогоПериодаНДС()

// Процедура - обработчик события "ПриИзменении" поля ввода "НДСНалоговыйПериод".
// 
Процедура НДСНалоговыйПериодПриИзменении(Элемент)
	
	Если НДСНалоговыйПериод.Пустая() Тогда
		НДСНалоговыйПериод = ?(Период < '20080101', Перечисления.Периодичность.Месяц, Перечисления.Периодичность.Квартал);
	КонецЕсли;
	
КонецПроцедуры // НДСНалоговыйПериодПриИзменении()

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода "НДСНалоговыйПериод".
// 
Процедура НДСНалоговыйПериодНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ЗаполнитьСписокВыбораНалоговогоПериодаНДС();
	
КонецПроцедуры // НДСНалоговыйПериодНачалоВыбораИзСписка()

Процедура УправлениеМоментомОпределенияНалоговойБазыПоНДС()
	
	Положения2006Года = (Период >='20060101');
	Если Положения2006Года и МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОплате Тогда
	     МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке;
	КонецЕсли; 

КонецПроцедуры

Процедура УправлениеНалоговымПериодомПоНДС()
	
	Положения2008Года = (Период >='20080101');
	Если Положения2008Года и НДСНалоговыйПериод = Перечисления.Периодичность.Месяц Тогда
		НДСНалоговыйПериод = Перечисления.Периодичность.Квартал;
	КонецЕсли; 
	
	УправлениеВидимостьюДоступностью();
	
КонецПроцедуры

Процедура УправлениеФлагами_ПартионныйУчетНДСвРазрезеСерийИХарактеристик_Складов()
	
	Если не СложныйУчетНДС и ПартионныйУчетНДСвРазрезеСерийИХарактеристик Тогда
		ПартионныйУчетНДСвРазрезеСерийИХарактеристик = Ложь;
	КонецЕсли; 
	
	Если не СложныйУчетНДС и ПартионныйУчетНДСвРазрезеСкладов Тогда
		ПартионныйУчетНДСвРазрезеСкладов = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПериодПриИзменении(Элемент)
	
	УправлениеВидимостьюДоступностью();
	УправлениеМоментомОпределенияНалоговойБазыПоНДС();
	УправлениеНалоговымПериодомПоНДС();
	
КонецПроцедуры

Процедура СложныйУчетНДСПриИзменении(Элемент)
	
	УправлениеВидимостьюДоступностью();
	УправлениеФлагами_ПартионныйУчетНДСвРазрезеСерийИХарактеристик_Складов();
	
КонецПроцедуры

Процедура УправлениеВидимостьюДоступностью()
	
	ЭлементыФормы.РозничнаяТорговляОблагаетсяЕНВД.Доступность = ОрганизацияЯвляетсяПлательщикомЕНВД;
	ЭлементыФормы.ПанельНДС.Видимость = Не ОрганизацияНеЯвляетсяПлательщикомНДС;
	
	Положения2006Года = (Период >='20060101');
	ЭлементыФормы.МоментОпределенияНалоговойБазыНДС.Доступность = Не Положения2006Года;
	
	Положения2008Года = (Период >='20080101');
	ЭлементыФормы.НДСНалоговыйПериод.Доступность = Не Положения2008Года;
	
	ЭлементыФормы.ПартионныйУчетНДСвРазрезеСерийИХарактеристик.Доступность = СложныйУчетНДС;
	ЭлементыФормы.ПартионныйУчетНДСвРазрезеСкладов.Доступность = СложныйУчетНДС;
	ЭлементыФормы.НДСПриНеподтвержденииСверху.Доступность = СложныйУчетНДС;
	
	ЭлементыФормы.ПрименяетсяОсвобождениеОтУплатыНДС.Видимость = УчетНДС.ВерсияПостановленияНДС1137(Период) = 3;
	
	Упрощенная = СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;
	
	ЭлементыФормы.ОбъектНалогообложения.Видимость = Упрощенная;
	ЭлементыФормы.Доходы.Видимость = Упрощенная;
	ЭлементыФормы.ДоходыМинусРасходы.Видимость = Упрощенная;
	
КонецПроцедуры

Процедура ОрганизацияНеЯвляетсяПлательщикомНДСПриИзменении(Элемент)
	
	УправлениеВидимостьюДоступностью();
	
КонецПроцедуры

Функция ПроверитьЗаполнениеРеквизитов()
	
	СписокОбязательныхПолей = Новый СписокЗначений;
	
	СписокОбязательныхПолей.Добавить(Организация, "Не указана организация");		
	СписокОбязательныхПолей.Добавить(Период, "Не указана дата начала действия учетной политики");		
	
	Если Не ОрганизацияНеЯвляетсяПлательщикомНДС Тогда
		СписокОбязательныхПолей.Добавить(ПорядокРегистрацииСчетовФактурНаАванс, "На закладке ""НДС"" не указан порядок регистрации счетов-фактур на аванс");
	КонецЕсли;
	

	ЕстьНеЗаполненные = Ложь;

	 Для Каждого Строка ИЗ СписокОбязательныхПолей Цикл
		 Если Не ЗначениеЗаполнено(Строка.Значение) Тогда
			 Сообщить(Строка);
			 ЕстьНеЗаполненные = Истина;
		 КонецЕсли;
	 КонецЦикла;
	 
	 Возврат ЕстьНеЗаполненные;
 
КонецФункции

Процедура ПередЗаписью(Отказ)

	УправлениеМоментомОпределенияНалоговойБазыПоНДС();
	УправлениеНалоговымПериодомПоНДС();

	Отказ = ПроверитьЗаполнениеРеквизитов();

КонецПроцедуры

Процедура ПослеЗаписи()

	ПроверитьКассыККМ();

	Оповестить("ИзменениеУчетнойПолитики");

КонецПроцедуры

Процедура ПроверитьКассыККМ()

	ЭлементыФормы.СписокКассККМ.Видимость = Ложь;

	// Проверка всех связанных с данной организацией касс ККМ на наличие включенного параметра "Формировать нефискальные чеки"
	Если Не РозничнаяТорговляОблагаетсяЕНВД Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.Период
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&Дата, Организация = &Ссылка) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
		|");
		Запрос.УстановитьПараметр("Ссылка", Организация);
		Запрос.УстановитьПараметр("Дата"  , ТекущаяДата());

		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();

		Если Выборка.Период = НачалоМесяца(Период) Тогда
			СуществуютНефискальныеКассы = Ложь;

			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	КассыККМ.ФормироватьНефискальныеЧеки
			|ИЗ
			|	Справочник.КассыККМ КАК КассыККМ
			|ГДЕ
			|	КассыККМ.Владелец = &Ссылка
			|");
			Запрос.УстановитьПараметр("Ссылка", Организация);

			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.ФормироватьНефискальныеЧеки Тогда
					СуществуютНефискальныеКассы = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;

			Если СуществуютНефискальныеКассы Тогда
				Сообщить("Внимание! Для организаций, у которых розничная торговля не облагается ЕНВД, " +
				"использование режима формирования нефискальных чеков ККМ недопустимо.
				|Перейдите к списку касс ККМ и выполните отбор по признакам:
				| - ""Владелец"" = """ + Организация + """
				| - ""Формировать нефискальные чеки"" = Истина.
				|Для всех отобранных касс снимите флажок ""Формировать нефискальные чеки"" .", СтатусСообщения.ОченьВажное);

				ЭлементыФормы.СписокКассККМ.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура СписокКассККМНажатие(Элемент)

	ФормаСпискаКассККМ = Справочники.КассыККМ.ПолучитьФормуСписка();

	ФормаСпискаКассККМ.СправочникСписок.Отбор.Владелец.Установить(Организация);
	ФормаСпискаКассККМ.СправочникСписок.Отбор.ФормироватьНефискальныеЧеки.Установить(Истина);

	Если ФормаСпискаКассККМ.Открыта() Тогда
		ФормаСпискаКассККМ.Активизировать();
	Иначе
		ФормаСпискаКассККМ.Открыть();
	КонецЕсли;

КонецПроцедуры

Процедура ОрганизацияЯвляетсяПлательщикомЕНВДПриИзменении(Элемент)
	
	РозничнаяТорговляОблагаетсяЕНВД = ОрганизацияЯвляетсяПлательщикомЕНВД;
	УправлениеВидимостьюДоступностью();
	
КонецПроцедуры

Процедура СистемаНалогообложенияПриИзменении(Элемент)
	УправлениеВидимостьюДоступностью();
КонецПроцедуры

ЗаполнитьСписокВыбораНалоговогоПериодаНДС()