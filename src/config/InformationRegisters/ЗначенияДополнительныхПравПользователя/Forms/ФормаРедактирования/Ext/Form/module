Процедура ОпределитьТекущегоПользователяГруппуПользователя()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь", глЗначениеПеременной("глТекущийПользователь"));
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Вложенный.Пользователь
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь КАК Пользователь,
	|		0 КАК Приоритет
	|	ИЗ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
	|	ГДЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь = &ТекущийПользователь
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь,
	|		1
	|	ИЗ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.ПользователиГруппы КАК ПользователиГруппы
	|			ПО ПользователиГруппы.Ссылка = ЗначенияДополнительныхПравПользователя.Пользователь
	|				И (ПользователиГруппы.Пользователь = &ТекущийПользователь)) КАК Вложенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вложенный.Приоритет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Пользователь = Выборка.Пользователь;
	Иначе
		Пользователь = глЗначениеПеременной("глТекущийПользователь");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДерево()
	
	ДеревоПрав.Строки.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Родитель,
	|	Ссылка,
	|	ЭтоГруппа,
	|	ЗначениеПрав.Значение
	|ИЗ
	|	ПланВидовХарактеристик.ПраваПользователей КАК Права
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначениеПрав
	|		ПО ЗначениеПрав.Право=Права.Ссылка
	|		И ЗначениеПрав.Пользователь = &Пользователь
	|УПОРЯДОЧИТЬ ПО
	|	Права.ЭтоГруппа ИЕРАРХИЯ,
	|	Права.Наименование
	|");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если  Не Выборка.Родитель.Пустая() Тогда
			СтрокаГруппы = ДеревоПрав.Строки.Найти(Выборка.Родитель, "Право", Истина);
			Если СтрокаГруппы=Неопределено Тогда
				СтрокаГруппы = ДеревоПрав.Строки.Добавить();
				СтрокаГруппы.Право = Выборка.Родитель;
				СтрокаГруппы.ЭтоГруппа = Выборка.ЭтоГруппа;
			КонецЕсли;
		Иначе
			СтрокаГруппы = ДеревоПрав;
		КонецЕсли;		
		СтрокаПрава = СтрокаГруппы.Строки.Добавить();
		СтрокаПрава.Право = Выборка.Ссылка;
		СтрокаПрава.Значение = Выборка.Ссылка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		СтрокаПрава.ЭтоГруппа = Выборка.ЭтоГруппа;
	КонецЦикла;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

Процедура ДеревоПравПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Право.ОтображатьКартинку=Истина;
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Право.ИндексКартинки=0;
		ОформлениеСтроки.Ячейки.Значение.ОтображатьТекст = Ложь
	Иначе
		ОформлениеСтроки.Ячейки.Право.ИндексКартинки=1;
		
		Если ДанныеСтроки.Право.ТипЗначения.СодержитТип(Тип("Булево"))
			И ДанныеСтроки.Право.ТипЗначения.Типы().Количество()=1 Тогда
			ОформлениеСтроки.Ячейки.Значение.ОтображатьТекст = Ложь;
			ОформлениеСтроки.Ячейки.Значение.ОтображатьФлажок = Истина;
			ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
			ОформлениеСтроки.Ячейки.Значение.Флажок = ДанныеСтроки.Право.ТипЗначения.ПривестиЗначение(ДанныеСтроки.Значение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДеревоПравПриИзмененииФлажка(Элемент, Колонка)
	СтрокаДерева = ДеревоПрав.Строки.Найти(Элемент.ТекущиеДанные.Право, "Право", Истина);
	СтрокаДерева.Значение = Не СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

Процедура ОбновитьНастройки()
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Использование = Истина;
	Набор.Отбор.Пользователь.Значение = Пользователь;
	ЗаполнитьНаборЗаписей(ДеревоПрав.Строки, Набор);
	Набор.Записать();
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(СтрокиДерева, НаборЗаписей)
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Не СтрокаДерева.ЭтоГруппа Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Пользователь = Пользователь;
			Запись.Право = СтрокаДерева.Право;
			Запись.Значение = СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
		Иначе
			ЗаполнитьНаборЗаписей(СтрокаДерева.Строки, НаборЗаписей);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОпределитьТекущегоПользователяГруппуПользователя();
	
	Если Не РольДоступна("ПолныеПрава") Тогда		
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗаполнитьДерево();
	Если ЗначениеЗаполнено(Право) Тогда
		СтрокаДерева = ДеревоПрав.Строки.Найти(Право, "Право", Истина);
		Если СтрокаДерева <> Неопределено Тогда
			ЭлементыФормы.Дерево.ТекущаяСтрока = СтрокаДерева;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДеревоЗначениеПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЭтаФорма.Модифицированность Тогда
		Ответ = Вопрос("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОбновитьНастройки();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура НаборПравПриИзменении(Элемент)
	
	ЗаполнитьДерево();
	
КонецПроцедуры

Процедура НаборПравНачалоВыбора(Элемент, СтандартнаяОбработка)
	Если ЭтаФорма.Модифицированность Тогда
		Ответ = Вопрос("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОбновитьНастройки();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанельПрименить(Кнопка)
	ОбновитьНастройки();
КонецПроцедуры

Процедура КоманднаяПанельОК(Кнопка)
	
	ОбновитьНастройки();
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

ДеревоПрав.Колонки.Добавить("ЭтоГруппа");
Право = Неопределено;