Перем млНовый;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТП "РегистрСведенийСписок"

// Процедура - обработчик события "ПриНачалеРедактирования"
//
// Параметры
//  Элемент     – <ТабличноеПоле> 
//              – редактируемое ТП ("РегистрСведенийСписок")
//
//  НоваяСтрока – <Булево> 
//              – признак редактирования новой строки; имеет значение Истина,
//                если строка была добавлена или скопирована.
//
//  Копирование - <Булево> 
//              – если данный параметр имеет значение Истина, то выполняется 
//                копирование строки
//
Процедура РегистрСведенийСписокПриНачалеРедактирования(Элемент, НоваяСтрока,
	                                                   Копирование)

	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Код =
			УправлениеРозничнойТорговлей.ПолучитьНовыйКодДляРегистраСведений("КодыВесовогоТовара");
		Элемент.ТекущиеДанные.Качество = млНовый;
	КонецЕсли;

КонецПроцедуры // РегистрСведенийСписокПриНачалеРедактирования()

// Процедура - обработчик события "Нажатие" кнопки "Заполнить".
//
Процедура ДействияФормыЗаполнить(Кнопка)

	Результат = РегистрыСведений.КодыВесовогоТовара.ПолучитьФорму("ФормаЗаполнения", ЭтаФорма).ОткрытьМодально();

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РегКоды = РегистрыСведений.КодыВесовогоТовара;

	СозданоНовых = 0;

	Выборка = Результат.Выбрать();

	Попытка
		Пока Выборка.Следующий() Цикл
			МЗ = РегКоды.СоздатьМенеджерЗаписи();
			МЗ.Код                        = УправлениеРозничнойТорговлей.ПолучитьНовыйКодДляРегистраСведений("КодыВесовогоТовара");
			МЗ.Номенклатура               = Выборка.Номенклатура;
			МЗ.СерияНоменклатуры          = Выборка.СерияНоменклатуры;
			МЗ.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			МЗ.Качество                   = Выборка.Качество;
			МЗ.Записать();

			СозданоНовых = СозданоНовых + 1;
		КонецЦикла;
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки());
	КонецПопытки;

	Предупреждение("Создано " + СокрЛП(СозданоНовых) + " новых записей.");

КонецПроцедуры // ДействияФормыЗаполнить()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

млНовый = Справочники.Качество.Новый;