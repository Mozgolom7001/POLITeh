// Вызывается из тела процедуры "Сформировать"
// осуществляет проверку значений "НачалоПериода" и "КонецПериода"
// введенных пользователем.
Функция ПроверкаПериода()
	
	ПроверкаПройдена=Истина;
	
	Если НачалоПериода > КонецПериода Тогда
		
		Предупреждение("Неправильно задан период формирования отчета!"+Символы.ПС+
		               "Дата начала больше даты окончания периода.");
					   
		ПроверкаПройдена=Ложь;
		
	ИначеЕсли НачалоПериода='00010101' Тогда
		
		Предупреждение("Не указана дата начала отчета");
					   
		ПроверкаПройдена=Ложь;
		
	ИначеЕсли КонецПериода='00010101' Тогда
		
		Предупреждение("Не указана дата конца отчета");
					   
		ПроверкаПройдена=Ложь;
	
	КонецЕсли;
		
	Возврат ПроверкаПройдена;
	
КонецФункции // ПроверкаПериода()

// Процедура вызывается при нажатии кнопки настройки периода в диалоге документа.
// Позволяет в удобном диалоговом окне задать значения реквизитов НачалоПериода
// и КонецПериода
Процедура ВыбПериодНажатие(Элемент)
	
	РаботаСДиалогами.ОбработчикНастройкаПериодаНажатие(НачалоПериода, КонецПериода);
	
КонецПроцедуры

Процедура Сформировать(Кнопка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ККМ) Тогда
		Предупреждение("Не указана касса ККМ.");
		Возврат;
	КонецЕсли;

	Если НЕ ПроверкаПериода() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчет(ЭлементыФормы.ТабличныйДокумент);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = НачалоКвартала(РабочаяДата);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонецПериода) Тогда
		КонецПериода = КонецКвартала(РабочаяДата);
	КонецЕсли;
	
    ОтчетСформирован = Ложь;
	ВыводЗаголовка();

КонецПроцедуры

Процедура ККМНачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФВ = Справочники.КассыККМ.ПолучитьФормуВыбора(,Элемент,);
	Если ЗначениеЗаполнено(Организация) Тогда
		ФВ.ПараметрОтборПоВладельцу = Организация;
	КонецЕсли;
	ФВ.Отбор.АрхивироватьЧеки.ВидСравнения  = ВидСравнения.Равно;
	ФВ.Отбор.АрхивироватьЧеки.Значение      = Истина;
	ФВ.Отбор.АрхивироватьЧеки.Использование = Истина;
	ФВ.ЭлементыФормы.СправочникСписок.НастройкаОтбора.АрхивироватьЧеки.Доступность = Истина;
	ФВ.Открыть();
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ККМ) И ККМ.Владелец <> Элемент.Значение Тогда
		ККМ = Справочники.КассыККМ.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанельЗаголовок(Кнопка)
	
	ПоказыватьЗаголовок = Не ПоказыватьЗаголовок;
	
	ВыводЗаголовка();
	
КонецПроцедуры

Процедура ВыводЗаголовка()

	Если ОтчетСформирован Тогда
		СформироватьОтчет(ЭлементыФормы.ТабличныйДокумент, Ложь);
	Иначе
		СформироватьОтчет(ЭлементыФормы.ТабличныйДокумент, Истина);
	КонецЕсли;

	ЭлементыФормы.КоманднаяПанель.Кнопки.Заголовок.Пометка = ПоказыватьЗаголовок;

КонецПроцедуры // ВыводЗаголовка()
