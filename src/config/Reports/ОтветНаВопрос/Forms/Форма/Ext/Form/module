Перем ВысотаЗаголовка;
Перем НеЗаполнятьНастройкиПриОткрытии;
Перем СтруктураСвязиЭлементовСДанными;
Перем ОтборРазвернут;

// Управляет пометками кнопок ком. панели
//
// Параметры:
//	Нет.
//
Процедура УправлениеПараметрамиОтображенияЭлементовФормы()
	
	Если ОбщийОтчет.ПоказыватьЗаголовок Тогда
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ЗаголовокОтчета.Пометка = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.ЗаголовокОтчета.Пометка = Истина;

	Иначе
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ЗаголовокОтчета.Пометка = Ложь;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.ЗаголовокОтчета.Пометка = Ложь;

	КонецЕсли;

	// Отображение заголовка отчета
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ЭлементыФормы.ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ОбщийОтчет.ПоказыватьЗаголовок;
	КонецЕсли;

	Если ОтборРазвернут Тогда
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.БыстрыеОтборы.Пометка = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.БыстрыеОтборы.Пометка = Истина;
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Нет;
	Иначе
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.БыстрыеОтборы.Пометка = Ложь;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.БыстрыеОтборы.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УправлениеПараметрамиОтображенияЭлементовФормы()

//  Управляет выводом заголовка
//
// Параметры:
//	Нет.
//
Процедура ВыводЗаголовка()

	// Перезаполнять заголовок можно только у "чистого" отчета
	Если ЭлементыФормы.ДокументРезультат.ВысотаТаблицы = 0 Тогда

		СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ОбщийОтчет.ПоказыватьЗаголовок, ВысотаЗаголовка, Истина);
		
	КонецЕсли;

	УправлениеПараметрамиОтображенияЭлементовФормы();
	
КонецПроцедуры // ВыводЗаголовка()

// Формирует текст заголовка
//
// Параметры:
//	Нет.
//
Процедура СформироватьЗаголовокФормы()

	Заголовок = УправлениеОтчетами.СформироватьЗаголовокОсновнойФормы(, , ОбщийОтчет.мНазваниеОтчета, -1);

КонецПроцедуры // СформироватьЗаголовокФормы()

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьОтчет() Экспорт
	
	НеЗаполнятьНастройкиПриОткрытии = Не Открыта();
	
	СписокПустыхЗначений = Новый СписокЗначений;
	ТипыПВХ = Метаданные.ПланыВидовХарактеристик.ВопросыДляАнкетирования.Тип.Типы();
	Если ОбщийОтчет.ПостроительОтчета.Отбор.Найти("Вопрос") <> Неопределено Тогда
		ОтчетДиаграмма.мНазваниеОтчета = Строка(ОбщийОтчет.ПостроительОтчета.Отбор.Вопрос.Значение);
	КонецЕсли;
	
	Для каждого ТипИзХарактеристики из ТипыПВХ Цикл
		СписокПустыхЗначений.Добавить(ОбщегоНазначения.ПустоеЗначениеТипа(ТипИзХарактеристики));
	КонецЦикла;
	СписокПустыхЗначений.Добавить(Неопределено);
	
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("СписокПустыхЗначений",		СписокПустыхЗначений);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Анкета",					ЭлементыФормы.ПолеНастройкиАнкета.Значение);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Раздел",					ЭлементыФормы.ПолеНастройкиРаздел.Значение);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Опрос",					ЭлементыФормы.ПолеНастройкиОпрос.Значение);
	ОтчетДиаграмма.ПостроительОтчета.Параметры.Вставить("СписокПустыхЗначений",	СписокПустыхЗначений);
	ОтчетДиаграмма.ПостроительОтчета.Параметры.Вставить("Анкета",				ЭлементыФормы.ПолеНастройкиАнкета.Значение);
	ОтчетДиаграмма.ПостроительОтчета.Параметры.Вставить("Раздел",				ЭлементыФормы.ПолеНастройкиРаздел.Значение);
	ОтчетДиаграмма.ПостроительОтчета.Параметры.Вставить("Опрос",				ЭлементыФормы.ПолеНастройкиОпрос.Значение);
	
	Если ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.Таблица Тогда
		
		// обновляем настройки построителя отчетов, формирующего диаграмму
		ОтчетДиаграмма.ПостроительОтчета.УстановитьНастройки(ОбщийОтчет.ПостроительОтчета.ПолучитьНастройки());
		
		ЭлементыФормы.ПанельИзображений.ТекущаяСтраница = ЭлементыФормы.ПанельИзображений.Страницы.Таблица;
		ОбновитьТаблицуОтчета();
		
	ИначеЕсли ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.Диаграмма Тогда
		
		Если ОтчетДиаграмма.ПостроительОтчета.ИзмеренияСтроки.Количество() > 0 Тогда
			ИзмерениеСтрокиДиаграммы = ОтчетДиаграмма.ПостроительОтчета.ИзмеренияСтроки[0];
			ИзмерениеСтрокиТаблицы = ОбщийОтчет.ПостроительОтчета.ИзмеренияСтроки.Найти(ИзмерениеСтрокиДиаграммы.Имя);
			Если ИзмерениеСтрокиТаблицы <> Неопределено Тогда
				МестоИзмерения = ОбщийОтчет.ПостроительОтчета.ИзмеренияСтроки.Индекс(ИзмерениеСтрокиТаблицы);
				Если МестоИзмерения > 0 Тогда
					ОбщийОтчет.ПостроительОтчета.ИзмеренияСтроки.Сдвинуть(ИзмерениеСтрокиТаблицы,-МестоИзмерения);
				КонецЕсли;
			Иначе
				ОбщийОтчет.ПостроительОтчета.ИзмеренияСтроки.Вставить(ИзмерениеСтрокиДиаграммы.ПутьКДанным,ИзмерениеСтрокиДиаграммы.Имя,ИзмерениеСтрокиДиаграммы.ТипИзмерения,ИзмерениеСтрокиДиаграммы.Макет,ИзмерениеСтрокиДиаграммы.МакетИерархии,0);
				ИзмерениеКолонкиТаблицы = ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Найти(ИзмерениеСтрокиДиаграммы.Имя);
				Если ИзмерениеКолонкиТаблицы <> Неопределено Тогда
					ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Удалить(ИзмерениеКолонкиТаблицы)
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ОтчетДиаграмма.ПостроительОтчета.ИзмеренияКолонки.Количество() > 0 Тогда
			ИзмерениеКолонкиДиаграммы = ОтчетДиаграмма.ПостроительОтчета.ИзмеренияКолонки[0];
			ИзмерениеКолонкиТаблицы = ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Найти(ИзмерениеКолонкиДиаграммы.Имя);
			Если ИзмерениеКолонкиТаблицы <> Неопределено Тогда
				МестоИзмерения = ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Индекс(ИзмерениеКолонкиТаблицы);
				Если МестоИзмерения > 0 Тогда
					ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Сдвинуть(ИзмерениеКолонкиТаблицы,-МестоИзмерения);
				КонецЕсли;
			ИначеЕсли ОбщийОтчет.ПостроительОтчета.ИзмеренияСтроки.Найти(ИзмерениеКолонкиДиаграммы.Имя) = Неопределено Тогда
				ОбщийОтчет.ПостроительОтчета.ИзмеренияКолонки.Вставить(ИзмерениеКолонкиДиаграммы.ПутьКДанным,ИзмерениеКолонкиДиаграммы.Имя,ИзмерениеКолонкиДиаграммы.ТипИзмерения,ИзмерениеКолонкиДиаграммы.Макет,ИзмерениеКолонкиДиаграммы.МакетИерархии,0);
			КонецЕсли;
		КонецЕсли;
		
		ОтчетДиаграмма.Показатели.Загрузить(ОбщийОтчет.Показатели.Выгрузить());
		
		ЭлементыФормы.ПанельИзображений.ТекущаяСтраница = ЭлементыФормы.ПанельИзображений.Страницы.Диаграмма;
		ОбновитьДиаграмму();
		
	ИначеЕсли ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.СводнаяТаблица Тогда
		
		// обновляем настройки построителя отчетов, формирующего диаграмму
		ОтчетДиаграмма.ПостроительОтчета.УстановитьНастройки(ОбщийОтчет.ПостроительОтчета.ПолучитьНастройки());
		
		ЭлементыФормы.ПанельИзображений.ТекущаяСтраница = ЭлементыФормы.ПанельИзображений.Страницы.СводнаяТаблица;
		ОбновитьСводнуюТаблицу();
		
	КонецЕсли;
	
КонецПроцедуры // ОбновитьОтчет()

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьТаблицуОтчета() Экспорт

	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ОбщийОтчет.ПоказыватьЗаголовок, ВысотаЗаголовка);

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ЭлементыФормы.ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ОбщийОтчет.ПоказыватьЗаголовок;
	КонецЕсли;

	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ДокументРезультат;

	УправлениеПараметрамиОтображенияЭлементовФормы();
	
КонецПроцедуры // ОбновитьТаблицуОтчета()

Процедура ОбновитьДиаграмму() Экспорт
	
	СформироватьДиаграмму(ЭлементыФормы.Диаграмма);
	ОбщийОтчетДиаграмма = ОтчетДиаграмма;
	
	Если ОбщийОтчетДиаграмма.НомерПоказателя = 2 тогда // это показатель Развернутый ответ
		ЭлементыФормы.Диаграмма.Очистить();
		РезультатРазвернутыйОтвет = ОтчетДиаграмма.ОбщийОтчет.ПостроительОтчета.Результат;
		ТаблицаРазвернутыйОтвет = РезультатРазвернутыйОтвет.Выгрузить(ОбходРезультатаЗапроса.Прямой);
		ТаблицаРазвернутыйОтвет.Колонки.Добавить("Ответ");
		СписокСтрокКУдалению = Новый СписокЗначений;
		Для Каждого СтрокаТаблицаРазвернутыйОтвет из ТаблицаРазвернутыйОтвет Цикл
			Попытка
				СтрокаТаблицаРазвернутыйОтвет.Ответ = Число(СтрокаТаблицаРазвернутыйОтвет.РазвернутыйОтвет);
			Исключение
			КонецПопытки;
		КонецЦикла;
		ТаблицаРазвернутыйОтвет.Свернуть("ТиповойОтвет", "Ответ");
		ВсегоСтрок = ТаблицаРазвернутыйОтвет.Количество() - 1;
		Для индСтроки = 0 по ВсегоСтрок Цикл
			СтрокаТаблицаРазвернутыйОтвет = ТаблицаРазвернутыйОтвет[ВсегоСтрок - индСтроки];
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицаРазвернутыйОтвет.ТиповойОтвет) тогда
				ТаблицаРазвернутыйОтвет.Удалить(СтрокаТаблицаРазвернутыйОтвет);
			КонецЕсли;
		КонецЦикла;
		ЭлементыФормы.Диаграмма.ИсточникДанных = ТаблицаРазвернутыйОтвет;
	КонецЕсли;
	ТипДиаграммыОтчета = ОбщийОтчетДиаграмма.ТипДиаграммыОтчета;
	Если ТипДиаграммыОтчета <> Неопределено Тогда
		ЭлементыФормы.Диаграмма.ТипДиаграммы = ТипДиаграммыОтчета;
	КонецЕсли;
	
	Если ОбщийОтчетДиаграмма.ВидПодписейККруговойДиаграмме<>Неопределено Тогда
		ЭлементыФормы.Диаграмма.ВидПодписей = ОбщийОтчетДиаграмма.ВидПодписейККруговойДиаграмме;
	КонецЕсли;
	
	Если ОбщийОтчетДиаграмма.РаздвижениеСерийКруговойДиаграммы<>Неопределено Тогда
		ЭлементыФормы.Диаграмма.АвтоРаздвижениеСерий = ОбщийОтчетДиаграмма.РаздвижениеСерийКруговойДиаграммы;
	КонецЕсли; 
	
	ЭлементыФормы.Диаграмма.Свет = ОбщийОтчетДиаграмма.Свет;
	ЭлементыФормы.Диаграмма.Окантовка = ОбщийОтчетДиаграмма.Окантовка;
	ЭлементыФормы.Диаграмма.Градиент = ОбщийОтчетДиаграмма.Градиент;
	
	Если ОбщийОтчетДиаграмма.ОриентацияИзометрическойДиаграммы <> Неопределено Тогда
		ЭлементыФормы.Диаграмма.Ориентация = ОбщийОтчетДиаграмма.ОриентацияИзометрическойДиаграммы;
	КонецЕсли;
	
	Если ОбщийОтчетДиаграмма.РежимПробеловДиаграммыСОбластями <> Неопределено Тогда
		ЭлементыФормы.Диаграмма.РежимПробелов = ОбщийОтчетДиаграмма.РежимПробеловДиаграммыСОбластями;
	КонецЕсли;
	
	Если ОбщийОтчетДиаграмма.ОграничениеСерий<>Неопределено Тогда
		ЭлементыФормы.Диаграмма.МаксимумСерий = ОбщийОтчетДиаграмма.ОграничениеСерий;
	КонецЕсли;
	
	ЭлементыФормы.Диаграмма.МаксимумСерийКоличество = ОбщийОтчетДиаграмма.МаксимумСерийКоличество;
	ЭлементыФормы.Диаграмма.МаксимумСерийПроцент = ОбщийОтчетДиаграмма.МаксимумСерийПроцент;

	ВыводимыйПоказатель = "";
	Если ОбщийОтчетДиаграмма.НомерПоказателя < ОбщийОтчетДиаграмма.мТаблицаПоказатели.Количество() Тогда
		ВыводимыйПоказатель = ОбщийОтчетДиаграмма.мТаблицаПоказатели[ОбщийОтчетДиаграмма.НомерПоказателя].ПредставлениеПоля;
	КонецЕсли;
	
	ЭлементыФормы.Диаграмма.ОбластьПостроения.ВертикальныеМетки = ОбщийОтчетДиаграмма.ВертикальныеМетки;

	ЭлементыФормы.Диаграмма.ОбластьЗаголовка.Текст = ОбщийОтчетДиаграмма.мНазваниеОтчета + ": " + ВыводимыйПоказатель;
	
	ЭлементыФормы.Диаграмма.БазовоеЗначение = ОбщийОтчетДиаграмма.БазовоеЗначение;
	
	ЭлементыФормы.Диаграмма.ОтображатьЗаголовок = ОбщийОтчетДиаграмма.ОтображатьЗаголовок;
	
	ЭлементыФормы.Диаграмма.ОтображатьЛегенду   = ОбщийОтчетДиаграмма.ОтображатьЛегенду;
	
	ТекущийЭлемент = ЭлементыФормы.Диаграмма;

КонецПроцедуры

Процедура ОбновитьСводнуюТаблицу() Экспорт

	СформироватьСводнуюТаблицу(ЭлементыФормы.СводнаяТаблица.ВстроенныеТаблицы.СводнаяТаблица);
	
	ТекущийЭлемент = ЭлементыФормы.СводнаяТаблица;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНОЙ ПАНЕЛИ

// Процедура - обработчик нажатия кнопки "Обновить".
//
Процедура КоманднаяПанельФормыОбновить(Кнопка)

	ОбновитьОтчет();

КонецПроцедуры // ВыполнитьНажатие()

// Процедура - обработчик нажатия кнопки "Таблица"
//
Процедура КоманднаяПанельФормыОтображениеТаблица(Кнопка)
	
	ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.Таблица;
	УправлениеОтчетами.ПометитьКнопкиОтображения(ЭтотОбъект, ЭтаФорма);
	ОбновитьОтчет();
	
КонецПроцедуры // КоманднаяПанельФормыОтображениеТаблица()

// Процедура - обработчик нажатия кнопки "Диаграмма"
//
Процедура КоманднаяПанельФормыОтображениеДиаграмма(Кнопка)

	ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.Диаграмма;
	УправлениеОтчетами.ПометитьКнопкиОтображения(ЭтотОбъект, ЭтаФорма);
	ОбновитьОтчет();
	
КонецПроцедуры // КоманднаяПанельФормыОтображениеДиаграмма()

// Процедура - обработчик нажатия кнопки "Сводная таблица"
//
Процедура КоманднаяПанельФормыОтображениеСводнаяТаблица(Кнопка)

	ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.СводнаяТаблица;
	УправлениеОтчетами.ПометитьКнопкиОтображения(ЭтотОбъект, ЭтаФорма);
	ОбновитьОтчет();
	
КонецПроцедуры // КоманднаяПанельФормыОтображениеСводнаяТаблица()

// Процедура - обработчик нажатия кнопки "Настройка".
//
Процедура КоманднаяПанельФормыНастройка(Кнопка)
	
	Если ВариантОтображения = Перечисления.ВариантыОтображенияОтчетов.Диаграмма Тогда
		
		ФормаНастройка = ОтчетДиаграмма.ПолучитьФорму("ФормаНастройка", ЭтаФорма);
		
		ОтчетДиаграмма.мНазваниеОтчета = ОбщийОтчет.мНазваниеОтчета + " - настройка диаграммы"; // временно уточним название отчета
		
		РезультатОткрытия = ФормаНастройка.ОткрытьМодально();

		Если РезультатОткрытия = Истина Тогда
			ОбщийОтчет.Показатели[ОтчетДиаграмма.НомерПоказателя].Использование = Истина;
			ОбновитьОтчет();
		КонецЕсли;
		
		ОтчетДиаграмма.мНазваниеОтчета = ОбщийОтчет.мНазваниеОтчета;
		
	Иначе
		
		ФормаНастройка = ПолучитьФормуНастройки();
		ФормаНастройка.ВладелецФормы = ЭтаФорма;
		
		СтруктураСНастройками = СформироватьСтруктуруДляСохраненияНастроек(ОбщийОтчет.ПоказыватьЗаголовок);
		
		СтараяДатаКон = ОбщийОтчет.ДатаКон;
		СтараяДатаНач = ОбщийОтчет.ДатаНач;
		
		НазваниеОтчета = ОбщийОтчет.мНазваниеОтчета; // временно уточним название отчета
		мНазваниеОтчета = ОбщийОтчет.мНазваниеОтчета + " - настройка таблицы";
		
		РезультатОткрытия = ФормаНастройка.ОткрытьМодально();
		
		Если РезультатОткрытия = Истина Тогда
			
			Для каждого СтрокаТЗ Из ОбщийОтчет.Показатели Цикл
				Если СтрокаТЗ.Использование Тогда
					ОтчетДиаграмма.НомерПоказателя = ОбщийОтчет.Показатели.Индекс(СтрокаТЗ);
					Прервать
				КонецЕсли;
			КонецЦикла;
			ОбновитьОтчет();
			
		Иначе
			
			// Форму закрыли эскейпом или по "Закрыть" - восстановим настройки, отчет формировать не будем!
			// Восстанавливаем сохраненные значения
			
			ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ОбщийОтчет.ПоказыватьЗаголовок);
			
			ДатаКон = СтараяДатаКон;
			ДатаНач = СтараяДатаНач;
			
		КонецЕсли;
		
		мНазваниеОтчета = НазваниеОтчета;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "НовыйОтчет"
//
Процедура КоманднаяПанельФормыНовыйОтчет(Кнопка)
	
	Если Строка(ЭтотОбъект) = "ВнешняяОбработкаОбъект." + ЭтотОбъект.Метаданные().Имя Тогда
			
		Предупреждение("Данный отчет является внешней обработкой." + Символы.ПС + "Открытие нового отчета возможно только для объектов конфигурации.");
		Возврат;
			
	Иначе
			
		НовыйОтчет = Отчеты[ЭтотОбъект.Метаданные().Имя].Создать();
			
	КонецЕсли;
	
	ФормаНовогоОтчета = НовыйОтчет.ПолучитьФорму();
	ФормаНовогоОтчета.Открыть();

КонецПроцедуры // КоманднаяПанельФормыНовыйОтчет()

// Процедура - обработчик нажатия кнопки "БыстрыеОтборы"
//
Процедура КоманднаяПанельФормыБыстрыеОтборы(Кнопка)

	ОтборРазвернут = НЕ ОтборРазвернут;

	УправлениеПараметрамиОтображенияЭлементовФормы();
	
КонецПроцедуры // КоманднаяПанельФормыБыстрыеОтборы()

// Процедура - обработчик нажатия кнопки "ЗаголовокОтчета"
//
Процедура КоманднаяПанельФормыЗаголовокОтчета(Кнопка)
	
	ПоказыватьЗаголовок = Не ОбщийОтчет.ПоказыватьЗаголовок;
	
	ВыводЗаголовка();
	
КонецПроцедуры // КоманднаяПанельФормыЗаголовокОтчета()

// Процедура - обработчик нажатия кнопки "На принтер"
Процедура ДействияФормыНаПринтер(Кнопка)
	
	ЭлементыФормы.ДокументРезультат.Напечатать();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события перед открытием формы
// 
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Лев(ТекстЗапросаОтчета, 7) <> "ВЫБРАТЬ" тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НеЗаполнятьНастройкиПриОткрытии = Ложь Тогда
		ЗаполнитьНачальныеНастройки();
		ЗаполнитьНачальныеНастройкиДиаграммы(ЭлементыФормы.Диаграмма);
	КонецЕсли;
	
	ВысотаЗаголовка = 0;
	СформироватьЗаголовокФормы();
	// Вспоминим параметры для данной формы
	ИмяОтчета=Метаданные().Имя;
	ОтборРазвернут=?(ВосстановитьЗначение(ИмяОтчета + "_ОтборПомечен") = Истина, Истина, Ложь); // отбор в первый раз не показывать
	ОбщийОтчет.ПоказыватьЗаголовок = ?(ВосстановитьЗначение(ИмяОтчета + "_ЗаголовокПомечен") = Ложь, Ложь, Истина); // заголовок в первый раз наоборот, показывать
	ВыводЗаголовка();
	
	УправлениеОтчетами.УстановитьСвязьПолейБыстрогоОтбораНаФорме(ЭлементыФормы, ОбщийОтчет.ПостроительОтчета.Отбор, СтруктураСвязиЭлементовСДанными, "ОтчетОбъект.ОбщийОтчет.ПостроительОтчета.Отбор");
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события при открытии формы
//
Процедура ПриОткрытии(Отказ)
	
	ВариантОтображения = ?(ВариантОтображения.Пустая(), Перечисления.ВариантыОтображенияОтчетов.Таблица, ВариантОтображения);
	
	УправлениеОтчетами.ПометитьКнопкиОтображения(ЭтотОбъект, ЭтаФорма);
	
	НеЗаполнятьНастройкиПриОткрытии = Ложь;

	УправлениеПараметрамиОтображенияЭлементовФормы();
	
КонецПроцедуры

// Процедура - обработчик события перед сохранением значений формы
//
Процедура ПередСохранениемЗначений(Отказ)

	СохраненныеНастройки = СформироватьСтруктуруДляСохраненияНастроек(ОбщийОтчет.ПоказыватьЗаголовок);

КонецПроцедуры // ПередСохранениемЗначений()

// Процедура - обработчик события после восстановления значений формы
//
Процедура ПослеВосстановленияЗначений()

	// Если настройка восстанавливается, когда открывается форма сформровенного отчета, игнорируем
	Если НеЗаполнятьНастройкиПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОбщийОтчет.СохраненныеНастройки) = Тип("Структура") Тогда
		
		ВосстановитьНастройкиИзСтруктуры(ОбщийОтчет.СохраненныеНастройки, ОбщийОтчет.ПоказыватьЗаголовок);
		
		// Очистим результат - он более не соответствует настройке
		ЭлементыФормы.ДокументРезультат.Очистить();
		ВысотаЗаголовка=0;
		
		ВыводЗаголовка();
		
		СформироватьЗаголовокФормы();
		
	КонецЕсли;

КонецПроцедуры // ПослеВосстановленияЗначений()

// Процедура - обработчик события при закрытии
//
Процедура ПриЗакрытии()
	
	ИмяОтчета = Метаданные().Имя;
	
	// Запомним параметры для данной формы
	СохранитьЗначение( ИмяОтчета+ "_ОтборПомечен", ЭлементыФормы.КоманднаяПанельФормы.Кнопки.БыстрыеОтборы.Пометка);
	
	// Запомним параметры для данной формы
	СохранитьЗначение( ИмяОтчета+ "_ЗаголовокПомечен", ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ЗаголовокОтчета.Пометка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "Обработка расшифровки" поля табличного документа ДокументРезультат
//
Процедура ДокументРезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ОбработкаРасшифровки(Расшифровка, Элемент, ВысотаЗаголовка, СтандартнаяОбработка);

КонецПроцедуры // ДокументРезультатОбработкаРасшифровки()

// Процедура - обработчик нажатия кнопки "Печать" диаграммы.
//
Процедура КоманднаяПанельДиаграммыПечать(Кнопка)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ОтчетДиаграмма.ПолучитьДиаграммуНаПечать(ДокументРезультат);
	ДокументРезультат.Показать();
	
КонецПроцедуры

Процедура ДиаграммаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ОтчетДиаграмма.ОбработкаРасшифровкиСтандартногоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект);
	
КонецПроцедуры

// При инициализации формы необходимо заполнить реквизиты и поля основного реквизита формы 
ОбщийОтчет.ЗаполнитьПоляОсновногоРеквизита(ОбщийОтчет);

НеЗаполнятьНастройкиПриОткрытии = Ложь;
ОтборРазвернут = Ложь;
