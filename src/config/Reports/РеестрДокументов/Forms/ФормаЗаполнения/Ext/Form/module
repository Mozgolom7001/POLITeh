////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
Перем мФормаВыбораФильтров;         // Форма выбора фильтра
Перем мФормаВыбораВидаДокумента;    // Форма выбора вида документа
Перем мФормаПодбораЗначенийФильтра; // Форма подбора значений во Множественный отбор
Перем мФормаПодбораВидаДокумента;   // Форма подбора вида документа во Множественный отбор

// Действие, назначаемое в качестве обработчика события НачалоВыбора колонке
// ЗначениеФильтра табличного поля ТаблицаЗначенияФильтров
Перем мДействиеТабличноеПолеЗначенияФильтровЗначениеФильтраНачалоВыбора;
// Имя отчета в метаданных
Перем мИмяОтчета;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

// Заполняет значения элементов формы отчета в соответствии со значениями реквизитов отчета
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьПоРеквизитам()
	
КонецПроцедуры // ЗаполнитьПоРеквизитам()

// Открывает форму выбора вида документов из списка
//
// Параметры:
//  Нет.
//
Процедура ВыборВидаДокумента(Элемент, МножВыбор = Ложь)
	
	Форма = ПолучитьФорму("ФормаВыбора", Элемент, "дляФормаФильтра");

	Если Форма.Открыта() Тогда
		Форма.Активизировать();
		Ответ = Вопрос("Предыдущая операция выбора вида документа не завершена.
			|Завершить?", РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;	

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МножественныйВыбор", МножВыбор);
	СтруктураПараметров.Вставить("ИсходнаяТаблица", "СписокВидовДокументов");

	СтруктураСуществующиеЗначения = Новый Структура;
	СтруктураНеиспользуемыеЗначения = Новый Структура;

	Строка=Отбор.Найти("ДокументОтчета", "ИмяОтбора");

	Если НЕ Строка=Неопределено Тогда
		Если ТипЗнч(Строка.ЗначениеОтбора) = Тип("СписокЗначений") Тогда
			Для каждого Элемент Из Строка.ЗначениеОтбора Цикл

				Если Не ПустаяСтрока(Элемент.Значение) Тогда
					СтруктураНеиспользуемыеЗначения.Вставить(Элемент.Значение,Элемент.Значение);
				КонецЕсли;

			КонецЦикла;
		ИначеЕсли Не ПустаяСтрока(Строка.ЗначениеОтбора) Тогда
			СтруктураНеиспользуемыеЗначения.Вставить(Строка.ЗначениеОтбора, Строка.ЗначениеОтбора);

		КонецЕсли;
	КонецЕсли;

	СтруктураПараметров.Вставить("СтруктураНеиспользуемыеЗначения", СтруктураНеиспользуемыеЗначения);
	СтруктураПараметров.Вставить("СтруктураСуществующиеЗначения", СтруктураСуществующиеЗначения);

	// Передача параметров в форму
	Форма.НачальноеЗначениеВыбора = СтруктураПараметров;
	Форма.РежимВыбора = Истина;
	Форма.Открыть();
	Если МножВыбор Тогда
		мФормаПодбораВидаДокумента = Форма;
	Иначе
		мФормаВыбораВидаДокумента = Форма;
	КонецЕсли;
	
КонецПроцедуры // ВыборВидаДокумента()

// Определяет значение владельца для поля со значением - подчиненным справочником
// по существующим значениям или выбором. Используется для определения владельца 
// полей колонок ЗначениеФильтра табличных полей ТабличноеПолеСписокФильтров и
// ТабличноеПолеЗначенияФильтров.
//
// Параметры: 
//	Нет.
//
// Возвращаемое значение:
//	Владелец справочника, или Неопределено, если владельца нет или не выбран.
//
Функция ОпределитьВладельца()

	Владелец = Неопределено;

	НайдСтрФильтры = мТаблицаФильтры.Найти(ЭлементыФормы.Отбор.ТекущиеДанные.ИмяОтбора, "ИмяПоля");
	Если НайдСтрФильтры <> Неопределено Тогда
		ИмяПоляВладелец = НайдСтрФильтры.ИмяПоляВладелец;
		Если Не ПустаяСтрока(ИмяПоляВладелец) Тогда
			НайдСтр = ЭлементыФормы.Отбор.Значение.Найти(ИмяПоляВладелец, "ИмяОтбора");
			Если НайдСтр <> Неопределено Тогда
				Если ТипЗнч(НайдСтр.ЗначениеФильтра) <> Тип("Строка") Тогда
					Владелец = НайдСтр.ЗначениеФильтра;
				Иначе
					Если НайдСтр.СписокФильтров.Количество() = 1 Тогда
						Владелец = НайдСтр.СписокФильтров[0].ЗначениеФильтра;

					ИначеЕсли НайдСтр.СписокФильтров.Количество() > 0 Тогда
						СписокВладельцев = Новый СписокЗначений;
						Для Каждого Строка Из НайдСтр.СписокФильтров Цикл
							СписокВладельцев.Добавить(Строка.ЗначениеФильтра);
						КонецЦикла;
						Выбор = СписокВладельцев.ВыбратьЭлемент("Выберите владельца");
						Если Выбор <> Неопределено Тогда
							Владелец = Выбор.Значение;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Если Владелец = Неопределено Тогда
				НайдСтр = мТаблицаФильтры.Найти(ИмяПоляВладелец, "ИмяПоля");
				Если НайдСтр <> Неопределено Тогда

					Типы = НайдСтр.ОписаниеТипов.Типы();

					Если Типы.Количество() = 1 Тогда
						ВвестиЗначение(Владелец, "Выберите владельца", Типы[0]);

					Иначе
						СписокВладельцев = Новый СписокЗначений;
						СписокВладельцев.ЗагрузитьЗначения(Типы);
						Выбор = СписокВладельцев.ВыбратьЭлемент("Выберите владельца");
						Если Выбор <> Неопределено Тогда
							ТипВладельца = Выбор.Значение;
							Если ТипВладельца <> Неопределено Тогда
								ВвестиЗначение(Владелец, "Выберите владельца", ТипВладельца);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			// Если это свойства, для них заполнено поле "Свойство"
		ИначеЕсли ЗначениеЗаполнено(НайдСтрФильтры.Свойство) > 0 Тогда

			Владелец = НайдСтрФильтры.Свойство;

		КонецЕсли;
	КонецЕсли;
	
	Возврат Владелец;
	
КонецФункции // ОпределитьВладельца()

// Добавляются фильтры по значениям свойств
//
// Параметры:
//  Запрос - запрос в текст которого добавляются условия и значения фильтров
//  МассивСвойств - массив со значениями фильтра по свойствам
//
//
Процедура ДобавитьФильтрыПоСвойствам(Запрос, МассивСвойств)
	Перем ИмяПоляЗапроса;

	Для Индекс = 0 По МассивСвойств.Количество() - 1 Цикл
		СтрокаОтбора = МассивСвойств[Индекс];

		Если НЕ СтрокаОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицыФильтров = мТаблицаФильтры.Найти(СтрокаОтбора.ИмяОтбора, "ИмяПоля");
		
		Если СтрокаТаблицыФильтров.Свойство = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ИмяПоляЗапроса = СтрокаТаблицыФильтров.ИмяПоляЗапроса;

		Если ТипЗнч(СтрокаОтбора.ЗначениеОтбора) = Тип("СписокЗначений") Тогда
			Массив = СтрокаОтбора.ЗначениеОтбора.ВыгрузитьЗначения();
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, Массив);
			Если Массив.Количество()>0 Тогда
				Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора + "Владелец", СтрокаТаблицыФильтров.Свойство);
			Иначе
				Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора + "Владелец", неопределено);
			КонецЕсли; 
		Иначе
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, СтрокаОтбора.ЗначениеОтбора);
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора + "Владелец", СтрокаТаблицыФильтров.Свойство);
		КонецЕсли;
		
		Если Индекс > 0 Тогда
			Запрос.Текст = Запрос.Текст + ",
				|";
		КонецЕсли;

		Если СтрокаОтбора.ВидСравнения = ВидСравнения.ВСписке
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.ВСпискеПоИерархии
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.Равно Тогда
			
			Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + "
			|	ПО " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Объект = " + ИмяПоляЗапроса + ".Ссылка И
			|	   " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Свойство = &" + СтрокаОтбора.ИмяОтбора + "Владелец И
			|	   " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Значение В (&" + СтрокаОтбора.ИмяОтбора + ")";
			
		ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСписке
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеРавно Тогда

			Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + "
			|	ПО " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Объект = " + ИмяПоляЗапроса + ".Ссылка И
			|	   " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Свойство = &" + СтрокаОтбора.ИмяОтбора + "Владелец И
			|	   " + ИмяПоляЗапроса + СтрокаОтбора.ИмяОтбора + ".Значение НЕ В (&" + СтрокаОтбора.ИмяОтбора + ")";
			
		КонецЕсли;
	КонецЦикла;
		
	Запрос.Текст = Запрос.Текст + "
		|";
		
КонецПроцедуры // ДобавитьФильтрыПоСвойствам()

// Печать реестра документов
//
// Параметры:
//  МассивДокументов - массив, содержащий документы, которые надо распечатать в реестре
//
Процедура ПечатьРеестра(РезультатЗапроса)
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	ВыборкаВалют = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабДок = ЭлементыФормы.ПолеТабличногоДокументаРеестр;
	ТабДок.Очистить();
	Макет  = ПолучитьМакет("РеестрДокументов");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = "Реестр документов";
	ТабДок.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДок.Вывести(ОбластьМакета);

	К=0;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипЗаписи() <> ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
			продолжить;
		КонецЕсли;
		К = К+1;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.Комментарий = СокрП(ОбластьМакета.Параметры.Комментарий);
		ОбластьМакета.Параметры.НомерСтроки     = К;
		
		ТабДок.Вывести(ОбластьМакета);
		
	КонецЦикла;
	ЛинияБезГраниц = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ИтогНадпись = "Итого:";
	Пока ВыборкаВалют.Следующий() Цикл
		Если ВыборкаВалют.ВалютаДокумента = NULL И ВыборкаВалют.СуммаДокумента = NULL
			ИЛИ ВыборкаВалют.ВалютаДокумента = Справочники.Валюты.ПустаяСсылка() И ВыборкаВалют.СуммаДокумента = 0.00 Тогда
			продолжить;
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.ВалютаДокумента = ВыборкаВалют.ВалютаДокумента;
		ОбластьМакета.Параметры.СуммаДокумента  = СокрЛП(Формат(ВыборкаВалют.СуммаДокумента, "ЧЦ=15;ЧДЦ=2"));
		ОбластьМакета.Параметры.Контрагент      = ИтогНадпись;
		Для х = 1 По ОбластьМакета.ШиринаТаблицы Цикл
			ОбластьМакета.Область(1, х, 1, х).Обвести(ЛинияБезГраниц, ЛинияБезГраниц, ЛинияБезГраниц, ЛинияБезГраниц);
		КонецЦикла;
		ТабДок.Вывести(ОбластьМакета);
		ИтогНадпись = "";
	КонецЦикла;
	
	// Зададим параметры макета
	ТабДок.Автомасштаб = Истина;
	ТабДок.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;

	// Первую колонку не печатаем
	ТабДок.ОбластьПечати = ТабДок.Область(1,2,ТабДок.ВысотаТаблицы,ТабДок.ШиринаТаблицы);
	
	ТабДок.ФиксацияСлева  = 4;
	ТабДок.ФиксацияСверху = 3;
	
КонецПроцедуры // ПечатьРеестра()

// Добавить в запрос фильтры по документу
//
// Параметры:
//  Запрос - запрос в который надо добавить фильтры
//  ДокВид - вид документа (строкой)
//  МассивСвойств - массив свойство по которым надо добавить фильтр
//  ФлагЕстьУсловия - флаг, были ли уже добавлены условия в запрос (есть ли уже в запросе секция "ГДЕ")
//
// Возвращаемое значение:
//  Истина - если в запрос были добавлены условия в секцию "ГДЕ"
//  Ложь   - в противном случае
//
Функция ДобавитьФильтрыПоДокументу(Запрос, ДокВид, МассивСвойств, ФлагЕстьУсловия)
	Перем ПервыйРаз;
	Перем ТекСтр;
	Перем Массив;

	ПервыйРаз = Не ФлагЕстьУсловия; // если уже есть категории, слово ГДЕ добавлять не надо.
	Для Каждого СтрокаОтбора Из Отбор Цикл

		Если НЕ СтрокаОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;

		Если СтрокаОтбора.ИмяОтбора = "Контрагент" Тогда
				ИмяПоля = "Контрагент";
		ИначеЕсли СтрокаОтбора.ИмяОтбора = "Склад" Тогда
			Если ОбщегоНазначения.ЕстьРеквизитДокумента("СкладОрдер", Метаданные.Документы[ДокВид]) Тогда
				ИмяПоля = "СкладОрдер";
			Иначе
				ИмяПоля = "Склад";
			КонецЕсли;
		Иначе
			ИмяПоля = СтрокаОтбора.ИмяОтбора;
		КонецЕсли;

		Если СтрокаОтбора.ИмяОтбора = "ДокументОтчета" Тогда
			Продолжить;
		ИначеЕсли Не СтрокаОтбора.ИмяОтбора = "Проведен"
		        И Не СтрокаОтбора.ИмяОтбора = "ПометкаУдаления" Тогда
			Если Не ОбщегоНазначения.ЕстьРеквизитДокумента(ИмяПоля, Метаданные.Документы[ДокВид]) Тогда
				//Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
				//	|ГДЕ ", " И ") + "
				//	| ЛОЖЬ";
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		// Фильтр по свойствам уже должен стоять
		Если Не мТаблицаФильтры.Найти(СтрокаОтбора.ИмяОтбора, "ИмяПоля").Свойство = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если ТипЗнч(СтрокаОтбора.ЗначениеОтбора) = Тип("СписокЗначений") Тогда
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, СтрокаОтбора.ЗначениеОтбора.ВыгрузитьЗначения());

			Если СтрокаОтбора.ВидСравнения = ВидСравнения.ВСпискеПоИерархии Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					| Док." + ИмяПоля + " В ИЕРАРХИИ (&" + СтрокаОтбора.ИмяОтбора + ")";
			ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					|НЕ Док." + ИмяПоля + " В ИЕРАРХИИ (&" + СтрокаОтбора.ИмяОтбора + ")";
			ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.ВСписке Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					| Док." + ИмяПоля + " В (&" + СтрокаОтбора.ИмяОтбора + ")";
			ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСписке Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					|НЕ Док." + ИмяПоля + " В (&" + СтрокаОтбора.ИмяОтбора + ")";
			КонецЕсли;

		Иначе

			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, СтрокаОтбора.ЗначениеОтбора);

			Если СтрокаОтбора.ВидСравнения = ВидСравнения.Равно Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					| Док." + ИмяПоля + " = &" + СтрокаОтбора.ИмяОтбора;
			ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.НеРавно Тогда
				Запрос.Текст = Запрос.Текст + ?(ПервыйРаз, "
					|ГДЕ ", " И ") + "
					|НЕ Док." + ИмяПоля + " = &" + СтрокаОтбора.ИмяОтбора;
			КонецЕсли;
		КонецЕсли;

		ПервыйРаз = Ложь;

	КонецЦикла;

	Возврат Не ПервыйРаз;

КонецФункции // ДобавитьФильтрыПоДокументу()

// Подготовить начало запроса на выборку документов
//
// Параметры
//  Запрос
//
Процедура ФормированиеЗапросаНачало(Запрос)
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтобранныеДокументы.*
	|#ПоместитьВоВременнуюТаблицу#
	|ИЗ(
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.*
	|ИЗ (";
	Если Не ДатаНач = Дата("00010101") Тогда
		Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНач));
	КонецЕсли;
	Если Не ДатаКон = Дата("00010101") Тогда
		Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКон));
	КонецЕсли;
КонецПроцедуры // ФормированиеЗапросаНачало()

// Подготовить окончание запроса на выборку документов
//
// Параметры
//  Запрос
//  ФлагЕстьКатегории
//  СтрокаОтбора
//
Процедура ФормированиеЗапросаОкончание(Запрос, ФлагЕстьКатегории, СтрокаОтбора)
	Запрос.Текст = Запрос.Текст + ") КАК Документы";
	
	Если ФлагЕстьКатегории Тогда
		
		// создадим массив значений фильтра
		Если ТипЗнч(СтрокаОтбора.ЗначениеОтбора) = Тип("СписокЗначений") Тогда
			Массив = СтрокаОтбора.ЗначениеОтбора.ВыгрузитьЗначения();
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, Массив);
		Иначе
			Запрос.УстановитьПараметр(СтрокаОтбора.ИмяОтбора, СтрокаОтбора.ЗначениеОтбора);
		КонецЕсли;
		
		Если СтрокаОтбора.ВидСравнения = ВидСравнения.Равно
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.ВСписке 
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.ВСпискеПоИерархии Тогда
			Запрос.Текст = Запрос.Текст + "
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииОбъектов
			|		ПО (РегистрСведений.КатегорииОбъектов.Объект.Ссылка = Документы.Ссылка)
			|		И(РегистрСведений.КатегорииОбъектов.Категория В (&"+СтрокаОтбора.ИмяОтбора+"))
			|";
			
		ИначеЕсли СтрокаОтбора.ВидСравнения = ВидСравнения.НеРавно
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСписке
			ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии Тогда
			
			Запрос.Текст = Запрос.Текст + "
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииОбъектов
			|		ПО (РегистрСведений.КатегорииОбъектов.Объект.Ссылка = Документы.Ссылка)
			|		И(РегистрСведений.КатегорииОбъектов.Категория В (&"+СтрокаОтбора.ИмяОтбора+"))
			|	ГДЕ
			|		(РегистрСведений.КатегорииОбъектов.ОБЪЕКТ ЕСТЬ NULL)
			|";
			
		КонецЕсли;
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + ") КАК ОтобранныеДокументы
	|УПОРЯДОЧИТЬ ПО Дата, Ссылка";
	
КонецПроцедуры // ФормированиеЗапросаОкончание()

// В текст запроса добавить документ
//
// Параметры
//  Запрос
//  МетаданныеДокумента
//  ВидДокумента
//  МассивСвойств
//  ФлагЕстьУсловия
//
Процедура ФормированиеЗапросаДобавитьДокумент(Запрос, МетаданныеДокумента, ВидДокумента, МассивСвойств, ФлагЕстьУсловия)
	Запрос.Текст = Запрос.Текст + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.Ссылка,
	|	Док.Дата,
	|	Док.Номер,
	|	""" + МетаданныеДокумента.Представление() + """ КАК ВидДокумента,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("ВидОперации", МетаданныеДокумента), "Док.ВидОперации", "NULL") + " КАК ВидОперации,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("Организация", МетаданныеДокумента), "Док.Организация", "NULL") + " КАК Организация,
	|	";
	Если ОбщегоНазначения.ЕстьРеквизитДокумента("СкладОрдер", МетаданныеДокумента) Тогда
		ИмяРеквизитаСклад = "СкладОрдер";
	Иначе
		ИмяРеквизитаСклад = "Склад";
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("Контрагент", МетаданныеДокумента), "Док.Контрагент", "NULL") + " КАК Контрагент,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента(ИмяРеквизитаСклад, МетаданныеДокумента), "Док."+ИмяРеквизитаСклад, "NULL") + " КАК Склад,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("СуммаДокумента", МетаданныеДокумента), "Док.СуммаДокумента", "0.00") + " КАК СуммаДокумента,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("ВалютаДокумента", МетаданныеДокумента), "Док.ВалютаДокумента", "NULL") + " КАК ВалютаДокумента,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("Комментарий", МетаданныеДокумента), "ПОДСТРОКА(Док.Комментарий,1,1000)", "NULL") + " КАК Комментарий,
	|	" + ?(ОбщегоНазначения.ЕстьРеквизитДокумента("Ответственный", МетаданныеДокумента), "Док.Ответственный", "NULL") + " КАК Ответственный
	|ИЗ
	|	Документ." + ВидДокумента + " КАК Док
	|";
	
	ФлагЕстьУсловия = Ложь;
	
	Если МассивСвойств.Количество() > 0 Тогда
		ДобавитьФильтрыПоСвойствам(Запрос, МассивСвойств);
	КонецЕсли;
	
	ЕстьФильтры  = ДобавитьФильтрыПоДокументу(Запрос, ВидДокумента, МассивСвойств, ФлагЕстьУсловия);
	
	Если Не ДатаНач = '00010101000000' Тогда
		Запрос.Текст = Запрос.Текст + ?(ЕстьФильтры, " И ", " ГДЕ ") + "
		|Док.Дата >= &ДатаНач
		|";
		ЕстьФильтры = Истина;
	КонецЕсли;
	Если Не ДатаКон = '00010101000000' Тогда
		Запрос.Текст = Запрос.Текст + ?(ЕстьФильтры, " И ", " ГДЕ ") + "
		|Док.Дата <= &ДатаКон
		|";
		ЕстьФильтры = Истина;
	КонецЕсли;
	

КонецПроцедуры // ФормированиеЗапросаДобавитьДокумент()

// Проверить, нужно ли исключить документ в тексте запроса
//
// Параметры
//  МетаданныеДокумента
//
// Возвращаемое значение:
//   Булево   – Истина - документ нужно исключить, Ложь - нет
//
Функция ИсключитьДокумент(МетаданныеДокумента)
	Для Каждого СтрокаОтбораДляПеребора Из Отбор Цикл
		
		Если НЕ СтрокаОтбораДляПеребора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ СтрокаОтбораДляПеребора.ИмяОтбора = "ДокументОтчета"
			И Не СтрокаОтбораДляПеребора.ИмяОтбора = "Проведен"
			И Не СтрокаОтбораДляПеребора.ИмяОтбора = "ПометкаУдаления"
			И НЕ СтрокаОтбораДляПеребора.ИмяОтбора = "Категория"
			И Найти(СтрокаОтбораДляПеребора.ПредставлениеОтбора, "(свойство документов)") = 0 Тогда
			Если СтрокаОтбораДляПеребора.ИмяОтбора = "Склад" Тогда
				Если ОбщегоНазначения.ЕстьРеквизитДокумента("СкладОрдер", МетаданныеДокумента) Тогда
					ИмяРеквизита = "СкладОрдер";
				Иначе
					ИмяРеквизита = "Склад";
				КонецЕсли;
			Иначе
				ИмяРеквизита = СтрокаОтбораДляПеребора.ИмяОтбора;
			КонецЕсли;
			Если НЕ ОбщегоНазначения.ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции // ИсключитьДокумент()

// Добавляет в запрос необходимые виды документов
//
// Параметры:
//  Запрос - запрос в который добавляется указанный	вид документа
//  МассивВидовДокументов - массив содержащий виды документов (строкой) которые надо добавить в запрос
//  МассивСвойств - массив свойств документов по которым надо установить фильтр
//  СтрКатегорий - строка таблицы фильтров в которой указаны фильтры по категории документов
//
Функция ДобавитьДокументВЗапрос(масЗапросов, МассивВидовДокументов, МассивСвойств, СтрокаОтбора)
	Перем ЕстьФильтры;
	Перем ФлагЕстьУсловия;

	ФлагЕстьКатегории = Не СтрокаОтбора = Неопределено;
	
	текНомерТаблицы = 0;
	текЗапрос       = Неопределено;
	максТаблиц      = 1;
	
	К1 = -1;
	ДобавлятьОкончаниеТекстаЗапроса = Ложь;
	
	Для Каждого ВидДокумента Из МассивВидовДокументов Цикл
		//Здесь мы должны понять, не установлен ли фильтр по какому-либо реквизиту, которого нет у документа
		//указанного вида. Если установлен, то мы просто не включаем документ в запрос, исключая его из отчета
		
		
		МетаданныеДокумента = Метаданные.Документы[ВидДокумента];
		
		Если ИсключитьДокумент(МетаданныеДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		Если текНомерТаблицы = 0 Тогда
			текЗапрос       = Новый Запрос;
			масЗапросов.Добавить(текЗапрос);
			ФормированиеЗапросаНачало(текЗапрос);
			ДобавлятьОкончаниеТекстаЗапроса = Истина;
		Иначе
			текЗапрос.Текст = текЗапрос.Текст + "
				|ОБЪЕДИНИТЬ ВСЕ";
		КонецЕсли;
		текНомерТаблицы = текНомерТаблицы + 1;
		
		ФормированиеЗапросаДобавитьДокумент(текЗапрос, МетаданныеДокумента, ВидДокумента, МассивСвойств, ФлагЕстьУсловия);
		
		Если текНомерТаблицы = максТаблиц Тогда
			текНомерТаблицы = 0;
			ФормированиеЗапросаОкончание(текЗапрос, ФлагЕстьКатегории, СтрокаОтбора);
			ДобавлятьОкончаниеТекстаЗапроса = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	
	Если текЗапрос = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДобавлятьОкончаниеТекстаЗапроса Тогда
		ФормированиеЗапросаОкончание(текЗапрос, ФлагЕстьКатегории, СтрокаОтбора);
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // ДобавитьДокументВЗапрос()

// Обработка выбранных документов
//
// Параметры:
//  Нет.
//
Процедура ВидОбработкиДокументы() Экспорт
	Перем МассивСвойств;
	Перем МассивВидовДокументов;
	Перем Индекс;
	Перем СтрФильтров;
	Перем СтрКатегорий;

	МассивСвойств = Новый Массив;
	СтрКатегорий  = Неопределено;

	Для Каждого К Из Отбор Цикл

		Если НЕ К.Использование Тогда
			Продолжить;
		КонецЕсли;

		СтрФильтров = мТаблицаФильтры.Найти(К.ИмяОтбора, "ИмяПоля");

		// На случай если не выбран фильтр
		Если СтрФильтров = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если Не СтрФильтров.Свойство = Неопределено Тогда
			МассивСвойств.Добавить(К);
		КонецЕсли;
		Если К.ИмяОтбора = "Категория" Тогда
			СтрКатегорий = К;
		КонецЕсли;

		// Проверим на заполненность отбора по виду документа
		Если К.ИмяОтбора = "ДокументОтчета" Тогда
			Если ТипЗнч(К.ЗначениеОтбора) = Тип("СписокЗначений") 
				И К.ЗначениеОтбора.Количество()=0 Тогда
				Предупреждение("Надо обязательно указать значение отбора для отбора по виду документа!", 60);
				Возврат;
			Иначе
				Если НЕ ЗначениеЗаполнено(К.ЗначениеОтбора) Тогда
					Предупреждение("Надо обязательно указать значение отбора для отбора по виду документа!", 60);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	МассивВидовДокументов = Новый Массив;

	Индекс = 0;
	СтрокаОтбора = Отбор.Найти("ДокументОтчета", "ИмяОтбора");

	Если СтрокаОтбора = Неопределено Тогда
		Для Каждого К Из мСписокВидовДокументов Цикл
			МассивВидовДокументов.Добавить(К.Значение);
		КонецЦикла;
	Иначе
		Если СтрокаОтбора.Использование Тогда
			Если СтрокаОтбора.ВидСравнения = ВидСравнения.НеРавно
				ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеВСписке Тогда

				СписокИсключений = Новый СписокЗначений;
				Если ТипЗнч(СтрокаОтбора.ЗначениеОтбора) <> Тип("СписокЗначений") Тогда
					СписокИсключений.Добавить(СтрокаОтбора.ЗначениеОтбора);
				Иначе
					Для Каждого К Из СтрокаОтбора.ЗначениеОтбора Цикл
						СписокИсключений.Добавить(К.Значение);
					КонецЦикла;
				КонецЕсли;
				Для Каждого К Из мСписокВидовДокументов Цикл
					Если СписокИсключений.НайтиПоЗначению(К.Значение) = Неопределено Тогда
						МассивВидовДокументов.Добавить(К.Значение);
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ТипЗнч(СтрокаОтбора.ЗначениеОтбора) <> Тип("СписокЗначений") Тогда
					МассивВидовДокументов.Добавить(СтрокаОтбора.ЗначениеОтбора);
				Иначе
					Для Каждого К Из СтрокаОтбора.ЗначениеОтбора Цикл
						МассивВидовДокументов.Добавить(К.Значение);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Для Каждого К Из мСписокВидовДокументов Цикл
				МассивВидовДокументов.Добавить(К.Значение);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	масЗапросов = Новый Массив;
	Если НЕ ДобавитьДокументВЗапрос(масЗапросов, МассивВидовДокументов, МассивСвойств, СтрКатегорий) Тогда
		Предупреждение("Установлены фильтры по реквизитам, которых нет в выбранных видах документов!
		|Отчет не может быть сформирован.");
		возврат;
	КонецЕсли;
	
	Колво = масЗапросов.Количество();
	НомерВыборкаДокументов = 1;
	НомерВыборкаВалюты     = 2;
	Если Колво = 1 Тогда
		// РЕЗУЛЬТАТ ПОЛУЧАЕТСЯ ОДНИМ ЗАПРОСОМ
		Запрос = масЗапросов.Получить(0);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, Символы.ПС + "#ПоместитьВоВременнуюТаблицу#", "");
		Запрос.Текст = Запрос.Текст + "
		|ИТОГИ
		|	СУММА(СуммаДокумента) КАК СуммаДокумента
		|ПО
		|	ВалютаДокумента КАК ВалютаДокумента
		|";
	Иначе
		// НУЖНО ДЕЛАТЬ НЕСКОЛЬКО ЗАПРОСОВ
		масИменТаблиц = Новый Массив;
		Менеджер = Новый МенеджерВременныхТаблиц;
		
		Для Сч=1 По Колво Цикл
			Запрос = масЗапросов.Получить(Сч-1);
			
			ИмяТаблицы = "ВременнаяТаблица" + Формат(Сч, "ЧГ=0");
			
			ТекстЗапроса = Запрос.Текст;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Символы.ПС + "#ПоместитьВоВременнуюТаблицу#", Символы.ПС + "Поместить " + ИмяТаблицы);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Символы.ПС + "УПОРЯДОЧИТЬ ПО Дата, Ссылка",   Символы.ПС + "ИНДЕКСИРОВАТЬ ПО Дата, Ссылка");
			Запрос.Текст = ТекстЗапроса;
			
			масИменТаблиц.Добавить(ИмяТаблицы);
			Запрос.МенеджерВременныхТаблиц = Менеджер;
			Запрос.Выполнить();
		КонецЦикла;
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОтобранныеДокументы.*
		|ИЗ (";
		
		Сч = 0;
		Для Каждого ИмяТаблицы Из масИменТаблиц Цикл
			ТекстЗапроса = ТекстЗапроса + ?(Сч=0, "", "
			|ОБЪЕДИНИТЬ ВСЕ") + "
			|ВЫБРАТЬ " + ИмяТаблицы + ".* ИЗ " + ИмяТаблицы;
			Сч = Сч + 1;
		КонецЦикла;
		
		ТекстЗапроса = ТекстЗапроса + ") КАК ОтобранныеДокументы
		|УПОРЯДОЧИТЬ ПО Дата, Ссылка
		|ИТОГИ
		|	СУММА(СуммаДокумента) КАК СуммаДокумента
		|ПО
		|	ВалютаДокумента КАК ВалютаДокумента
		|";
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();

	ПечатьРеестра(РезультатЗапроса);
КонецПроцедуры // ВидОбработкиДокументы()

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Состояние("Заполнение по умолчанию");

	ЗаполнитьНачальныеНастройки(Истина);
	ЗаполнитьПоРеквизитам();
	
	// Установим дату начала отчета
	Если ЗначениеЗаполнено(глЗначениеПеременной("глТекущийПользователь")) Тогда
		ДатаНач = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),"ОсновнаяДатаНачалаОтчетов");
	КонецЕсли;

КонецПроцедуры // ПередОткрытием()

// Обработчик события ПередУдалением элемента формы Отбор.
//
Процедура ОтборПередУдалением(Элемент, Отказ)

	Если Элемент.ТекущиеДанные <> Неопределено И мСписокОбязательныхОтборов.НайтиПоЗначению(Элемент.ТекущиеДанные.ИмяОтбора) <> Неопределено Тогда

		Отказ = Истина;

	КонецЕсли;

КонецПроцедуры

// Обработчик события ПередНачаломИзменения элемента формы Отбор.
//
Процедура ОтборПередНачаломИзменения(Элемент, Отказ)

	Если Элемент.ТекущиеДанные <> Неопределено
		И Элемент.ТекущаяКолонка.Имя = "Поле"
		И НЕ ПустаяСтрока(СокрЛП(Элемент.ТекущиеДанные.ИмяОтбора))
		И мСписокОбязательныхОтборов.НайтиПоЗначению(СокрЛП(Элемент.ТекущиеДанные.ИмяОтбора)) <> Неопределено Тогда

		Отказ = Истина;

	КонецЕсли; 

КонецПроцедуры

// Обработчик события ПередНачаломДобавления элемента формы Отбор.
//
Процедура ОтборПередНачаломДобавления(Элемент, Отказ, Копирование)

	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли; 

КонецПроцедуры

// Обработчик события ПриОкончанииРедактирования элемента формы Отбор.
//
Процедура ОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущиеДанные.ЗначениеОтбора = Неопределено Тогда

		Если Элемент.ТекущиеДанные.ВидСравнения = ВидСравнения.Равно ИЛИ Элемент.ТекущиеДанные.ВидСравнения = ВидСравнения.НеРавно Тогда
			Если ТипЗнч(Элемент.ТекущиеДанные.ОписаниеТиповЗначенийОтбора) = Тип("ОписаниеТипов") Тогда
				Элемент.ТекущиеДанные.ЗначениеОтбора = ОпределитьПустоеЗначениеТипа(Элемент.ТекущиеДанные.ОписаниеТиповЗначенийОтбора);
			КонецЕсли; 
		Иначе
			Элемент.ТекущиеДанные.ЗначениеОтбора = Новый СписокЗначений;
		КонецЕсли; 

	КонецЕсли; 

КонецПроцедуры

// Обработчик события НачалоВыбора элемента формы Отбор.Поле.
//
Процедура ОтборПолеНачалоВыбора(Элемент, СтандартнаяОбработка)

	Форма = ПолучитьФорму("ФормаВыбора", Элемент, "дляФормаФильтра");

	Если Форма.Открыта() Тогда
		Форма.Активизировать();
		Ответ = Вопрос("Предыдущая операция выбора фильтра не завершена.
			|Завершить?",РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МножественныйВыбор", Ложь);
	СтруктураПараметров.Вставить("ИсходнаяТаблица", "СписокФильтров");

	СтруктураСуществующиеЗначения = Новый Структура;
	СтруктураНеиспользуемыеЗначения = Новый Структура;

	Для Каждого Строка Из Отбор Цикл

		Если ПустаяСтрока(Строка.ИмяОтбора) Тогда
			Продолжить;
		КонецЕсли;

		// Кроме этой строки
		Если Отбор.Индекс(Строка) <> Отбор.Индекс(ЭлементыФормы.Отбор.ТекущаяСтрока) Тогда
			СтруктураНеиспользуемыеЗначения.Вставить(Строка.ИмяОтбора);
		КонецЕсли;

	КонецЦикла;
	
	Если Не ПустаяСтрока(ЭлементыФормы.Отбор.ТекущиеДанные.ИмяОтбора) Тогда
		СтруктураСуществующиеЗначения.Вставить(ЭлементыФормы.Отбор.ТекущиеДанные.ИмяОтбора);
	КонецЕсли;

	СтруктураПараметров.Вставить("СтруктураНеиспользуемыеЗначения", СтруктураНеиспользуемыеЗначения);
	СтруктураПараметров.Вставить("СтруктураСуществующиеЗначения", СтруктураСуществующиеЗначения);

	// Передача параметров в форму
	Форма.НачальноеЗначениеВыбора = СтруктураПараметров;
	Форма.РежимВыбора = Истина;
	Форма.Открыть();
	мФормаВыбораФильтров = Форма;

КонецПроцедуры

// Обработчик события ОбработкаВыбора элемента формы Отбор.Поле.
//
Процедура ОтборПолеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ТабличноеПоле = ЭлементыФормы.Отбор;

	Если ТабличноеПоле.ТекущаяСтрока  = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РедактируемаяСтрока = ТабличноеПоле.Значение[ТабличноеПоле.Значение.Индекс(ТабличноеПоле.ТекущаяСтрока)];

	Для Каждого Строка Из ВыбранноеЗначение Цикл 

		РедактируемаяСтрока.ИмяОтбора           = Строка.ИмяФильтра;
		РедактируемаяСтрока.ПредставлениеОтбора = Строка.ПредставлениеФильтра;
		РедактируемаяСтрока.ОписаниеТиповЗначенийОтбора = Строка.ОписаниеТипов;

		МассивТипов = Строка.ОписаниеТипов.Типы();

		// Если тип единичный, то присвоим пустое значение этого типа
		Если МассивТипов.Количество() = 1 Тогда
			РедактируемаяСтрока.ЗначениеОтбора = ОбщегоНазначения.ПустоеЗначениеТипа(МассивТипов[0]);
		Иначе
			РедактируемаяСтрока.ЗначениеОтбора = Неопределено;
		КонецЕсли;

		РедактируемаяСтрока.Использование = Истина;
		РедактируемаяСтрока.ВидСравнения = ВидСравнения.Равно;

	КонецЦикла;

КонецПроцедуры

// Обработчик события НачалоВыбораИзСписка элемента формы Отбор.ВидСравнения.
//
Процедура ОтборВидСравненияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	СписокВидовСравнения = Новый СписокЗначений;
	Если СтрЧислоВхождений(ЭлементыФормы.Отбор.ТекущаяСтрока.ИмяОтбора, "Категор") Тогда
		СписокВидовСравнения.Добавить(ВидСравнения.Равно);
		СписокВидовСравнения.Добавить(ВидСравнения.НеРавно);
		СписокВидовСравнения.Добавить(ВидСравнения.ВСписке);
		СписокВидовСравнения.Добавить(ВидСравнения.НеВСписке);
	ИначеЕсли ЭлементыФормы.Отбор.ТекущаяСтрока.ИмяОтбора = "ДокументОтчета"  Тогда
		СписокВидовСравнения.Добавить(ВидСравнения.Равно);
		СписокВидовСравнения.Добавить(ВидСравнения.НеРавно);
		СписокВидовСравнения.Добавить(ВидСравнения.ВСписке);
		СписокВидовСравнения.Добавить(ВидСравнения.НеВСписке);
	ИначеЕсли ТипЗнч(ЭлементыФормы.Отбор.ТекущаяСтрока.ОписаниеТиповЗначенийОтбора) = Тип("ОписаниеТипов")
		И ЭлементыФормы.Отбор.ТекущаяСтрока.ОписаниеТиповЗначенийОтбора.СодержитТип(Тип("Булево")) Тогда
		СписокВидовСравнения.Добавить(ВидСравнения.Равно);
	Иначе
		СписокВидовСравнения.Добавить(ВидСравнения.Равно);
		СписокВидовСравнения.Добавить(ВидСравнения.НеРавно);
		СписокВидовСравнения.Добавить(ВидСравнения.ВСписке);
		СписокВидовСравнения.Добавить(ВидСравнения.ВСпискеПоИерархии);
		СписокВидовСравнения.Добавить(ВидСравнения.НеВСписке);
		СписокВидовСравнения.Добавить(ВидСравнения.НеВСпискеПоИерархии);
	КонецЕсли;

	Элемент.СписокВыбора = СписокВидовСравнения;

КонецПроцедуры

// Обработчик события ОбработкаВыбора элемента формы Отбор.ВидСравнения.
//
Процедура ОтборВидСравненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	ЗначениеОтбораТекущейСтроки = ЭлементыФормы.Отбор.ТекущаяСтрока.ЗначениеОтбора;

	Если ВыбранноеЗначение = ВидСравнения.Равно 
		ИЛИ ВыбранноеЗначение = ВидСравнения.НеРавно Тогда

		Если ТипЗнч(ЗначениеОтбораТекущейСтроки) = Тип("СписокЗначений") Тогда

			Если ЗначениеОтбораТекущейСтроки.Количество() > 0 тогда
				ЗначениеОтбораТекущейСтроки = ЗначениеОтбораТекущейСтроки[0].Значение;
			КонецЕсли;

		КонецЕсли;

	Иначе

		Если ТипЗнч(ЗначениеОтбораТекущейСтроки) <> Тип("СписокЗначений") Тогда

			СтароеЗначение = ЗначениеОтбораТекущейСтроки;

			ЭлементыФормы.Отбор.ТекущаяСтрока.ЗначениеОтбора = Новый СписокЗначений;
			Если СтароеЗначение <> Неопределено Тогда
				ЭлементыФормы.Отбор.ТекущаяСтрока.ЗначениеОтбора.Добавить(СтароеЗначение);
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Обработчик события НачалоВыбора элемента формы Отбор.Значение.
//
Процедура ОтборЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)

	Если ПустаяСтрока(ЭлементыФормы.Отбор.ТекущаяСтрока.ПредставлениеОтбора) Тогда
		Предупреждение("Выберите отбор!",60);
		СтандартнаяОбработка=Ложь;
		Возврат;
	КонецЕсли;

	Фильтр = ЭлементыФормы.Отбор.ТекущаяСтрока;

	Владелец = ОпределитьВладельца();

	Если Фильтр.ОписаниеТиповЗначенийОтбора = Неопределено Тогда

		СтрокаГруппировки = мТаблицаФильтры.Найти(Фильтр.ИмяОтбора, "ИмяПоля");
		Если СтрокаГруппировки <> Неопределено Тогда
			Фильтр.ОписаниеТиповЗначенийОтбора = СтрокаГруппировки.ОписаниеТипов;
		КонецЕсли; 

	КонецЕсли; 

	ТипыФильтра = Фильтр.ОписаниеТиповЗначенийОтбора;

	МассивТипов = ТипыФильтра.Типы();

	Если ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.Равно
		ИЛИ ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.НеРавно Тогда

		МассивТипов = ТипыФильтра.Типы();

		Если МассивТипов.Количество() = 1 Тогда

			Если МассивТипов[0]=Тип("Число") Тогда
				Элемент.Значение=0;
			ИначеЕсли МассивТипов[0]=Тип("Строка") Тогда
				Если Фильтр.ИмяОтбора = "ДокументОтчета" Тогда
					ВыборВидаДокумента(Элемент);
					СтандартнаяОбработка = Ложь;
					Возврат;
				Иначе
					Элемент.Значение = "";
				КонецЕсли;
			ИначеЕсли МассивТипов[0]=Тип("Дата") Тогда
				Элемент.Значение=ТекущаяДата();
			ИначеЕсли МассивТипов[0]=Тип("Булево") Тогда
				Элемент.Значение=Истина;
			Иначе
				Элемент.Значение = Новый(МассивТипов[0]);
			КонецЕсли;

			Элемент.ВыбиратьТип = Ложь;

		Иначе

			Элемент.ОграничениеТипа = Фильтр.ОписаниеТиповЗначенийОтбора;
			Элемент.ВыбиратьТип = Истина;

		КонецЕсли;

		Если МассивТипов[0]=Тип("СправочникСсылка.КатегорииОбъектов") Тогда

			СтандартнаяОбработка=Ложь;

			СтрокаФильтры=мТаблицаФильтры.Найти(ЭлементыФормы.Отбор.ТекущаяСтрока.ИмяОтбора,
			"ИмяПоля");

			ФормаВыбораКатегории= Справочники["КатегорииОбъектов"].ПолучитьФормуВыбора(, Элемент,);
			ФормаВыбораКатегории.РежимВыбора = Истина;
			ФормаВыбораКатегории.Отбор.НазначениеКатегории.Значение      = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Документы;
			ФормаВыбораКатегории.Отбор.НазначениеКатегории.Использование = Истина;

			ФормаВыбораКатегории.ПараметрВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
			ФормаВыбораКатегории.Открыть();

		КонецЕсли;

	Иначе

		Если ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.ВСписке
			ИЛИ ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.ВСпискеПоИерархии
			ИЛИ ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.НеВСписке
			ИЛИ ЭлементыФормы.Отбор.ТекущаяСтрока.ВидСравнения = ВидСравнения.ВСпискеПоИерархии Тогда

			Если Фильтр.ИмяОтбора = "ДокументОтчета" Тогда

				ВыборВидаДокумента(Элемент, Истина);
				СтандартнаяОбработка = Ложь;
				Возврат;
			КонецЕсли;
		КонецЕсли;

		Массив = Новый Массив;
		Массив.Добавить("СписокЗначений");
		ОписаниеТиповСписокЗначений  = Новый ОписаниеТипов(Массив);
		Элемент.ОграничениеТипа      = ОписаниеТиповСписокЗначений;
		Элемент.Значение.ТипЗначения = Фильтр.ОписаниеТиповЗначенийОтбора;
		Элемент.ВыбиратьТип          = Ложь;

	КонецЕсли;

	Если Владелец <> Неопределено Тогда
		Элемент.ВыборПоВладельцу = Владелец;
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы Отбор.Значение.
//
Процедура ОтборЗначениеПриИзменении(Элемент)

	Если Элемент.Значение <> Неопределено И ЭлементыФормы.Отбор.ТекущиеДанные <> Неопределено Тогда

		Если (ТипЗнч(Элемент.Значение) = Тип("СписокЗначений") И Элемент.Значение.Количество() > 0)
		 ИЛИ (Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) И НЕ Элемент.Значение.Пустая())
		 ИЛИ (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) И НЕ Элемент.Значение.Пустая()) Тогда

			ЭлементыФормы.Отбор.ТекущиеДанные.Использование = Истина;

		КонецЕсли; 

	КонецЕсли; 

КонецПроцедуры

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьОтчет() Экспорт

	Если ДатаНач > ДатаКон И ДатаКон <> '00010101000000' Тогда
		Предупреждение("Дата начала периода не может быть больше даты конца периода", 60);
		Возврат;
	КонецЕсли; 

	ВидОбработкиДокументы();
	ЭлементыФормы.ПанельОтчета.ТекущаяСтраница = ЭлементыФормы.ПанельОтчета.Страницы["Реестр"];

КонецПроцедуры // ОбновитьОтчет()

// процедура - обработчик нажатия на кнопку выполнить
//
//
Процедура КнопкаСформироватьНажатие(Элемент)

	ОбновитьОтчет();

КонецПроцедуры // КнопкаВыполнитьНажатие()

// процедура - обработчик нажатия на кнопку выбор периода
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	НП = Новый НастройкаПериода;
	НП.ДатаНачала    = ДатаНач;
	НП.ДатаОкончания = ДатаКон;
	Если НП.Редактировать() Тогда
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

// Процедура - обработчик события "При открытии" формы отчета.
//
Процедура ПриОткрытии()
	Перем НоваяКолонка;

	Если ЗначениеЗаполнено(НачальноеЗначениеВыбора) Тогда

		мНастройка = НачальноеЗначениеВыбора;

	КонецЕсли;

	Если Не ВладелецФормы = Неопределено Тогда

		ЗаполнитьРеквизитыПоДиалогу();

		КнопкаСформироватьНажатие(ЭлементыФормы.КнопкаСформировать);
		ЭлементыФормы.ПанельОтчета.ТекущаяСтраница = ЭлементыФормы.ПанельОтчета.Страницы["Таблица"];

	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ СОХРАНЕНИЯ/ВОССТАНОВЛЕНИЯ НАСТРОЕК ОТЧЕТА

// Перенос настройки, заданной значениями элементов формы отчета, в реквизиты отчета.
//
// Параметры: 
//  Нет.
//
Процедура ЗаполнитьРеквизитыПоДиалогу()

	// Добавим отборы
	Таблица = ЭлементыФормы.ТабличноеПолеСписокФильтров.Значение;
	ФильтрыОтчета.Очистить();
	Для Каждого СтрокаТаблицы Из Таблица Цикл

		Если СтрокаТаблицы.СписокФильтров.Количество() = 0 Тогда
			НоваяСтрока = ФильтрыОтчета.Добавить();
			НоваяСтрока.ИмяФильтра            = СтрокаТаблицы.ИмяФильтра;
			НоваяСтрока.ПредставлениеФильтра  = СтрокаТаблицы.ПредставлениеФильтра;
			НоваяСтрока.ЗначениеФильтра       = СтрокаТаблицы.ЗначениеФильтра;
			НоваяСтрока.ОписаниеФильтра       = СтрокаТаблицы.ОписаниеФильтра;
			НоваяСтрока.ТипФильтра            = СтрокаТаблицы.ТипФильтра;
			НоваяСтрока.ПредставлениеЗначения = СтрокаТаблицы.ПредставлениеЗначения;
		Иначе

			Для Каждого Фильтр Из СтрокаТаблицы.СписокФильтров Цикл

				НоваяСтрока = ФильтрыОтчета.Добавить();
				НоваяСтрока.ИмяФильтра            = СтрокаТаблицы.ИмяФильтра;
				НоваяСтрока.ПредставлениеФильтра  = СтрокаТаблицы.ПредставлениеФильтра;
				НоваяСтрока.ЗначениеФильтра       = Фильтр.ЗначениеФильтра;
				НоваяСтрока.ОписаниеФильтра       = СтрокаТаблицы.ОписаниеФильтра;
				НоваяСтрока.ТипФильтра            = СтрокаТаблицы.ТипФильтра;
				НоваяСтрока.ПредставлениеЗначения = Фильтр.ПредставлениеЗначения;

			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьРеквизитыПоДиалогу()

Процедура ОтборЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	РедактируемаяСтрока = Отбор[Отбор.Индекс(ЭлементыФормы.Отбор.ТекущаяСтрока)];

	Если РедактируемаяСтрока.ИмяОтбора = "ДокументОтчета" Тогда

		СтандартнаяОбработка = Ложь;

		Если РедактируемаяСтрока.ВидСравнения = ВидСравнения.Равно
			ИЛИ РедактируемаяСтрока.ВидСравнения = ВидСравнения.НеРавно Тогда

			Для Каждого Строка Из ВыбранноеЗначение Цикл // должно содержать не более одной строки

				РедактируемаяСтрока.ЗначениеОтбора       = Строка.ИмяФильтра;
				//РедактируемаяСтрока.ПредставлениеОтбора = Строка.ПредставлениеФильтра;

			КонецЦикла;

		ИначеЕсли РедактируемаяСтрока.ВидСравнения = ВидСравнения.ВСписке
			ИЛИ РедактируемаяСтрока.ВидСравнения = ВидСравнения.НеВСписке Тогда

			РедактируемаяСтрока.ЗначениеОтбора = Новый СписокЗначений;

			Для Каждого Строка Из ВыбранноеЗначение Цикл // должно содержать не более одной строки

				РедактируемаяСтрока.ЗначениеОтбора.Добавить(Строка.ИмяФильтра, Строка.ПредставлениеФильтра);

			КонецЦикла;

		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОтборПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки.ИмяОтбора = "ДокументОтчета" 
		И (ДанныеСтроки.ВидСравнения = ВидСравнения.Равно
		ИЛИ ДанныеСтроки.ВидСравнения = ВидСравнения.НеРавно) Тогда

		МетаданныеДокумент = Метаданные.Документы.Найти(ДанныеСтроки.ЗначениеОтбора);
		Если МетаданныеДокумент<>Неопределено Тогда
			ОформлениеСтроки.Ячейки.Значение.Текст =  МетаданныеДокумент.Представление();
		Иначе
			ОформлениеСтроки.Ячейки.Значение.Текст =  "<>";
		КонецЕсли; 

	КонецЕсли;

КонецПроцедуры

Процедура ПередСохранениемЗначений(Отказ)

	СохраненныеНастройки = Новый Структура;
	СохраненныеНастройки.Вставить("Отбор", Отбор.Скопировать());

	// Остальные реквизиты отчета сохраняются стандартно
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()

	Отбор.Очистить();

	Для Каждого Строка Из СохраненныеНастройки.Отбор Цикл
		НоваяСтрока = Отбор.Добавить();
		Для Каждого Кол Из Отбор.Колонки Цикл
			НоваяСтрока[Отбор.Колонки.Индекс(Кол)] = Строка[Отбор.Колонки.Индекс(Кол)];
		КонецЦикла; 
	КонецЦикла;

	// Остальные реквизиты отчета сохраняются стандартно
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

// Переменные для сохранения/восстановления настроек
мИмяОтчета = "РеестрДокументов";

МассивТаблицаЗначений = Новый Массив;
МассивТаблицаЗначений.Добавить(Тип("ТаблицаЗначений"));

ОписаниеТиповТаблицаЗначений = Новый ОписаниеТипов(МассивТаблицаЗначений);
Отбор.Колонки.Добавить("ИмяОтбора");
Отбор.Колонки.Добавить("ОписаниеТиповЗначенийОтбора");
Отбор.Индексы.Добавить("ИмяОтбора");
