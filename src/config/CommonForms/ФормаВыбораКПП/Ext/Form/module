
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Если Контрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ТекстСообщения = "КПП для физических лиц не задается";
		ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораКППКонтрагента(ЭлементыФормы.КППКонтрагента.СписокВыбора, Контрагент);
	Если ПустаяСтрока(КППКонтрагента) И ЭлементыФормы.КППКонтрагента.СписокВыбора.Количество() > 0 Тогда
		КППКонтрагента = ЭлементыФормы.КППКонтрагента.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если РольКонтрагента = "Поставщик" Тогда
		Заголовок	= НСтр("ru = 'КПП поставщика'");
		ЭлементыФормы.СтраницыПодсказки.ТекущаяСтраница	= ЭлементыФормы.СтраницыПодсказки.Страницы.ПодсказкаПоставщика;
	ИначеЕсли РольКонтрагента = "Покупатель" Тогда
		Заголовок	= НСтр("ru = 'КПП покупателя'");
		ЭлементыФормы.СтраницыПодсказки.ТекущаяСтраница	= ЭлементыФормы.СтраницыПодсказки.Страницы.ПодсказкаПокупателя;
	Иначе
		Заголовок	= НСтр("ru = 'КПП контрагента'");
		ЭлементыФормы.СтраницыПодсказки.ТекущаяСтраница	= ЭлементыФормы.СтраницыПодсказки.Страницы.ПодсказкаПустая;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос(НСтр("ru = 'Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			СохранитьИзменения();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Процедура - обработчик события "Нажатие" кнопки ОК в форме.
//
Процедура КнопкаОКНажатие(Элемент)

	СохранитьИзменения();

КонецПроцедуры // КнопкаОКНажатие()


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбора, ГоловнойКонтрагент)
	 
	 Запрос = Новый Запрос;
	 Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	 Запрос.УстановитьПараметр("ИНН", ГоловнойКонтрагент.ИНН);
	 
	 // Первый элемент - всегда КПП головного контрагента, даже, если не задан
	 Запрос.Текст = 
	 "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 |	Контрагенты.КПП КАК КПП,
	 |	Контрагенты.Представление КАК ПредставлениеКонтрагента
	 |ИЗ
	 |	Справочник.Контрагенты КАК Контрагенты
	 |ГДЕ
	 |	Контрагенты.Ссылка = &ГоловнойКонтрагент
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	Контрагенты.КПП,
	 |	Контрагенты.Представление
	 |ИЗ
	 |	Справочник.Контрагенты КАК Контрагенты
	 |ГДЕ
	 |	НЕ Контрагенты.Ссылка = &ГоловнойКонтрагент
	 |	И Контрагенты.ОбособленноеПодразделение
	 |	И Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
	 |	И Контрагенты.ИНН = &ИНН
	 |	И НЕ Контрагенты.ПометкаУдаления
	 |	И НЕ Контрагенты.КПП ПОДОБНО """"";
	 
	СписокВыбора.Очистить();
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПредставлениеКПП	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 (%2)", ?(ПустаяСтрока(Выборка.КПП), "<не задан>", Выборка.КПП), Выборка.ПредставлениеКонтрагента);
		СписокВыбора.Добавить(Выборка.КПП, ПредставлениеКПП);
	КонецЦикла;
	
КонецПроцедуры

Процедура СохранитьИзменения()
	
	Если ПроверитьЗаполнение() Тогда
		
		Модифицированность = Ложь;
		ЗначениеВыбора = Новый Структура("КППКонтрагента", КППКонтрагента);
		ОповеститьОВыборе(ЗначениеВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

