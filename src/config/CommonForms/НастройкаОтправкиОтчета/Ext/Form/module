
Процедура ПриОткрытии()
	
	ВосстановитьЗначения();
	
КонецПроцедуры

Функция ПолучитьДвоичныеДанные(ИмяФайла)

	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда
		Данные = Новый ДвоичныеДанные(ИмяФайла);
		Попытка
			УдалитьФайлы(ИмяФайла);
		Исключение
		КонецПопытки;
		Возврат Данные;
	Иначе
		Возврат Неопределено;
	КонецЕсли; 
	

КонецФункции

Процедура ПолучитьПолучателей(ПараметрОбъектПечати, СписокПолучателей)
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ПараметрОбъектПечати))
			И ПараметрОбъектПечати.Метаданные().Реквизиты.Найти("Контрагент") <> Неопределено
	Тогда
		СписокПолучателей.Добавить(ПараметрОбъектПечати.Контрагент);
	Иначе
		Если ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.КонтактныеЛица")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.ФизическиеЛица")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.Пользователи")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.КонтактныеЛицаКонтрагентов")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.Контрагенты")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.Организации")
			ИЛИ ТипЗнч(ОбъектПечати) = Тип("СправочникСсылка.ЛичныеКонтакты")
		Тогда
			СписокПолучателей.Добавить(ПараметрОбъектПечати);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПолучитьПолучателей()

Процедура ОсновныеДействияФормыOK(Кнопка)
	
	СохранитьЗначения();
	
	СтруктураНовогоПисьма = Новый Структура;
	
	Если ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.Текст Тогда
		
		СтруктураНовогоПисьма.Вставить("ВидТекста", Перечисления.ВидыТекстовЭлектронныхПисем.Текст);
		ФайлВФорматеТекст = ПолучитьИмяВременногоФайла();
		Отчет.Записать(ФайлВФорматеТекст, ТипФайлаТабличногоДокумента.TXT);
		ТекстПисьма = Новый ТекстовыйДокумент;
		ТекстПисьма.Прочитать(ФайлВФорматеТекст);
		ТекстПисьма.ВставитьСтроку(1, "------------------------------------------------------");
		ТекстПисьма.ВставитьСтроку(1, "");
		ТекстСообщенияДляОтправки = ТекстПисьма.ПолучитьТекст();
		СтруктураНовогоПисьма.Вставить("Тело", ТекстСообщенияДляОтправки);
		УдалитьФайлы(ФайлВФорматеТекст);
		
	ИначеЕсли ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.HTML Тогда
		
		СтруктураНовогоПисьма.Вставить("ВидТекста", Перечисления.ВидыТекстовЭлектронныхПисем.HTML);
		ФайлВФорматеHTML = ПолучитьИмяВременногоФайла();
		Отчет.Записать(ФайлВФорматеHTML, ТипФайлаТабличногоДокумента.HTML);
		ТекстПисьма = Новый ТекстовыйДокумент;
		ТекстПисьма.Прочитать(ФайлВФорматеHTML);
		
		ХТМЛТекст = ТекстПисьма.ПолучитьТекст();
		
		НовыйHTMLДокумент = Новый COMОбъект("HtmlFile");
		НовыйHTMLДокумент.open("text/html");
		НовыйHTMLДокумент.write(ХТМЛТекст);
		НовыйHTMLДокумент.close();
		
		ТегТела = НовыйHTMLДокумент.all.tags("BODY");
		Для а = 0 По ТегТела.length - 1 Цикл
			ТегТела.item(а).innerHTML = "
			|<P><BR><BR><BR>
			|<HR>
			|</P>
			|<P></P>" + ТегТела.item(а).innerHTML;
		КонецЦикла;
		
		ТегКартинки = НовыйHTMLДокумент.all.tags("IMG");
		Для а = 0 По ТегКартинки.length - 1 Цикл
			Источник = ТегКартинки.item(а).src;
			Если Лев(Источник, 6) = "about:" И Найти(Источник, "files\image") > 0 Тогда
				ТегКартинки.item(а).src = КаталогВременныхФайлов() + Прав(Источник, СтрДлина(Источник) - 6);
			КонецЕсли;
		КонецЦикла;
		
		ТекстСообщенияДляОтправки = "";
		Для а = 0 По НовыйHTMLДокумент.all.length - 1 Цикл
			Если НовыйHTMLДокумент.all.item(а).tagName = "HTML" Тогда
				ТекстСообщенияДляОтправки = НовыйHTMLДокумент.all.item(а).innerHTML;
			КонецЕсли; 
		КонецЦикла;
		
		СтруктураНовогоПисьма.Вставить("Тело", ТекстСообщенияДляОтправки);
		УдалитьФайлы(ФайлВФорматеHTML);
		
	Иначе
		
		СтруктураНовогоПисьма.Вставить("ВидТекста", Перечисления.ВидыТекстовЭлектронныхПисем.Текст);
		
	КонецЕсли; 
	
	СтруктураНовогоПисьма.Вставить("Тема", ТемаСообщения);
	
	СписокПолучателей = Новый СписокЗначений();
	Если ЗначениеЗаполнено(ОбъектПечати) Тогда
		Если ТипЗнч(ОбъектПечати) = Тип("СписокЗначений") Тогда
			Для Каждого ОбъектПечатиЭлементСписка Из ОбъектПечати Цикл
				Если ЗначениеЗаполнено(ОбъектПечатиЭлементСписка.Значение) Тогда
					ПолучитьПолучателей(ОбъектПечатиЭлементСписка.Значение, СписокПолучателей);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ПолучитьПолучателей(ОбъектПечати, СписокПолучателей);
		КонецЕсли;
		Если СписокПолучателей.Количество() > 0.00 Тогда
			
			Запрос = Новый Запрос();
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КонтактнаяИнформация.Объект КАК Объект,
			|	КонтактнаяИнформация.Объект.Наименование КАК НаименованиеОбъекта,
			|	КонтактнаяИнформация.Вид КАК Вид,
			|	КонтактнаяИнформация.Представление КАК Представление,
			|	КонтактнаяИнформация.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Объект В (&СписокПолучателей)
			|	И КонтактнаяИнформация.Тип = &Тип
			|
			|УПОРЯДОЧИТЬ ПО
			|	КонтактнаяИнформация.Объект,
			|	ЗначениеПоУмолчанию УБЫВ
			|ИТОГИ ПО
			|	Объект
			|";
			
			Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Запрос.УстановитьПараметр("СписокПолучателей", СписокПолучателей);
			ВыборкаОбъектыПолучатели = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СписокПолучателей.Очистить();
			Пока ВыборкаОбъектыПолучатели.Следующий() Цикл
				ВыборкаЭлАдресПолучателя = ВыборкаОбъектыПолучатели.Выбрать();
				Если ВыборкаЭлАдресПолучателя.Следующий() Тогда
					СписокПолучателей.Добавить(ВыборкаЭлАдресПолучателя.Представление, ВыборкаЭлАдресПолучателя.НаименованиеОбъекта);
				КонецЕсли;
			КонецЦикла;
		Иначе
			СписокПолучателей.Очистить();
		КонецЕсли;
	КонецЕсли;
	СтруктураНовогоПисьма.Вставить("Кому", СписокПолучателей);
	
	СписокФайловВложений = Новый СписокЗначений;
	
	Если ЗначениеЗаполнено(ИмяФайлаВложения) Тогда
		ИмяФайлаВложения = ИмяФайлаВложения;
	Иначе
		ИмяФайлаВложения = ТемаСообщения;
	КонецЕсли; 
	
	Если ВложенияHTML Тогда
		Если ФайлВФорматеHTML = Неопределено Тогда
			ФайлВФорматеHTML = ПолучитьИмяВременногоФайла();
			Отчет.Записать(ФайлВФорматеHTML, ТипФайлаТабличногоДокумента.HTML);
		КонецЕсли;
		СписокФайловВложений.Добавить(Новый Структура("Хранилище, ИмяФайла, Наименование", ПолучитьДвоичныеДанные(ФайлВФорматеHTML), (ИмяФайлаВложения + ".HTM"), ИмяФайлаВложения));
	КонецЕсли; 
	
	Если ВложенияTXT Тогда
		Если ФайлВФорматеТекст = Неопределено Тогда
			ФайлВФорматеТекст = ПолучитьИмяВременногоФайла();
			Отчет.Записать(ФайлВФорматеТекст, ТипФайлаТабличногоДокумента.TXT);
		КонецЕсли; 
		СписокФайловВложений.Добавить(Новый Структура("Хранилище, ИмяФайла, Наименование", ПолучитьДвоичныеДанные(ФайлВФорматеТекст), (ИмяФайлаВложения + ".TXT"), ИмяФайлаВложения));
	КонецЕсли; 
	
	Если ВложенияMXL Тогда
		ФайлВФорматеMXL = ПолучитьИмяВременногоФайла();
		Отчет.Записать(ФайлВФорматеMXL, ТипФайлаТабличногоДокумента.MXL);
		СписокФайловВложений.Добавить(Новый Структура("Хранилище, ИмяФайла, Наименование", ПолучитьДвоичныеДанные(ФайлВФорматеMXL), (ИмяФайлаВложения + ".MXL"), ИмяФайлаВложения));
	КонецЕсли; 
	
	Если ВложенияXLS Тогда
		ФайлВФорматеXLS = ПолучитьИмяВременногоФайла();
		Отчет.Записать(ФайлВФорматеXLS, ТипФайлаТабличногоДокумента.XLS);
		СписокФайловВложений.Добавить(Новый Структура("Хранилище, ИмяФайла, Наименование", ПолучитьДвоичныеДанные(ФайлВФорматеXLS), (ИмяФайлаВложения + ".XLS"), ИмяФайлаВложения));
	КонецЕсли;
	
	Если СписокФайловВложений.Количество() > 0 Тогда
		СтруктураНовогоПисьма.Вставить("СписокФайловВложений", СписокФайловВложений);
	КонецЕсли; 
	
	СтруктураСозданногоПисьма = УправлениеЭлектроннойПочтой.НаписатьПисьмо(глЗначениеПеременной("глТекущийПользователь"), СтруктураНовогоПисьма,,,,,, Истина, Ложь);
	
	ЭтаФорма.Закрыть();
	
	Если ТипЗнч(СтруктураСозданногоПисьма) = Тип("Структура") Тогда
		СтруктураСозданногоПисьма.Форма.Открыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьЗначения()

	СтруктураЗначений = Новый Структура();
	СтруктураЗначений.Вставить("ВидТекстаПисьма", ВидТекстаПисьма);
	СтруктураЗначений.Вставить("ВложенияHTML"   , ВложенияHTML);
	СтруктураЗначений.Вставить("ВложенияMXL"    , ВложенияMXL);
	СтруктураЗначений.Вставить("ВложенияTXT"    , ВложенияTXT);
	СтруктураЗначений.Вставить("ВложенияXLS"    , ВложенияXLS);
	
	СохранитьЗначение("НастройкиОтправкиОтчетов", СтруктураЗначений);

КонецПроцедуры

Процедура ВосстановитьЗначения()

	Перем ЗначениеСтруктуры;
	
	ВосстановленноеЗначение = ВосстановитьЗначение("НастройкиОтправкиОтчетов");
	
	Если ТипЗнч(ВосстановленноеЗначение) = Тип("Структура") Тогда
		ВосстановленноеЗначение.Свойство("ВидТекстаПисьма", ЗначениеСтруктуры);
		Если ЗначениеСтруктуры = Неопределено Тогда
			ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.HTML;
		Иначе
			ВидТекстаПисьма = ЗначениеСтруктуры;
		КонецЕсли; 
		ВосстановленноеЗначение.Свойство("ВложенияHTML", ЗначениеСтруктуры);
		Если ЗначениеСтруктуры <> Неопределено Тогда
			ВложенияHTML = ЗначениеСтруктуры;
		КонецЕсли; 
		ВосстановленноеЗначение.Свойство("ВложенияMXL", ЗначениеСтруктуры);
		Если ЗначениеСтруктуры <> Неопределено Тогда
			ВложенияMXL = ЗначениеСтруктуры;
		КонецЕсли; 
		ВосстановленноеЗначение.Свойство("ВложенияTXT", ЗначениеСтруктуры);
		Если ЗначениеСтруктуры <> Неопределено Тогда
			ВложенияTXT = ЗначениеСтруктуры;
		КонецЕсли; 
		ВосстановленноеЗначение.Свойство("ВложенияXLS", ЗначениеСтруктуры);
		Если ЗначениеСтруктуры <> Неопределено Тогда
			ВложенияXLS = ЗначениеСтруктуры;
		КонецЕсли; 
	Иначе
		ВидТекстаПисьма = Перечисления.ВидыТекстовЭлектронныхПисем.HTML;
		ВложенияXLS = Истина;
	КонецЕсли; 

КонецПроцедуры
