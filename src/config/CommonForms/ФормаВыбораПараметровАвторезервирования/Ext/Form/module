// Процедура вызывается при нажатии кнопки "Значения по умолчанию"
// командной панели формы.
//
Процедура ДействияФормыЗначенияПоУмолчанию(Кнопка)

	УстановитьЗначенияПоУмолчанию();

КонецПроцедуры // ДействияФормыЗначенияПоУмолчанию()

// Процедура вызывается при нажатии кнопки "Заполнить и провести"
// основной командной панели формы.
//
Процедура ОсновныеДействияФормыЗаполнитьИПровести(Кнопка)
	Если НЕ Авторезервирование И НЕ Авторазмещение Тогда
		Предупреждение("Должен быть установлен хотя бы один признак - 'резервирование' или 'размещение'");
		Возврат;
	КонецЕсли;
	
	ЗаполнятьДокумент = Истина;
	Закрыть(истина);

КонецПроцедуры // ОсновныеДействияФормыЗаполнитьИПровести()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	УстановитьЗначенияПоУмолчанию();
	ЗаполнятьДокумент = Ложь;
    УстановитьДоступность();
КонецПроцедуры

Процедура УстановитьДоступность()
	Если ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.РезервированиеТоваров") Тогда
		ВидОперации = ФормаДокумента.ЭтотОбъект.ВидОперации;
		Если ВидОперации = Перечисления.ВидыОперацийРезервированиеТоваров.ПоСкладам Тогда
			Авторазмещение = ложь;
			ЭлементыФормы.АвтоРазмещение.Доступность = ложь;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРезервированиеТоваров.ПоЗаказам Тогда
			Авторезервирование = ложь;
			ЭлементыФормы.Авторезервирование.Доступность = ложь;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры


// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()

	ФормаДокумента.Авторезервирование = Авторезервирование И ЗаполнятьДокумент;
	ФормаДокумента.Авторазмещение     = Авторазмещение И ЗаполнятьДокумент;

	Если ЗаполнятьДокумент Тогда
		ФормаДокумента.ОчищатьРазмещениеПередЗаполнением    = ОчищатьРазмещениеПередЗаполнением;
		ФормаДокумента.СтратегияАвторезервированияПоЗаказам = СтратегияАвторезервированияПоЗаказам;
	КонецЕсли;

КонецПроцедуры

// Устанавливает значения настроек по умолчанию.
//
Процедура УстановитьЗначенияПоУмолчанию()

	Авторезервирование = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "Авторезервирование");
	Авторазмещение     = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "Авторазмещение");

	СтратегияАвторезервированияПоЗаказам = Константы.СтратегияАвторезервированияНоменклатурыПоЗаказам.Получить();
	Если НЕ ЗначениеЗаполнено(СтратегияАвторезервированияПоЗаказам) Тогда
		СтратегияАвторезервированияПоЗаказам = Перечисления.СтратегииАвторезервированияНоменклатуры.СначалаВЗаказахПоставщикамПотомНаСкладах;
	КонецЕсли;

	ОчищатьРазмещениеПередЗаполнением = Ложь;

КонецПроцедуры // УстановитьЗначенияПоУмолчанию()

