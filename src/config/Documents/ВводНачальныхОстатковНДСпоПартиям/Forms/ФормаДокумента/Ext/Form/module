////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента;

//Хранит список числовых колонок документа (Структура)
Перем ВестиСкладскойУчетБУ;

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

// Хранит колонки табличной части Данные по партиям
Перем мКолонкиДанныеПоПартиям;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ()
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));

	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДанныеПоПартиям,ЭлементыФормы.КоманднаяПанельПоПартиям);
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДанныеПоСФ,ЭлементыФормы.КоманднаяПанельПоСФ);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура устанавливает видимость для тех колонок в табличной части 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	ОтобразитьСчетаФактуры();
	
КонецПроцедуры //УстановитьВидимость()

// Процедура вызывается по кнопке при изменении активной строки в таблице косвенных 
// расходов, а также при переходе на закладку "Косвенные расходы".
Процедура ОтобразитьСчетаФактуры(ТекСтрокаПартий = Неопределено)

	Если ТекСтрокаПартий = Неопределено и не ЭлементыФормы.ДанныеПоПартиям.ТекущаяСтрока = Неопределено Тогда
		ТекСтрокаПартий = ЭлементыФормы.ДанныеПоПартиям.ТекущиеДанные;
	КонецЕсли; 
	
	
	// Включаем отбор тех дополнительных сведений, которые относятся к текущей строке,
	// выбранной в табличной части "ДанныеПоПартиям"
	Если ТекСтрокаПартий <> Неопределено Тогда
		// Включаем отбор ключу из текущей строки табличной части "ДополнительныеСведения"
		ЭлементыФормы.ДанныеПоСФ.ТолькоПросмотр = Ложь;
		ЭлементыФормы.ДанныеПоСФ.ОтборСтрок.КлючСтроки.Значение = ТекСтрокаПартий.КлючСтроки;

	Иначе
		// Текущая строка в таблице косвенных расходов не установлена
		ЭлементыФормы.ДанныеПоСФ.ТолькоПросмотр = Истина;
		Если ДанныеПоПартиям.Количество() = 0 Тогда
			ДанныеПоСФ.Очистить();
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ОтобразитьСчетаФактуры()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ


// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа.
//
Процедура ДействияФормыРедактироватьНомер(Кнопка)

	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

КонецПроцедуры

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ОсновныеДействияФормыДействиеПечать(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()


	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), , , ПараметрОбъектКопирования, ПараметрОснование);
		
	Иначе
		
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);

	КонецЕсли;
	
	мТекущаяДатаДокумента = Дата;
	
	// Установить видимость реквизитов и заголовков колонок.
	УстановитьВидимость();

	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	
	// Предполагается использовать отбор по таблице "ДанныеПоСФ"
	ЭлементыФормы.ДанныеПоСФ.ОтборСтрок.КлючСтроки.Использование = Истина;
	ЭлементыФормы.ДанныеПоСФ.ОтборСтрок.КлючСтроки.Значение = 0;

	// Установить видимость колонок "ХарактеристикаНоменклатуры" и "СерияНоменклатуры"
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(мКолонкиДанныеПоПартиям);
	РаботаСДиалогами.УстановитьВидимостьСерииНоменклатуры(мКолонкиДанныеПоПартиям);

	// Установить печатную форму по умолчанию.
	РаботаСДиалогами.УстановитьКнопкуПечати(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа

КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	Если Не ПустаяСтрока(Номер) Тогда
			МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
	
КонецПроцедуры // ОрганизацияПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОЙ ЧАСТИ


// Процедура - обработчик события "ПриАктивизацииСтроки" 
// для табличной части "ДанныеПоСФ".
//
Процедура ДанныеПоПартиямПриАктивизацииСтроки(Элемент)
	
    ПодключитьОбработчикОжидания("ОбработчикОжиданияДанныеПоПартиямПриАктивизацииСтроки", 0.1, Истина);	 
		
КонецПроцедуры // ДанныеПоПартиямПриАктивизацииСтроки

// Процедура - обрабочик ожидания для события ПриАктивизацииСтроки
// табличного поля ДанныеПоПартиям
//
Процедура ОбработчикОжиданияДанныеПоПартиямПриАктивизацииСтроки()
    
    ОтобразитьСчетаФактуры();
    
КонецПроцедуры // ОбработчикОжиданияДанныеПоПартиямПриАктивизацииСтроки

 
// Процедура - обработчик события "ПриНачалеРедактированияСтроки" 
// для табличной части "ДанныеПоСФ".
//
Процедура ДанныеПоПартиямПриНачалеРедактирования(Элемент, НоваяСтрока)
	
	Если НоваяСтрока Тогда

		// Инициализируем значение "КлючСтроки" для установки связи данной таблицы с таблицей 
		// "ДанныеПоПартиям".
		// Значение должно быть уникальным в пределах таблицы ДанныеПоПартиям
		НеУстановлено = Истина;
		Кандидат = ДанныеПоПартиям.Количество();

		Пока НеУстановлено Цикл
			Если ДанныеПоПартиям.Найти(Кандидат, "КлючСтроки") = Неопределено Тогда
				// Уникальное значение ключа
				ЭлементыФормы.ДанныеПоПартиям.ТекущиеДанные.КлючСтроки = Кандидат;
				НеУстановлено = Ложь;

			Иначе
				// Такое значение ключа уже использовано
				Кандидат = Кандидат + 1;

			КонецЕсли;

		КонецЦикла;
		
		ТекущаяСтрока = Элемент.ТекущиеДанные;
		Если не Элемент.Колонки.Найти("Номенклатура")=Неопределено Тогда
			ТекущаяСтрока.Номенклатура = Неопределено;
		КонецЕсли; 

		ОтобразитьСчетаФактуры();

	КонецЕсли;
	
КонецПроцедуры

Процедура ДанныеПоПартиямПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОтобразитьСчетаФактуры(Элемент.ТекущиеДанные);
КонецПроцедуры

// Процедура - обработчик события "ПередУдалением" 
// строки табличной части "ДанныеПоСФ".
//
Процедура ДанныеПоПартиямПередУдалением(Элемент, Отказ)
	
	// Необходимо очистить дополнительные сведения
	КлючПоиска = Новый Структура("КлючСтроки" , ЭлементыФормы.ДанныеПоСФ.ОтборСтрок.КлючСтроки.Значение);
	
	НайденныеСтроки = ДанныеПоСФ.НайтиСтроки(КлючПоиска);
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		ДанныеПоСФ.Удалить(Строка);
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик события "ПослеУдаления" 
// для табличной части "ДанныеПоСФ".
//
Процедура ДанныеПоПартиямПослеУдаления(Элемент)
	
	ОтобразитьСчетаФактуры();
	
КонецПроцедуры

// Процедура - обработчик события "ПриНачалеРедактирования"
// строки табличной части "Дополнительные сведения".
//
Процедура ДанныеПоСФПриНачалеРедактирования(Элемент, НоваяСтрока)
	
	Если НоваяСтрока Тогда
		ЭлементыФормы.ДанныеПоСФ.ТекущиеДанные.КлючСтроки = ЭлементыФормы.ДанныеПоСФ.ОтборСтрок.КлючСтроки.Значение;
	КонецЕсли; 
		
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении"
// колонки "СтавкаНДС" табличной части "Дополнительные сведения".
//
Процедура ДанныеПоСФСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ДанныеПоСФ.ТекущиеДанные;
	Если ТекущиеДанные.Стоимость <> 0 Тогда
		ТекущиеДанные.НДС = УчетНДС.РассчитатьСуммуНДС(ТекущиеДанные.Стоимость, Истина, Истина, 
												УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка"
// колонки "ВидЦенности" табличной части "Дополнительные сведения".
//
Процедура ДанныеПоСФВидЦенностиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Элемент.СписокВыбора.Очистить();
	
	Элемент.СписокВыбора.Добавить(Перечисления.ВидыЦенностей.Товары);
	Элемент.СписокВыбора.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	Элемент.СписокВыбора.Добавить(Перечисления.ВидыЦенностей.ПосредническиеУслуги);
	Элемент.СписокВыбора.Добавить(Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги);
		
КонецПроцедуры

Процедура ДействияФормыЗаполнить(Кнопка)
	
	Если ДанныеПоПартиям.Количество() > 0 Тогда
	
		ТекстВопроса = "Перед заполнением табличные части будут очищены. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да,);
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 
		
	КонецЕсли;
	
	ДанныеПоСФ.Очистить();
	ДанныеПоПартиям.Очистить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегПартии.Номенклатура КАК Номенклатура,
	|	РегПартии.Склад КАК Склад,
	|	РегПартии.ДокументОприходования КАК Партия,
	|	РегПартии.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РегПартии.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СУММА(РегПартии.КоличествоОстаток) КАК КолОст,
	|	СУММА(РегПартии.СтоимостьОстаток) КАК СуммаБезНДС,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.ВидЦенности КАК ВидЦенности,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СчетФактура.НДСВключенВСтоимость, ЛОЖЬ) КАК НДСВключенВСтоимость
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	|		&КонДата,
	|		ДокументОприходования.Организация = &Орг
	|		    И СтатусПартии = &СтатусПартии_Купленный) КАК РегПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный.Обороты КАК НДСПредъявленныйОбороты
	|		ПО РегПартии.ДокументОприходования = НДСПредъявленныйОбороты.СчетФактура
	|			И (НДСПредъявленныйОбороты.ВидЦенности = &ВЦ_Товары)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегПартии.Номенклатура,
	|	РегПартии.Склад,
	|	РегПартии.ДокументОприходования,
	|	РегПартии.ХарактеристикаНоменклатуры,
	|	РегПартии.СерияНоменклатуры,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СчетФактура.НДСВключенВСтоимость, ЛОЖЬ)
	|
	|ИМЕЮЩИЕ
	|	СУММА(РегПартии.КоличествоОстаток) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Партия,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры
	|ИТОГИ
	|	СУММА(КолОст),
	|	СУММА(СуммаБезНДС)
	|ПО
	|	Партия,
	|	Склад,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр( "КонДата",  ?(ЭтоНовый(), КонецДня(Дата), Дата));
	Запрос.УстановитьПараметр( "СтатусПартии_Купленный", Перечисления.СтатусыПартийТоваров.Купленный);
	Запрос.УстановитьПараметр( "Орг",      Организация);
	Запрос.УстановитьПараметр( "ВЦ_Товары",               Перечисления.ВидыЦенностей.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	МетаТип = Метаданные.Документы[Метаданные().Имя].ТабличныеЧасти["ДанныеПоПартиям"].Реквизиты["Партия"].Тип;
	
	Ключ = 0;
	
	ЗаполнятьСтоимость = глЗначениеПеременной("ВалютаРегламентированногоУчета") = глЗначениеПеременной("ВалютаУправленческогоУчета");
	
	ОбходПоПартиям = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходПоПартиям.Следующий() Цикл
		Если НЕ МетаТип.СодержитТип(ТипЗнч(ОбходПоПартиям.Партия)) ИЛИ НЕ ЗначениеЗаполнено(ОбходПоПартиям.Партия) Тогда
			Продолжить;
		КонецЕсли;
		ОбходПоСкладам = ОбходПоПартиям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ОбходПоСкладам.Следующий() Цикл
			ОбходПоНоменклатуре = ОбходПоСкладам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ОбходПоНоменклатуре.Следующий() Цикл
				ОбходПоХаракт = ОбходПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ОбходПоХаракт.Следующий() Цикл
					ОбходПоСериям = ОбходПоХаракт.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ОбходПоСериям.Следующий() Цикл
						Ключ = Ключ + 1;
							
						// Добавим строку в таб.часть Партии
						СтрокаПартий              = ДанныеПоПартиям.Добавить();
						СтрокаПартий.КлючСтроки   = Ключ;
						СтрокаПартий.Номенклатура = ОбходПоСериям.Номенклатура;
						СтрокаПартий.Партия       = ОбходПоСериям.Партия;
						СтрокаПартий.Склад        = ОбходПоСериям.Склад;
						СтрокаПартий.Количество   = ОбходПоСериям.КолОст;
									
						СтрокаПартий.ХарактеристикаНоменклатуры = ОбходПоСериям.ХарактеристикаНоменклатуры;
						СтрокаПартий.СерияНоменклатуры          = ОбходПоСериям.СерияНоменклатуры;
									
						Обход = ОбходПоСериям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока Обход.Следующий() Цикл
								
							СтрокаСФ = ДанныеПоСФ.Добавить();
							СтрокаСФ.КлючСтроки   = Ключ;
							СтрокаСФ.СчетФактура  = Обход.Партия;
							СтрокаСФ.ВидЦенности  = Обход.ВидЦенности;
							
							СтрокаСФ.СтавкаНДС    = Обход.СтавкаНДС;
								
							СтрокаСФ.Количество = Обход.КолОст;
							СтрокаСФ.НДСВключенВСтоимость = Обход.НДСВключенВСтоимость;
							Если ЗаполнятьСтоимость Тогда
								СтрокаСФ.НДС = УчетНДС.РассчитатьСуммуНДС( Обход.СуммаБезНДС, Истина, Обход.НДСВключенВСтоимость, УчетНДС.ПолучитьСтавкуНДС(Обход.СтавкаНДС));
										
								СтрокаСФ.Стоимость  = ?( Обход.НДСВключенВСтоимость, Обход.СуммаБезНДС, Обход.СуммаБезНДС + СтрокаСФ.НДС);
							КонецЕсли;
							
						КонецЦикла; // По детальным записям
								
					КонецЦикла; // По сериям
				КонецЦикла; // По хар-кам
			КонецЦикла; // По Номенклатуре
		КонецЦикла; // По складам
	КонецЦикла; // По партиям
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля ввода СчетФактура
//
Процедура ДанныеПоСФСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтруктОтбора = Новый Структура();

	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктОтбора.Вставить("Организация", Организация);
	КонецЕсли;

	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, СтруктОтбора, "ДанныеПоСФ");

КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля ввода Партия
//
Процедура ДанныеПоПартиямПартияНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтруктОтбора = Новый Структура();

	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктОтбора.Вставить("Организация", Организация);
	КонецЕсли;

	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, СтруктОтбора, "ДанныеПоПартиям");

КонецПроцедуры

Процедура ПослеЗаписи()

	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

мКолонкиДанныеПоПартиям = ЭлементыФормы.ДанныеПоПартиям.Колонки;