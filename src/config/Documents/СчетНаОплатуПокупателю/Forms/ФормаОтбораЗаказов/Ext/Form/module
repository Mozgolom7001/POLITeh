Перем НП;
Перем ДатаНачала;
Перем ДатаОкончания;

// Переменные настройки диалога
Перем ФлагВидимостиРазмещениеРезервов;
Перем ФлагВидимостиПросроченоДнейОплаты;
Перем ФлагВидимостиДатыОплаты;
Перем ФлагВидимостиПросроченоДнейОтгрузки;
Перем ФлагВидимостиДатыОтгрузки;
Перем ФлагДоступностиИзмененияНастроек;
Перем ФлагДоступностиИзмененияДляОтгрузки;

Перем СоответствиеНазначений;
Перем СтруктураДляОтбораПоКатегориям;
Перем СтруктураПредставлениеПолей;

Перем мКрасныйЦвет, мСинийЦвет, мЦветГруппы, мЦветТекста, мОбычныйШрифт, мЖирныйШрифт;

Перем мИспользоватьТару, мЧастичноеЗаполнение, мКонтролироватьПревышениеОбъемаЗаказа;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура определяет факт частичного заполнения табличных частей по способу заполнения.
//
// Параметры:
//  Пометка       - булево, признак установки/снятия флажков.
//  ТабличноеПоле - табличное поле, флажки которого необходимо установить/снять.
//
Процедура ОпределитьЧастичноеЗаполнение()

	мЧастичноеЗаполнение = Ложь;
	Если СпособЗаполнения = 2 Тогда
		мЧастичноеЗаполнение = Истина;
	КонецЕсли;

КонецПроцедуры // ОпределитьЧастичноеЗаполнение()

// Устанавливает доступные значения поля СпособЗаполнения.
// вызывает анализ текущего состояния заказа.
//
Процедура УстановитьСпособЗаполнения()

	Если ДокументОбъект.Товары.Количество() = 0
		И ДокументОбъект.Услуги.Количество() = 0
		И ДокументОбъект.ВозвратнаяТара.Количество() = 0 Тогда
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Очистить();
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Добавить(1, "Заполнить по заказам");
		СпособЗаполнения = 1;
	ИначеЕсли НЕ ДокументОбъект.ЭтоНовый() Тогда
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Очистить();
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Добавить(2, "Добавить по заказам");
		СпособЗаполнения = 2;
	Иначе
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Очистить();
		ЭлементыФормы.ПолеНастройкиСпособЗаполнения.СписокВыбора.Добавить(1, "Заполнить по заказам");
		СпособЗаполнения = 1;
	КонецЕсли;
	
	ОпределитьЧастичноеЗаполнение();

КонецПроцедуры

// Процедура устанавливает/снимает флажки в заданном табличном поле.
//
// Параметры:
//  Пометка       - булево, признак установки/снятия флажков.
//  ТабличноеПоле - табличное поле, флажки которого необходимо установить/снять.
//
Процедура УстановитьСнятьФлажки(Пометка, ТабличноеПоле)

	Для каждого СтрокаТабличнойЧасти Из ТабличноеПоле Цикл
		СтрокаТабличнойЧасти.Пометка = Пометка;
	КонецЦикла;

КонецПроцедуры // УстановитьСнятьФлажки()

// Процедура обнуляет столбец "Добавить в документ" в заданном табличном поле.
//
// Параметры:
//  ТабличноеПоле - обрабатываемое табличное поле.
//
Процедура ОбнулитьСтолбецДобавитьВДокумент(ТабличноеПоле)

	Для каждого СтрокаТабличнойЧасти Из ТабличноеПоле Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			СтрокаТабличнойЧасти.ДобавитьВДокумент = 0;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ОбнулитьСтолбецДобавитьВДокумент()

// Процедура открывает отчет "Анализ заказа" по значению заказа.
//
// Параметры:
//  ЗаказПокупателя - ссылка на документ заказ.
//
Процедура ОткрытьОтчетАнализЗаказа(ЗаказПокупателя)

    УправлениеЗаказами.СформироватьОтчетАнализЗаказа(ЗаказПокупателя,ложь, истина);

КонецПроцедуры // ОткрытьОтчетАнализЗаказа()

// Процедура заполняет столбец "Добавить в документ" в заданном табличном поле.
//
// Параметры:
//  ТабличноеПоле       - обрабатываемое табличное поле.
//  ИмяСтолбцаИсточника - столбец, данными которого будет заполняться столбец "Добавить в документ".
//
Процедура ЗаполнитьСтолбецДобавитьВДокумент(ТабличноеПоле, ИмяСтолбцаИсточника)

	Для Каждого СтрокаТабличнойЧасти Из ТабличноеПоле Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			Отгрузить = СтрокаТабличнойЧасти[ИмяСтолбцаИсточника];
			Если мЧастичноеЗаполнение Тогда
				Заполнено         = СтрокаТабличнойЧасти.Заполнено;
				Добавить          = Отгрузить - ?(Заполнено > 0, Заполнено, 0);
				СтрокаТабличнойЧасти.ДобавитьВДокумент = ?(Добавить > 0, Добавить, 0);
			Иначе
				СтрокаТабличнойЧасти.ДобавитьВДокумент = Отгрузить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСтолбецДобавитьВДокумент()

// Устанавливает видимость полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьВидимостьЭлементовНастройки()
	
	// Поле Размещение резервов видимо когда Наличие резервов установлено как С резервами
	ЭлементыФормы.ПолеНастройкиРазмещениеРезервов.Видимость = ФлагВидимостиРазмещениеРезервов;
	
	// Поле Дата отгрузки видимо когда Меньше/Равна/Больше
	ЭлементыФормы.ПолеДатаОтгрузки.Видимость = ФлагВидимостиДатыОтгрузки;
	
	// Поле ввода Просрочено дней видимо только когда Просрочен
	ЭлементыФормы.НадписьОтгрузкаДней.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.ПолеПросроченоДнейОтгрузки.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.НадписьОтгрузкаНа.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	
	// Поле Дата оплаты видимо когда Меньше/Равна/Больше
	ЭлементыФормы.ПолеДатаОплаты.Видимость = ФлагВидимостиДатыОплаты;
	
	// Поле ввода Просрочено дней видимо только когда Просрочен
	ЭлементыФормы.НадписьОплатаДней.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	ЭлементыФормы.ПолеПросроченоДнейОплаты.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	ЭлементыФормы.НадписьОплатаНа.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	
КонецПроцедуры // УстановитьВидимостьЭлементовНастройки()

// Устанавливает доступность полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьДоступностьЭлементовНастройки()
	
	ЭлементыФормы.ПолеНастройкиНаличиеРезервов.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСостояниеОплаты.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСрокОплатыСравнение.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСостояниеОтгрузки.Доступность = ФлагДоступностиИзмененияДляОтгрузки;
	ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение.Доступность = ФлагДоступностиИзмененияНастроек;

КонецПроцедуры // УстановитьДоступностьЭлементовНастройки()

// Устанавливает доступность кнопок в зависимости от наличия документов в таблице.
//
Процедура УстановитьДоступностьКнопокПанелиЗаказов()
	
	// При пустом ТабличноеПолеЗаказы не должны быть доступны кнопки действий над заказами.
	Если ТабличноеПолеЗаказы.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКнопокПанелиЗаказов()

// Устанавливает доступность кнопок в зависимости от наличия номенклатуры в таблицах.
//
Процедура УстановитьДоступностьКнопокПанелейТабЧастей()

	// При пустом табличное поле не должны быть доступны кнопки действий.
	Если Товары.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельТовары.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельТовары.Доступность = Ложь;
	КонецЕсли;

	Если Услуги.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельУслуги.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельУслуги.Доступность = Ложь;
	КонецЕсли;
	
	Если ВозвратнаяТара.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельВозвратнаяТара.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельВозвратнаяТара.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКнопокПанелейТабЧастей()

// Устанавливает список "Вариантов отбора".
//
Процедура УстановитьСтруктуруВариантовОтбора()
	
	// Установить вариант отбора заказов
	ВариантОтбора = ЗаполнитьСписокВариантовОтбора();
	
	ПолеНастройкиВариантОтбораПриИзменении(ЭлементыФормы.ПолеНастройкиВариантОтбора);
	
КонецПроцедуры // УстановитьСтруктуруВариантовОтбора()

// Вызывается при изменении настроек отбора в форме.
//
Процедура ПриИзмененииНастроекПоУмолчанию()

	ПолеНастройкиНаличиеРезервовПриИзменении(ЭлементыФормы.ПолеНастройкиНаличиеРезервов);
	ПолеНастройкиСрокОплатыСравнениеПриИзменении(ЭлементыФормы.ПолеНастройкиСрокОплатыСравнение);
	ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение);
	
	Если ВариантОтбора = 1 Тогда
		// Для данного вида заполнения доступны для изменения настройки условий отгрузки
		ФлагДоступностиИзмененияДляОтгрузки = Истина;
	ИначеЕсли ВариантОтбора = 2 Тогда
		// Для данного вида заполнения недоступны для изменения настройки условий отгрузки
		ФлагДоступностиИзмененияДляОтгрузки = Ложь;
	КонецЕсли;
	
	ФлагДоступностиИзмененияНастроек = Истина;
	
	УстановитьДоступностьЭлементовНастройки();
			
КонецПроцедуры // ПриИзмененииНастроекПоУмолчанию()

// Заполнить список вариантов отбора.
// и вернуть Вид заполнения по умолчанию для данного Вида операции.
//
// Возвращаемое значение:
//  Число - Вид заполнения по умолчанию для данного Вида операции
//
Функция ЗаполнитьСписокВариантовОтбора()
	
	ЭлементыФормы.ПолеНастройкиВариантОтбора.СписокВыбора.Очистить();
	
	ЭлементыФормы.ПолеНастройкиВариантОтбора.СписокВыбора.Добавить(1, "Произвольный отбор");
	ЭлементыФормы.ПолеНастройкиВариантОтбора.СписокВыбора.Добавить(2, "Неисполненные заказы");

	Возврат 2;

КонецФункции // ЗаполнитьСписокВариантовОтбора()

// Заполняет установки по умолчанию для отбора по всем возможным условиям.
//
Процедура УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()
	
	// Не важно
	НаличиеРезервов	=1;
	
	// Не важно
	СостояниеОплаты = 1;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Не важно
	СрокОплатыСравнение = 1;
	// Не важно
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВариантОтбора));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОплаты = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОплаты = 1;
	КонецЕсли;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВариантОтбора));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОтгрузки = 1;
	КонецЕсли;
	
	// По умолчанию Текущая дата
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()

// Заполняет установки по умолчанию для отбора "Неисполненных" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляНеисполненныхЗаказов()

	// Без резервов
	НаличиеРезервов = 1;
	
	// Оплачен полностью
	СостояниеОплаты = 1;
	
	// Отгружен полностью
	СостояниеОтгрузки = 2;
	
	// Срок оплаты Не важно
	СрокОплатыСравнение = 1;
	// Срок отгрузки Меньше
	СрокОтгрузкиСравнение = 3;
	
	// По умолчанию 1 день (в отборе не участвуют)
	ПросроченоДнейОплаты = 1;
	ПросроченоДнейОтгрузки = 1;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОплаты = ТекущаяДата();
	// Дата документа плюс 1 день
	ДатаОтгрузки = ДокументОбъект.Дата + 24*60*60;
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляНеисполненныхЗаказов()

// Создает запрос и заполняет начальные установки построителя отбора для заказов.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета)

	// Запрос для отбора
	ТекстЗапроса = "
	|ВЫБРАТЬ //РАЗЛИЧНЫЕ
	|	истина 																	КАК Переносить,
	|	ложь 																	КАК НеЗаполнять,
	|	ВЫБОР КОГДА ЕстьNULL(ЗаказыПокупателейОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ										       КАК ЗаказыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(ЗаказыПокупателейТара.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ										        КАК ТараКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ										        КАК РезервыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ										        КАК РазмещенияКоличество,
	|	ЕстьNULL(ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток,0)         КАК ЗаказыОстаток,
	|	ЕстьNULL(РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток,0)     КАК РасчетыОстаток,
	|
	|	ДокументыЗаказПокупателя.Ссылка,
	|	ДокументыЗаказПокупателя.Дата,
	|	ДокументыЗаказПокупателя.Номер,
	|	ДокументыЗаказПокупателя.Контрагент,
	|	ДокументыЗаказПокупателя.ДоговорКонтрагента,
	|	ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента,
	|	ДокументыЗаказПокупателя.Организация,
	|	ДокументыЗаказПокупателя.Подразделение,
	|	ДокументыЗаказПокупателя.СкладГруппа,
	|	ДокументыЗаказПокупателя.Ответственный,
	|	ДокументыЗаказПокупателя.СуммаДокумента,
	|	ДокументыЗаказПокупателя.ВалютаДокумента,
	|	ДокументыЗаказПокупателя.ТипЦен,
	|	ДокументыЗаказПокупателя.ДатаОплаты,
	|	ДокументыЗаказПокупателя.ДатаОтгрузки,
	|	ДокументыЗаказПокупателя.УсловиеПродаж,
	|	ДокументыЗаказПокупателя.Грузополучатель,
	|	ДокументыЗаказПокупателя.ДисконтнаяКарта,
	|	ДокументыЗаказПокупателя.Проведен
	|
	|{ВЫБРАТЬ 
	|	ДокументыЗаказПокупателя.Контрагент.* КАК Контрагент,
	|	ДокументыЗаказПокупателя.ДоговорКонтрагента.* КАК ДоговорКонтрагента,
	|	ДокументыЗаказПокупателя.Организация.* КАК Организация,
	|	ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента.* КАК КонтактноеЛицоКонтрагента,
	|	ДокументыЗаказПокупателя.Подразделение.* КАК Подразделение,
	|	ДокументыЗаказПокупателя.СкладГруппа.* КАК СкладГруппа,
	|	ДокументыЗаказПокупателя.Ответственный.* КАК Ответственный,
	|	ДокументыЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	ДокументыЗаказПокупателя.ТипЦен.* КАК ТипЦен,
	|	ДокументыЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ДокументыЗаказПокупателя.УсловиеПродаж.* КАК УсловиеПродаж,
	|	ДокументыЗаказПокупателя.Грузополучатель.* КАК Грузополучатель,
	|	ДокументыЗаказПокупателя.ДисконтнаяКарта.* КАК ДисконтнаяКарта
	|//СВОЙСТВА
	|}
	|
	|ИЗ
	|	Документ.ЗаказПокупателя                                    КАК ДокументыЗаказПокупателя
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(, СтатусПартии <> &СтатусТары) 
	|		                                                        КАК ЗаказыПокупателейОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = ЗаказыПокупателейОстатки.ЗаказПокупателя
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(, СтатусПартии = &СтатусТары) 
	|		                                                        КАК ЗаказыПокупателейТара
	|		ПО ДокументыЗаказПокупателя.Ссылка = ЗаказыПокупателейТара.ЗаказПокупателя
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.РасчетыСКонтрагентами.Остатки         КАК РасчетыСКонтрагентамиОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = РасчетыСКонтрагентамиОстатки.Сделка
	|
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|       РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки       КАК ТоварыВРезервеНаСкладахОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = ТоварыВРезервеНаСкладахОстатки.ДокументРезерва
	|
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.РазмещениеЗаказовПокупателей.Остатки  КАК РазмещениеЗаказовПокупателейОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = РазмещениеЗаказовПокупателейОстатки.ЗаказПокупателя
	|
	|//СОЕДИНЕНИЯ
	|
	|	{ ГДЕ 
	|	 ДокументыЗаказПокупателя.Контрагент.* КАК Контрагент,
	|	 ДокументыЗаказПокупателя.ДоговорКонтрагента.* КАК ДоговорКонтрагента,
	|	 ДокументыЗаказПокупателя.Организация.* КАК Организация,
	|	 ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента.* КАК КонтактноеЛицоКонтрагента,
	|	 ДокументыЗаказПокупателя.СкладГруппа.* КАК СкладГруппа,
	|	 ДокументыЗаказПокупателя.Подразделение.* КАК Подразделение,
	|	 ДокументыЗаказПокупателя.Ответственный.* КАК Ответственный,
	|	 ДокументыЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	 ДокументыЗаказПокупателя.ТипЦен.* КАК ТипЦен,
	|	 ДокументыЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	 ДокументыЗаказПокупателя.УсловиеПродаж.* КАК УсловиеПродаж,
	|	 ДокументыЗаказПокупателя.Грузополучатель.* КАК Грузополучатель,
	|	 ДокументыЗаказПокупателя.ДисконтнаяКарта.* КАК ДисконтнаяКарта
	|//СВОЙСТВА
	|//КАТЕГОРИИ	
	|	}
	|
	|ГДЕ
	|	(ДокументыЗаказПокупателя.Проведен) И
	|
	|	((ДокументыЗаказПокупателя.Дата >= &ДатаНач) ИЛИ (&ДатаНач = &ПустаяДата)) И
	|	((ДокументыЗаказПокупателя.Дата <= &ДатаКон) ИЛИ (&ДатаКон = &ПустаяДата)) И
	|
	|	(	((&СрокОплатыСравнение = 6) И (ДокументыЗаказПокупателя.ДатаОплаты = &ПустаяДата)) ИЛИ
	|		((&СрокОплатыСравнение = 5) И (ДокументыЗаказПокупателя.ДатаОплаты > &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 4) И (ДокументыЗаказПокупателя.ДатаОплаты = &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 3) И (ДокументыЗаказПокупателя.ДатаОплаты < &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 2) И (ДокументыЗаказПокупателя.ДатаОплаты <= &ТребуемаяДатаОплаты)) ИЛИ
	|		(&СрокОплатыСравнение = 1)
	|	) И
	|
	|	(	((&СрокОтгрузкиСравнение = 6) И (ДокументыЗаказПокупателя.ДатаОтгрузки = &ПустаяДата)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 5) И (ДокументыЗаказПокупателя.ДатаОтгрузки > &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 4) И (ДокументыЗаказПокупателя.ДатаОтгрузки = &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 3) И (ДокументыЗаказПокупателя.ДатаОтгрузки < &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 2) И (ДокументыЗаказПокупателя.ДатаОтгрузки <= &ТребуемаяДатаОтгрузки)) ИЛИ
	|		(&СрокОтгрузкиСравнение = 1)
	|	) И
	|
	|	(	((&СостояниеОплаты = 3) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток IS NULL) ИЛИ (РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток < 0))) ИЛИ
	|		((&СостояниеОплаты = 2) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И (РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток > 0)) ИЛИ
	|		(&СостояниеОплаты = 1)
	|	) И
	|
	|	(	((&СостояниеОтгрузки = 3) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток IS NULL) ИЛИ (ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток < 0)) И ((ЗаказыПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (ЗаказыПокупателейОстатки.КоличествоОстаток < 0)) И ((ЗаказыПокупателейТара.КоличествоОстаток IS NULL) ИЛИ (ЗаказыПокупателейТара.КоличествоОстаток < 0))) ИЛИ
	|		((&СостояниеОтгрузки = 2) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток > 0) ИЛИ (ЗаказыПокупателейОстатки.КоличествоОстаток > 0) ИЛИ (ЗаказыПокупателейТара.КоличествоОстаток > 0))) ИЛИ
	|		(&СостояниеОтгрузки = 1)
	|	) И
	|
	|	(	((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 3) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0)) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 2) И (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 1) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0))) ИЛИ
	|		((&НаличиеРезервов = 2) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		(&НаличиеРезервов = 1)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыЗаказПокупателя.Дата, ДокументыЗаказПокупателя.Номер 
	|";

	СтруктураПредставлениеПолей = Новый Структура;
	
	СтруктураПредставлениеПолей.Вставить("Контрагент",                  "Контрагент");
	СтруктураПредставлениеПолей.Вставить("ДоговорКонтрагента",          "Договор");
	СтруктураПредставлениеПолей.Вставить("Организация",                 "Организация");
	СтруктураПредставлениеПолей.Вставить("КонтактноеЛицоКонтрагента",   "Контактное лицо");
	СтруктураПредставлениеПолей.Вставить("Подразделение",               "Подразделение");
	СтруктураПредставлениеПолей.Вставить("СкладГруппа",                 "Склад/группа");
	СтруктураПредставлениеПолей.Вставить("Ответственный",               "Ответственный");
	СтруктураПредставлениеПолей.Вставить("СуммаДокумента",              "Сумма документа");
	СтруктураПредставлениеПолей.Вставить("ТипЦен",                      "Тип цен");
	СтруктураПредставлениеПолей.Вставить("ВалютаДокумента",             "Валюта документа");
	СтруктураПредставлениеПолей.Вставить("УсловиеПродаж",               "Условие продаж");
	СтруктураПредставлениеПолей.Вставить("Грузополучатель",             "Грузополучатель");
	СтруктураПредставлениеПолей.Вставить("ДисконтнаяКарта",             "Дисконтная карта");
	
	Если ИспользоватьСвойстваИКатегории Тогда

        СоответствиеНазначений = Новый Соответствие;
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		//ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта
		
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";
		
		ТаблицаПолей.Очистить();
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.Контрагент";
		НоваяСтрока.Представление = "Контрагент";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты;

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.Контрагент";
		НоваяСтрока.Представление = "Грузополучатель";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты;

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента";
		НоваяСтрока.Представление = "КонтактноеЛицоКонтрагента";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_КонтактныеЛица;
		
		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, СоответствиеНазначений, ПостроительОтчета.Параметры,, ТекстПоляКатегорий, ТекстПоляСвойств,, "//СВОЙСТВА", "//КАТЕГОРИИ", "//СОЕДИНЕНИЯ", , СтруктураДляОтбораПоКатегориям);
		
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстЗапроса) = Ложь Тогда
		ПостроительОтчета.Текст = ТекстЗапроса;
	КонецЕсли;
	
	УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, СоответствиеНазначений, СтруктураПредставлениеПолей);
	
	// Заполнить отбор построителя по умолчанию
	Если НЕ(ПостроительОтчета.Отбор.Количество() > 0) Тогда
		Если НастройкиИзДокумента Тогда
			ПостроительОтчета.УстановитьНастройки(НастройкиОтбора);
		Иначе
			ЗаполнитьОтборПостроителя(ПостроительОтчета);
		КонецЕсли;
	КонецЕсли;
	
	// Заполнить представление полей построителя по именам
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
КонецПроцедуры // ИнициироватьПостроительДляОтбораЗаказов()

// Заполняет переменные построителя выбранными в форме значениями.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьПараметрыПостроителя(ПостроительОтчета)
	
	// Заполним параметры построителя
	ПостроительОтчета.Параметры.Вставить("НаличиеРезервов",       НаличиеРезервов);
	ПостроительОтчета.Параметры.Вставить("РазмещениеРезервов",    РазмещениеРезервов);
	ПостроительОтчета.Параметры.Вставить("СостояниеОплаты",       СостояниеОплаты);
	ПостроительОтчета.Параметры.Вставить("СостояниеОтгрузки",     СостояниеОтгрузки);
	ПостроительОтчета.Параметры.Вставить("СрокОплатыСравнение",   СрокОплатыСравнение);
	ПостроительОтчета.Параметры.Вставить("СрокОтгрузкиСравнение", СрокОтгрузкиСравнение);
	
	ПостроительОтчета.Параметры.Вставить("СтатусТары",   Перечисления.СтатусыПартийТоваров.ВозвратнаяТара);
	
	ПостроительОтчета.Параметры.Вставить("ДатаНач",      ДатаНачала);
	ПостроительОтчета.Параметры.Вставить("ДатаКон",      ДатаОкончания);
	ПостроительОтчета.Параметры.Вставить("ДатаОплаты",   ДатаОплаты);
	ПостроительОтчета.Параметры.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	ПостроительОтчета.Параметры.Вставить("ТекущаяДата",  ТекущаяДата());
	ПостроительОтчета.Параметры.Вставить("ПустаяДата",   Дата('00010101'));
	
	ПостроительОтчета.Параметры.Вставить("ТребуемаяДатаОплаты",   НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОплаты));
	ПостроительОтчета.Параметры.Вставить("ТребуемаяДатаОтгрузки", НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОтгрузки));

КонецПроцедуры // ЗаполнитьПараметрыПостроителя()

// Заполняет отбор построителя по умолчанию.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьОтборПостроителя(ПостроительОтчета)
	
	// Добавим поля отбора по умолчанию
	Поле = ПостроительОтчета.Отбор.Добавить("Контрагент", "Контрагент", "Контрагент");
	
	Поле.Установить(ДокументОбъект.Контрагент);
	
	Поле = ПостроительОтчета.Отбор.Добавить("ДоговорКонтрагента", "ДоговорКонтрагента", "Договор");
	
	Поле.Установить(ДокументОбъект.ДоговорКонтрагента);
	
	Поле = ПостроительОтчета.Отбор.Добавить("Организация", "Организация", "Организация");
	
	Поле.Установить(ДокументОбъект.Организация);
	
	Поле = ПостроительОтчета.Отбор.Добавить("ВалютаДокумента", "ВалютаДокумента", "Валюта документа");
	
	Поле.Установить(ДокументОбъект.ВалютаДокумента);
	
	Поле = ПостроительОтчета.Отбор.Добавить("Ответственный", "Ответственный", "Ответственный");
	Поле.Установить(ДокументОбъект.Ответственный.Ссылка);
	Поле.Использование = Ложь;

	Поле = ПостроительОтчета.Отбор.Добавить("СкладГруппа", "СкладГруппа", "Склад/группа");
	Поле.Использование = Ложь;
	
КонецПроцедуры // ЗаполнитьОтборПостроителя()

// Устанавливает видимость указанной страницы.
//
Процедура ПерейтиНаСтраницу(ИмяСтраницы)
	
	Если ИмяСтраницы = "Страница1" Тогда
		Панель.Страницы.Страница1.Видимость = Истина;
		Панель.Страницы.Страница2.Видимость = Ложь;
		Панель.ТекущаяСтраница = Панель.Страницы.Страница1;
		ЭтаФорма.Заголовок = "Отобрать заказы покупателей";
	Иначе
		Панель.Страницы.Страница1.Видимость = Ложь;
		Панель.Страницы.Страница2.Видимость = Истина;
		Панель.ТекущаяСтраница = Панель.Страницы.Страница2;
		ЭтаФорма.Заголовок = "Заполнить по заказам покупателей";
	КонецЕсли;

КонецПроцедуры // ПерейтиНаСтраницу()

// Устанавливает доступность элементов зависимости от режима заполнения.
//
Процедура УстановитьДоступностьЭлементовПанелиОтбораНоменклатуры()
	
	Если НеЗаполнятьТовары Тогда
		ЭлементыФормы.ФлажокТовары.Доступность = Ложь;
		ПоказыватьТовары = Ложь;
	Иначе
		ЭлементыФормы.ФлажокТовары.Доступность = Истина;
	КонецЕсли;
	
	Если НеЗаполнятьУслуги Тогда
		ЭлементыФормы.ФлажокУслуги.Доступность = Ложь;
		ПоказыватьУслуги = Ложь;
	Иначе
		ЭлементыФормы.ФлажокУслуги.Доступность = Истина;
	КонецЕсли;
	
	Если мИспользоватьТару Тогда
		ЭлементыФормы.ФлажокТара.Видимость = Истина;
		Если НеЗаполнятьТару Тогда
			ЭлементыФормы.ФлажокТара.Доступность = Ложь;
			ПоказыватьТару = Ложь;
		Иначе
			ЭлементыФормы.ФлажокТара.Доступность = Истина;
		КонецЕсли;
	Иначе
		ЭлементыФормы.ФлажокТара.Видимость = Ложь;
		НеЗаполнятьТару = Истина;
		ПоказыватьТару = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьЭлементовПанелиОтбораНоменклатуры()

// Заполняет табличные поля Товары, Тара, Услуги номенклатурой отобранных заказов.
//
Процедура ЗаполнитьПанельОтбораНоменклатуры()

	// Заполнить табличные поля:
	// Товары:

	Если НеЗаполнятьТовары <> Истина Тогда
		
		ЗапросПоТоварам = Новый Запрос;

		Если мЧастичноеЗаполнение Тогда
			ЗапросПоТоварам.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
		КонецЕсли;

		ЗапросПоТоварам.УстановитьПараметр("ДоговорКонтрагента", ДокументОбъект.ДоговорКонтрагента);
		ЗапросПоТоварам.УстановитьПараметр("СписокЗаказов",      СписокЗаказов);
		ЗапросПоТоварам.УстановитьПараметр("СтатусТары",         Перечисления.СтатусыПартийТоваров.ВозвратнаяТара);

		ЗапросПоТоварам.Текст = "
		|ВЫБРАТЬ
		|	Истина                                                КАК Пометка,
		|	ВнутреннийЗапрос.Номенклатура.Код                     КАК Код,
		|	ВнутреннийЗапрос.Номенклатура.Артикул                 КАК Артикул,
		|	ВнутреннийЗапрос.Номенклатура                         КАК Номенклатура,
		|	ВнутреннийЗапрос.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	ВнутреннийЗапрос.ХарактеристикаНоменклатуры           КАК ХарактеристикаНоменклатуры,
		|	ВнутреннийЗапрос.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|	СУММА(ВнутреннийЗапрос.Запланировано)                 КАК Запланировано,"
		+ ?(мЧастичноеЗаполнение, "                               
		|	СУММА(ВнутреннийЗапрос.Заполнено)                     КАК Заполнено,", "") + "
		|	СУММА(ВнутреннийЗапрос.ОсталосьОтгрузить)             КАК ОсталосьОтгрузить,
		|	0                                                     КАК ДобавитьВДокумент
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		ОстаткиЗаказ.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура                         КАК Номенклатура,
		|		ОстаткиЗаказ.ХарактеристикаНоменклатуры           КАК ХарактеристикаНоменклатуры,
		|		МАКСИМУМ(ОстаткиЗаказ.КоличествоКонечныйОстаток)  КАК ОсталосьОтгрузить,"
		+ ?(мЧастичноеЗаполнение, "                               
		|		СУММА(ДокументСчет.Количество)                    КАК Заполнено,", "") + "
		|		СУММА(ОстаткиЗаказ.КоличествоПриход)              КАК Запланировано
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.ОстаткиИОбороты(,,,, ДоговорКонтрагента = &ДоговорКонтрагента 
		|		                                              И ЗаказПокупателя В (&СписокЗаказов)
		|		                                              И СтатусПартии <> &СтатусТары
		|		                                              И Номенклатура.Услуга <> Истина) КАК ОстаткиЗаказ
		|" + ?(мЧастичноеЗаполнение, "
		|ЛЕВОЕ СОЕДИНЕНИЕ // колонка заполнено
		|	Документ.СчетНаОплатуПокупателю.Товары КАК ДокументСчет
		|ПО
		|	ДокументСчет.Ссылка                       = &ДокументСсылка
		|	И ОстаткиЗаказ.ЗаказПокупателя            = ДокументСчет.ЗаказПокупателя
		|	И ОстаткиЗаказ.Номенклатура               = ДокументСчет.Номенклатура
		|	И ОстаткиЗаказ.ХарактеристикаНоменклатуры = ДокументСчет.ХарактеристикаНоменклатуры", "") + "
		|
		|	СГРУППИРОВАТЬ ПО
		|		ОстаткиЗаказ.ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура,
		|		ОстаткиЗаказ.ХарактеристикаНоменклатуры
		|
		|	) КАК ВнутреннийЗапрос
		|ГДЕ
		|	Запланировано > 0
		|	И ОсталосьОтгрузить >= 0
		|
		|	СГРУППИРОВАТЬ ПО
		|	ВнутреннийЗапрос.ЗаказПокупателя,
		|	ВнутреннийЗапрос.Номенклатура,
		|	ВнутреннийЗапрос.ХарактеристикаНоменклатуры
		|";

		Товары = ЗапросПоТоварам.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл

			СтрокаТабличнойЧасти.Запланировано        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Запланировано),     0, СтрокаТабличнойЧасти.Запланировано);
			Если мЧастичноеЗаполнение Тогда
				СтрокаТабличнойЧасти.Заполнено        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Заполнено),         0, СтрокаТабличнойЧасти.Заполнено);
			КонецЕсли;
			СтрокаТабличнойЧасти.ОсталосьОтгрузить    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ОсталосьОтгрузить), 0, СтрокаТабличнойЧасти.ОсталосьОтгрузить);
			СтрокаТабличнойЧасти.ДобавитьВДокумент    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДобавитьВДокумент), 0, СтрокаТабличнойЧасти.ДобавитьВДокумент);

		КонецЦикла;
		
		ЗаполнитьСтолбецДобавитьВДокумент(Товары, "ОсталосьОтгрузить");
		
	КонецЕсли;
	
	// Возвратная тара:

	Если НеЗаполнятьТару <> Истина Тогда
	
		ЗапросПоТаре = Новый Запрос;

		Если мЧастичноеЗаполнение Тогда
			ЗапросПоТаре.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
		КонецЕсли;

		ЗапросПоТаре.УстановитьПараметр("ДоговорКонтрагента", ДокументОбъект.ДоговорКонтрагента);
		ЗапросПоТаре.УстановитьПараметр("СписокЗаказов",      СписокЗаказов);
		ЗапросПоТаре.УстановитьПараметр("СтатусТары",         Перечисления.СтатусыПартийТоваров.ВозвратнаяТара);

		ЗапросПоТаре.Текст = "
		|ВЫБРАТЬ
		|	Истина                                                КАК Пометка,
		|	ВнутреннийЗапрос.Номенклатура.Код                     КАК Код,
		|	ВнутреннийЗапрос.Номенклатура.Артикул                 КАК Артикул,
		|	ВнутреннийЗапрос.Номенклатура                         КАК Номенклатура,
		|	ВнутреннийЗапрос.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	ВнутреннийЗапрос.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|	СУММА(ВнутреннийЗапрос.Запланировано)                 КАК Запланировано,"
		+ ?(мЧастичноеЗаполнение, "                               
		|	СУММА(ВнутреннийЗапрос.Заполнено)                     КАК Заполнено,", "") + "
		|	СУММА(ВнутреннийЗапрос.ОсталосьОтгрузить)             КАК ОсталосьОтгрузить,
		|	0                                                     КАК ДобавитьВДокумент
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		ОстаткиЗаказ.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура                         КАК Номенклатура,
		|		МАКСИМУМ(ОстаткиЗаказ.КоличествоКонечныйОстаток)  КАК ОсталосьОтгрузить,"
		+ ?(мЧастичноеЗаполнение, "                               
		|		СУММА(ДокументСчет.Количество)                    КАК Заполнено,", "") + "
		|		СУММА(ОстаткиЗаказ.КоличествоПриход)              КАК Запланировано
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.ОстаткиИОбороты(,,,, ДоговорКонтрагента = &ДоговорКонтрагента 
		|		                                              И ЗаказПокупателя В (&СписокЗаказов)
		|		                                              И СтатусПартии = &СтатусТары
		|		                                              И Номенклатура.Услуга <> Истина) КАК ОстаткиЗаказ
		|" + ?(мЧастичноеЗаполнение, "
		|ЛЕВОЕ СОЕДИНЕНИЕ // колонка заполнено
		|	Документ.СчетНаОплатуПокупателю.Товары КАК ДокументСчет
		|ПО
		|	ДокументСчет.Ссылка            = &ДокументСсылка
		|	И ОстаткиЗаказ.ЗаказПокупателя = ДокументСчет.ЗаказПокупателя
		|	И ОстаткиЗаказ.Номенклатура    = ДокументСчет.Номенклатура", "") + "
		|
		|	СГРУППИРОВАТЬ ПО
		|		ОстаткиЗаказ.ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура
		|
		|	) КАК ВнутреннийЗапрос
		|ГДЕ
		|	Запланировано > 0
		|	И ОсталосьОтгрузить >= 0
		|
		|	СГРУППИРОВАТЬ ПО
		|	ВнутреннийЗапрос.ЗаказПокупателя,
		|	ВнутреннийЗапрос.Номенклатура
		|";

		ВозвратнаяТара = ЗапросПоТаре.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТабличнойЧасти Из ВозвратнаяТара Цикл

			СтрокаТабличнойЧасти.Запланировано        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Запланировано),     0, СтрокаТабличнойЧасти.Запланировано);
			Если мЧастичноеЗаполнение Тогда
				СтрокаТабличнойЧасти.Заполнено        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Заполнено),         0, СтрокаТабличнойЧасти.Заполнено);
			КонецЕсли;
			СтрокаТабличнойЧасти.ОсталосьОтгрузить    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ОсталосьОтгрузить), 0, СтрокаТабличнойЧасти.ОсталосьОтгрузить);
			СтрокаТабличнойЧасти.ДобавитьВДокумент    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДобавитьВДокумент), 0, СтрокаТабличнойЧасти.ДобавитьВДокумент);

		КонецЦикла;
		
		ЗаполнитьСтолбецДобавитьВДокумент(ВозвратнаяТара, "ОсталосьОтгрузить");
		
	КонецЕсли;
		
	// Услуги:
		
	Если НеЗаполнятьУслуги <> Истина Тогда
	
		ЗапросПоУслугам = Новый Запрос;

		Если мЧастичноеЗаполнение Тогда
			ЗапросПоУслугам.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
		КонецЕсли;

		ЗапросПоУслугам.УстановитьПараметр("ДоговорКонтрагента", ДокументОбъект.ДоговорКонтрагента);
		ЗапросПоУслугам.УстановитьПараметр("СписокЗаказов",      СписокЗаказов);

		ЗапросПоУслугам.Текст = "
		|ВЫБРАТЬ
		|	Истина                                                КАК Пометка,
		|	ВнутреннийЗапрос.Номенклатура.Код                     КАК Код,
		|	ВнутреннийЗапрос.Номенклатура.Артикул                 КАК Артикул,
		|	ВнутреннийЗапрос.Номенклатура                         КАК Номенклатура,
		|	ВнутреннийЗапрос.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	ВнутреннийЗапрос.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|	СУММА(ВнутреннийЗапрос.Запланировано)                 КАК Запланировано,"
		+ ?(мЧастичноеЗаполнение, "                               
		|	СУММА(ВнутреннийЗапрос.Заполнено)                     КАК Заполнено,", "") + "
		|	СУММА(ВнутреннийЗапрос.ОсталосьОтгрузить)             КАК ОсталосьОтгрузить,
		|	0                                                     КАК ДобавитьВДокумент
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		ОстаткиЗаказ.ЗаказПокупателя                      КАК ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура                         КАК Номенклатура,
		|		МАКСИМУМ(ОстаткиЗаказ.КоличествоКонечныйОстаток)  КАК ОсталосьОтгрузить,"
		+ ?(мЧастичноеЗаполнение, "                               
		|		СУММА(ДокументСчет.Количество)                    КАК Заполнено,", "") + "
		|		СУММА(ОстаткиЗаказ.КоличествоПриход)              КАК Запланировано
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.ОстаткиИОбороты(,,,, ДоговорКонтрагента = &ДоговорКонтрагента 
		|		                                              И ЗаказПокупателя В (&СписокЗаказов)
		|		                                              И Номенклатура.Услуга = Истина) КАК ОстаткиЗаказ
		|" + ?(мЧастичноеЗаполнение, "
		|ЛЕВОЕ СОЕДИНЕНИЕ // колонка заполнено
		|	Документ.СчетНаОплатуПокупателю.Товары КАК ДокументСчет
		|ПО
		|	ДокументСчет.Ссылка            = &ДокументСсылка
		|	И ОстаткиЗаказ.ЗаказПокупателя = ДокументСчет.ЗаказПокупателя
		|	И ОстаткиЗаказ.Номенклатура    = ДокументСчет.Номенклатура", "") + "
		|
		|	СГРУППИРОВАТЬ ПО
		|		ОстаткиЗаказ.ЗаказПокупателя,
		|		ОстаткиЗаказ.Номенклатура
		|
		|	) КАК ВнутреннийЗапрос
		|ГДЕ
		|	Запланировано > 0
		|	И ОсталосьОтгрузить >= 0
		|
		|	СГРУППИРОВАТЬ ПО
		|	ВнутреннийЗапрос.ЗаказПокупателя,
		|	ВнутреннийЗапрос.Номенклатура
		|";

		Услуги = ЗапросПоУслугам.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТабличнойЧасти Из Услуги Цикл

			СтрокаТабличнойЧасти.Запланировано        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Запланировано),     0, СтрокаТабличнойЧасти.Запланировано);
			Если мЧастичноеЗаполнение Тогда
				СтрокаТабличнойЧасти.Заполнено        = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Заполнено),         0, СтрокаТабличнойЧасти.Заполнено);
			КонецЕсли;
			СтрокаТабличнойЧасти.ОсталосьОтгрузить    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ОсталосьОтгрузить), 0, СтрокаТабличнойЧасти.ОсталосьОтгрузить);
			СтрокаТабличнойЧасти.ДобавитьВДокумент    = ?(не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДобавитьВДокумент), 0, СтрокаТабличнойЧасти.ДобавитьВДокумент);

		КонецЦикла;
		
		ЗаполнитьСтолбецДобавитьВДокумент(Услуги, "ОсталосьОтгрузить");
		
	КонецЕсли;

	// Установить видимость колонок "ХарактеристикаНоменклатуры"
	РаботаСДиалогами.УстановитьВидимостьХарактеристикиНоменклатуры(ЭлементыФормы.Товары.Колонки);

	// Установить видимость колонок "Заполнено"
	ЭлементыФормы.Товары.Колонки.Заполнено.Видимость         = мЧастичноеЗаполнение;
	ЭлементыФормы.Услуги.Колонки.Заполнено.Видимость         = мЧастичноеЗаполнение;
	ЭлементыФормы.ВозвратнаяТара.Колонки.Заполнено.Видимость = мЧастичноеЗаполнение;

	// Установить видимость закладок
	ЭлементыФормы.ОсновнаяПанель.Страницы.Товары.Видимость = ПоказыватьТовары;
	ЭлементыФормы.ОсновнаяПанель.Страницы.Услуги.Видимость = ПоказыватьУслуги;
	ЭлементыФормы.ОсновнаяПанель.Страницы.Тара.Видимость   = ПоказыватьТару;

КонецПроцедуры // ЗаполнитьПанельОтбораНоменклатуры()

// Выполняет перенос отобранных позиций номенклатуры в документ счет (в режиме заполнения)
//
Процедура ЗаполнитьТабличныеЧастиДокумента()

	СтруктураВозвращаемыхЗначений = Новый Структура();

	СтруктураВозвращаемыхЗначений.Вставить("Команда",            "ЗаполнениеТабличныхЧастей");

	СтруктураВозвращаемыхЗначений.Вставить("ЗаполнятьТовары",    ПоказыватьТовары);
	Если ПоказыватьТовары Тогда
		КопияТовары = Товары.Скопировать();
		КопияТовары.Колонки.ДобавитьВДокумент.Имя = "Количество";

		НомерСтрокиТаблицы = 0;
		Пока НомерСтрокиТаблицы < КопияТовары.Количество() Цикл

			СтрокаТаблицы = КопияТовары.Получить(НомерСтрокиТаблицы);

			СтрокаКоличество = ?(не ЗначениеЗаполнено(СтрокаТаблицы.Количество), 0, СтрокаТаблицы.Количество);
			
			Если НЕ СтрокаТаблицы.Пометка
				ИЛИ СтрокаКоличество <= 0 Тогда
				КопияТовары.Удалить(СтрокаТаблицы);
				Продолжить;
			КонецЕсли;

			НомерСтрокиТаблицы = НомерСтрокиТаблицы + 1;

		КонецЦикла;
		СтруктураВозвращаемыхЗначений.Вставить("Товары",         КопияТовары);
	КонецЕсли;

	СтруктураВозвращаемыхЗначений.Вставить("ЗаполнятьТару",      ПоказыватьТару);
	Если ПоказыватьТару Тогда
		КопияВозвратнаяТара = ВозвратнаяТара.Скопировать();
		КопияВозвратнаяТара.Колонки.ДобавитьВДокумент.Имя = "Количество";

		НомерСтрокиТаблицы = 0;
		Пока НомерСтрокиТаблицы < КопияВозвратнаяТара.Количество() Цикл

			СтрокаТаблицы = КопияВозвратнаяТара.Получить(НомерСтрокиТаблицы);

			СтрокаКоличество = ?(не ЗначениеЗаполнено(СтрокаТаблицы.Количество), 0, СтрокаТаблицы.Количество);
			
			Если НЕ СтрокаТаблицы.Пометка
				ИЛИ СтрокаКоличество <= 0 Тогда
				КопияВозвратнаяТара.Удалить(СтрокаТаблицы);
				Продолжить;
			КонецЕсли;

			НомерСтрокиТаблицы = НомерСтрокиТаблицы + 1;

		КонецЦикла;
		СтруктураВозвращаемыхЗначений.Вставить("ВозвратнаяТара", КопияВозвратнаяТара);
	КонецЕсли;

	СтруктураВозвращаемыхЗначений.Вставить("ЗаполнятьУслуги",    ПоказыватьУслуги);
	Если ПоказыватьУслуги Тогда
		КопияУслуги = Услуги.Скопировать();
		КопияУслуги.Колонки.ДобавитьВДокумент.Имя = "Количество";

		НомерСтрокиТаблицы = 0;
		Пока НомерСтрокиТаблицы < КопияУслуги.Количество() Цикл

			СтрокаТаблицы = КопияУслуги.Получить(НомерСтрокиТаблицы);

			СтрокаКоличество = ?(не ЗначениеЗаполнено(СтрокаТаблицы.Количество), 0, СтрокаТаблицы.Количество);
			
			Если НЕ СтрокаТаблицы.Пометка
				ИЛИ СтрокаКоличество <= 0 Тогда
				КопияУслуги.Удалить(СтрокаТаблицы);
				Продолжить;
			КонецЕсли;

			НомерСтрокиТаблицы = НомерСтрокиТаблицы + 1;

		КонецЦикла;

		СтруктураВозвращаемыхЗначений.Вставить("Услуги",         КопияУслуги);
	КонецЕсли;

	ОповеститьОВыборе(СтруктураВозвращаемыхЗначений);

КонецПроцедуры // ЗаполнитьТабличныеЧастиДокумента()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Если не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		// Или форма открыта некорректно, или документ еще не был записан
		ОбщегоНазначения.СообщитьОбОшибке("Документ не записан. Заполнение невозможно!", Отказ);
		Возврат;
	ИначеЕсли ДокументОбъект.Проведен И ДокументОбъект.Модифицированность() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Документ не записан. Заполнение невозможно!", Отказ);
		Возврат;
	ИначеЕсли не ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбран контрагент. Заполнение невозможно!", Отказ);
		Возврат;
	ИначеЕсли не ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбран договор. Заполнение невозможно!", Отказ);
		Возврат;
	ИначеЕсли не ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация. Заполнение невозможно!", Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	// Заполнить список доступных вариантов отбора
	УстановитьСтруктуруВариантовОтбора();
	
	// Создать и заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
	// Установить доступность кнопок командной панели таблицы Заказов
	УстановитьДоступностьКнопокПанелиЗаказов();
	
	// Установим конец периода на конец дня, предшествующего дате документа закрытия
	ДатаКон = КонецДня(ДокументОбъект.Дата);
	ДатаКонПриИзменении(ЭлементыФормы.ДатаКон);

	ПерейтиНаСтраницу("Страница1");
	
	ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеЗаполнить.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события ПриИзменении элемента формы ДатаНач.
//
Процедура ДатаНачПриИзменении(Элемент)

	// Установка даты начала периода по умолчанию
	НП.УстановитьПериод(ДатаНач, КонецДня(ДатаКон), Истина);
	
КонецПроцедуры // ДатаНачПриИзменении()

// Обработчик события ПриИзменении элемента формы ДатаКон.
//
Процедура ДатаКонПриИзменении(Элемент)

	// Установка даты конца периода по умолчанию
	Если КонецДня(ДатаКон) > КонецДня(ДокументОбъект.Дата) Тогда
		// Установим конец периода на конец дня даты документа счет
		ДатаКон = КонецДня(ДокументОбъект.Дата);
		Предупреждение("Дата окончания периода не должна быть больше даты документа!",,ЭтаФорма.Заголовок);
	Иначе
		ДатаКон = КонецДня(ДатаКон);
	КонецЕсли;
	
	НП.УстановитьПериод(ДатаНач, ДатаКон, Истина);
	
КонецПроцедуры	// ДатаКонПриИзменении()

// Обработчик события Нажатие кнопки настройки периода.
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	// Установка периода отбора
	Если НП.Редактировать() Тогда
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

// Обработчик события Выбор поля ВариантОтбора.
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ПолеНастройкиВариантОтбораПриИзменении(Элемент)

	// Установить настройки диалога по варианту отбора
	Если Элемент.Значение = 1 Тогда 
		УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора();
	ИначеЕсли Элемент.Значение = 2 Тогда 
		УстановитьНастройкиПоУмолчаниюДляНеисполненныхЗаказов();
	КонецЕсли;
	
	ПриИзмененииНастроекПоУмолчанию();
	
КонецПроцедуры // ПолеНастройкиВариантОтбораПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиНаличиеРезервов.
//
Процедура ПолеНастройкиНаличиеРезервовПриИзменении(Элемент)
	
	// Если с резервами, то доступен отбор по местоположение резервов
	Если Элемент.Значение = 3 Тогда 
		ФлагВидимостиРазмещениеРезервов = Истина;
	Иначе
		ФлагВидимостиРазмещениеРезервов = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
	
КонецПроцедуры // ПолеНастройкиНаличиеРезервовПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиСрокОплатыСравнение.
//
Процедура ПолеНастройкиСрокОплатыСравнениеПриИзменении(Элемент)
	
	Если Элемент.Значение = 1 ИЛИ Элемент.Значение = 6 Тогда 
		// По значению Не важно и Не заполнена скроем поля настройки отбора 
		ФлагВидимостиПросроченоДнейОплаты = Ложь;
		ФлагВидимостиДатыОплаты = Ложь;
	ИначеЕсли Элемент.Значение = 2 Тогда 
		// По значению Просрочен доступен отбор по Количеству дней
		ФлагВидимостиПросроченоДнейОплаты = Истина;
		ФлагВидимостиДатыОплаты = Ложь;
	Иначе
		// Доступен отбор по Дате оплаты
		ФлагВидимостиПросроченоДнейОплаты = Ложь;
		ФлагВидимостиДатыОплаты = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
			
КонецПроцедуры // ПолеНастройкиСрокОплатыСравнениеПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиСрокОтгрузкиСравнение.
//
Процедура ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(Элемент)
	
	Если Элемент.Значение = 1 ИЛИ Элемент.Значение = 6 Тогда 
		// По значению Не важно и Не заполнена скроем поля настройки отбора 
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Ложь;
	ИначеЕсли Элемент.Значение = 2 Тогда 
		// По значению Просрочен доступен отбор по Количеству дней
		ФлагВидимостиПросроченоДнейОтгрузки = Истина;
		ФлагВидимостиДатыОтгрузки = Ложь;
	Иначе
		// Доступен отбор по Дате отгрузки
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
			
КонецПроцедуры // ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеПросроченоДнейОтгрузки.
//
Процедура ПолеПросроченоДнейОтгрузкиПриИзменении(Элемент)

	// Сохраним значение поля данного ВидаЗаполнения
	СохранитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВариантОтбора), ПросроченоДнейОтгрузки);

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ПолеПросроченоДнейОплаты.
//
Процедура ПолеПросроченоДнейОплатыПриИзменении(Элемент)
	
	// Сохраним значение поля для данного ВидаЗаполнения
	СохранитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВариантОтбора), ПросроченоДнейОплаты);
	
КонецПроцедуры

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура ОбновлениеОтображения()

	// Подсчитаем количество строк в табличных частях.
	СтраницаПанели = ЭлементыФормы.ОсновнаяПанель.Страницы;
	
	СтраницаПанели.Товары.Заголовок = "Товары (" + Товары.Количество() + " поз.)";
	СтраницаПанели.Тара  .Заголовок = "Тара ("   + ВозвратнаяТара.Количество() + " поз.)";
	СтраницаПанели.Услуги.Заголовок = "Услуги (" + Услуги.Количество() + " поз.)";

КонецПроцедуры // ОбновлениеОтображения()

// Обработчик события Выбор, открывает Документ по двойному щелчку в списке.
//
Процедура ТабличноеПолеЗаказыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	Если (Колонка.Имя <> "Переносить") Тогда
		Если ВыбраннаяСтрока.Ссылка <> Неопределено Тогда
			ВыбраннаяСтрока.Ссылка.ПолучитьФорму(, ЭтаФорма).Открыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеЗаказыВыбор()

// Обработчик события ПриИзменении элемента формы ИспользоватьСвойстваИКатегории.
//
Процедура ИспользоватьСвойстваИКатегорииПриИзменении(Элемент)
	
	// Заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
КонецПроцедуры

// Обработчик события ПередЗакрытием формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	// Отключим стандартную обработку для этой формы
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ФлажокТовары
//
Процедура ФлажокТоварыПриИзменении(Элемент)

	// Установим видимость таблицы Товары по значению флажка
	Если ПоказыватьТовары Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Товары.Видимость = Истина;
	ИначеЕсли ПоказыватьТару ИЛИ ПоказыватьУслуги Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Товары.Видимость = Ложь;
	Иначе
		ЭлементыФормы.ОсновнаяПанель.Страницы.Товары.Видимость = Истина;
		ПоказыватьТовары = Истина;
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ФлажокУслуги
//
Процедура ФлажокУслугиПриИзменении(Элемент)

	// Установим видимость таблицы Услуги по значению флажка
	Если ПоказыватьУслуги Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Услуги.Видимость = Истина;
	ИначеЕсли ПоказыватьТовары ИЛИ ПоказыватьТару Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Услуги.Видимость = Ложь;
	Иначе
		ЭлементыФормы.ОсновнаяПанель.Страницы.Услуги.Видимость = Истина;
		ПоказыватьУслуги = Истина;
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ФлажокТара
//
Процедура ФлажокТараПриИзменении(Элемент)

	// Установим видимость таблицы Тара по значению флажка
	Если ПоказыватьТару Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Тара.Видимость = Истина;
	ИначеЕсли ПоказыватьТовары ИЛИ ПоказыватьУслуги Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Тара.Видимость = Ложь;
	Иначе
		ЭлементыФормы.ОсновнаяПанель.Страницы.Тара.Видимость = Истина;
		ПоказыватьТару = Истина;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "ПриВыводеСтроки" табличного поля "Заказы".
//
Процедура ТабличноеПолеЗаказыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.НеЗаполнять Тогда
		ОформлениеСтроки.Ячейки.Переносить.ТолькоПросмотр = Истина;
		ОформлениеСтроки.ЦветФона = мЦветГруппы;
	Иначе
		ОформлениеСтроки.Ячейки.Переносить.ТолькоПросмотр = Ложь;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "ПриВыводеСтроки" табличного поля "Отбор".
//
Процедура ТабличноеПолеОтборПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ДанныеСтроки.Имя = "Контрагент" 
	 ИЛИ ДанныеСтроки.Имя = "ДоговорКонтрагента" 
	 ИЛИ ДанныеСтроки.Имя = "Организация"
	 ИЛИ ДанныеСтроки.Имя = "ВалютаДокумента" Тогда
		ОформлениеСтроки.Ячейки.Использование.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Имя.ТолькоПросмотр           = Истина;
		ОформлениеСтроки.Ячейки.ВидСравнения.ТолькоПросмотр  = Истина;
		ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр      = Истина;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ТОВАРЫ

// Процедура - обработчик события "ПередНачаломДобавления" табличного поля "Товары".
//
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

КонецПроцедуры // ТоварыПередНачаломДобавления()

// Процедура - обработчик события "ПередУдалением" табличного поля "Товары".
//
Процедура ТоварыПередУдалением(Элемент, Отказ)

	Отказ = Истина;

КонецПроцедуры // ТоварыПередУдалением()

// Процедура - обработчик события "ПриВыводеСтроки" табличного поля "Товары".
//
Процедура ТоварыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;

    ДобавитьВДокумент = ДанныеСтроки.ДобавитьВДокумент;
	ОсталосьОтгрузить = ДанныеСтроки.ОсталосьОтгрузить;
	Заполнено = ?(мЧастичноеЗаполнение, ДанныеСтроки.Заполнено, 0);

	Если ДобавитьВДокумент > (ОсталосьОтгрузить - Заполнено) Тогда
		
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.Шрифт = мЖирныйШрифт;
		
		Если мКонтролироватьПревышениеОбъемаЗаказа Тогда
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мКрасныйЦвет;
		Иначе
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мСинийЦвет;
		КонецЕсли;
	КонецЕсли;

	Если ДобавитьВДокумент < (ОсталосьОтгрузить - Заполнено) Тогда
		ОформлениеСтроки.Ячейки.ОсталосьОтгрузить.Шрифт = мЖирныйШрифт;
	КонецЕсли;

	Если не ЗначениеЗаполнено(ДанныеСтроки.ЗаказПокупателя) Тогда
		ДанныеСтроки.Пометка = Ложь;
		ДанныеСтроки.ДобавитьВДокумент = 0;
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Истина;
	Иначе
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Ложь;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Ложь;
	КонецЕсли;

КонецПроцедуры // ТоварыПриВыводеСтроки()
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ВОЗВРАТНАЯ ТАРА

// Процедура - обработчик события "ПередНачаломДобавления" табличной части "Возвратная тара".
//
Процедура ВозвратнаяТараПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

КонецПроцедуры // ВозвратнаяТараПередНачаломДобавления()

// Процедура - обработчик события "ПередУдалением" табличной части "Возвратная тара".
//
Процедура ВозвратнаяТараПередУдалением(Элемент, Отказ)

	Отказ = Истина;

КонецПроцедуры // ВозвратнаяТараПередУдалением()

// Процедура - обработчик события "ПриВыводеСтроки" табличной части "Возвратная тара".
//
Процедура ВозвратнаяТараПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;

    ДобавитьВДокумент = ДанныеСтроки.ДобавитьВДокумент;
	ОсталосьОтгрузить = ДанныеСтроки.ОсталосьОтгрузить;
	Заполнено = ?(мЧастичноеЗаполнение, ДанныеСтроки.Заполнено, 0);

	Если ДобавитьВДокумент > (ОсталосьОтгрузить - Заполнено) Тогда
		
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.Шрифт = мЖирныйШрифт;
		
		Если мКонтролироватьПревышениеОбъемаЗаказа Тогда
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мКрасныйЦвет;
		Иначе
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мСинийЦвет;
		КонецЕсли;
	КонецЕсли;

	Если ДобавитьВДокумент < (ОсталосьОтгрузить - Заполнено) Тогда
		ОформлениеСтроки.Ячейки.ОсталосьОтгрузить.Шрифт = мЖирныйШрифт;
	КонецЕсли;

	Если не ЗначениеЗаполнено(ДанныеСтроки.ЗаказПокупателя) Тогда
		ДанныеСтроки.Пометка = Ложь;
		ДанныеСтроки.ДобавитьВДокумент = 0;
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Истина;
	Иначе
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Ложь;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Ложь;
	КонецЕсли;

КонецПроцедуры // ВозвратнаяТараПриВыводеСтроки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ УСЛУГИ

// Процедура - обработчик события "ПередНачаломДобавления" табличной части "Услуги".
//
Процедура УслугиПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

КонецПроцедуры // УслугиПередНачаломДобавления()

// Процедура - обработчик события "ПередУдалением" табличной части "Услуги".
//
Процедура УслугиПередУдалением(Элемент, Отказ)

	Отказ = Истина;

КонецПроцедуры // УслугиПередУдалением()

// Процедура - обработчик события "ПриВыводеСтроки" табличной части "Услуги".
//
Процедура УслугиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
    ДобавитьВДокумент = ДанныеСтроки.ДобавитьВДокумент;
	ОсталосьОтгрузить = ДанныеСтроки.ОсталосьОтгрузить;
	Заполнено = ?(мЧастичноеЗаполнение, ДанныеСтроки.Заполнено, 0);
	
	Если ДобавитьВДокумент > (ОсталосьОтгрузить - Заполнено) Тогда
		
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.Шрифт = мЖирныйШрифт;
		
		Если мКонтролироватьПревышениеОбъемаЗаказа Тогда
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мКрасныйЦвет;
		Иначе
			ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ЦветТекста = мСинийЦвет;
		КонецЕсли;
	КонецЕсли;

	Если ДобавитьВДокумент < (ОсталосьОтгрузить - Заполнено) Тогда
		ОформлениеСтроки.Ячейки.ОсталосьОтгрузить.Шрифт = мЖирныйШрифт;
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(ДанныеСтроки.ЗаказПокупателя) Тогда
		ДанныеСтроки.Пометка = Ложь;
		ДанныеСтроки.ДобавитьВДокумент = 0;
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Истина;
	Иначе
		ОформлениеСтроки.Ячейки.ДобавитьВДокумент.ТолькоПросмотр = Ложь;
		ОформлениеСтроки.Ячейки.Пометка.ТолькоПросмотр = Ложь;
	КонецЕсли;

КонецПроцедуры // УслугиПриВыводеСтроки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ТЧ ЗАКАЗЫ

// Обработчик события Нажатие кнопки ВключитьВсе командной панели табличного поля Заказы.
//
Процедура КоманднаяПанельЗаказыДействиеВключитьВсе(Кнопка)
	
	// Установим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Истина;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВключитьВсе()

// Обработчик события Нажатие кнопки ВыключитьВсе командной панели табличного поля Заказы.
//
Процедура КоманднаяПанельЗаказыДействиеВыключитьВсе(Кнопка)
	
	// Сбросим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Ложь;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВыключитьВсе()

// Обработчик события Нажатие кнопки Инвертировать командной панели табличного поля Заказы.
//
Процедура КоманднаяПанельЗаказыДействиеИнвертировать(Кнопка)

	// Инвертиртируем отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = НЕ Строка.Переносить;
	КонецЦикла;

КонецПроцедуры

// Обработчик события нажатие кнопки Анализ командной панели табличного поля Заказы.
// вызывает анализ текущего состояния заказа.
//
Процедура ДействияФормыДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока <> Неопределено Тогда
		
		ОткрытьОтчетАнализЗаказа(ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока.Ссылка);
		
	КонецЕсли;

КонецПроцедуры // ДействияФормыДействиеАнализ()

// Обработчик события Нажатие кнопки Заполнить.
//
Процедура КоманднаяПанельЗаказыДействиеЗаполнить(Кнопка)

	//Произведем отбор заказов по условиям отбора
	Если (ДатаНач > ДатаКон) И (ДатаКон <> Дата('00010101')) Тогда
		Предупреждение("Дата начала периода не может быть больше даты окончания периода!",,ЭтаФорма.Заголовок);
		Возврат;
	КонецЕсли;
	
	// Заполним Построитель параметрами из полей формы и выполним отбор
	ДатаНачала = НачалоДня(ДатаНач);
	ДатаОкончания = КонецДня(ДатаКон);
	ЗаполнитьПараметрыПостроителя(ПостроительОтчета);
	ПостроительОтчета.Выполнить();
	
	// Заполним список отобранных заказов
	ТабличноеПолеЗаказы.Очистить();
	ТабличноеПолеЗаказы = ПостроительОтчета.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	УстановитьДоступностьКнопокПанелиЗаказов();

	Если ТабличноеПолеЗаказы.Количество() > 0 Тогда
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ДействиеДалее.КнопкаПоУмолчанию = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеЗаполнить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельЗаказыДействиеЗаполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ТЧ ТОВАРЫ

// Обработчик события Нажатие кнопки "Установить все флажки" командной панели табличного поля Товары. 
// командной панели табличного поля "Товары".
//
Процедура КоманднаяПанельТоварыДействиеУстановитьФлажки(Кнопка)

	УстановитьСнятьФлажки(Истина, Товары);

КонецПроцедуры // КоманднаяПанельТоварыДействиеУстановитьФлажки()

// Обработчик события Нажатие кнопки "Снять все флажки" командной панели табличного поля Товары. 
// командной панели табличного поля "Товары".
//
Процедура КоманднаяПанельТоварыДействиеСнятьФлажки(Кнопка)

	УстановитьСнятьФлажки(Ложь, Товары);

КонецПроцедуры // КоманднаяПанельТоварыДействиеСнятьФлажки()

// Обработчик события Нажатие кнопки Инвертировать командной панели табличного поля Товары.
//
Процедура КоманднаяПанельТоварыДействиеИнвертировать(Кнопка)

	// Инвертиртируем отметки в таблице Товары формы.
	Для Каждого Строка Из Товары Цикл
		Строка.Пометка = НЕ Строка.Пометка;
	КонецЦикла;

КонецПроцедуры

// Обработчик события Нажатие кнопки Анализ командной панели табличного поля Товары.
// вызывает анализ текущего состояния заказа.
//
Процедура КоманднаяПанельТоварыДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.Товары.ТекущаяСтрока <> Неопределено Тогда
		ОткрытьОтчетАнализЗаказа(ЭлементыФормы.Товары.ТекущаяСтрока.ЗаказПокупателя);
	КонецЕсли;

КонецПроцедуры

// Процедура вызывается при выборе пункта меню "Обнулить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Товары".
//
Процедура КоманднаяПанельТоварыОбнулить(Кнопка)

	ОбнулитьСтолбецДобавитьВДокумент(Товары);

КонецПроцедуры // КоманднаяПанельТоварыОбнулить()

// Процедура вызывается при выборе пункта меню "Заполнить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Товары".
//
Процедура КоманднаяПанельТоварыЗаполнить(Кнопка)

	ЗаполнитьСтолбецДобавитьВДокумент(Товары, "ОсталосьОтгрузить");

КонецПроцедуры // КоманднаяПанельТоварыЗаполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ТЧ ВОЗВРАТНАЯ ТАРА

// Обработчик события Нажатие кнопки "Установить все флажки" командной панели табличного поля ВозвратнаяТара.
// командной панели табличного поля "Возвратная тара".
//
Процедура КоманднаяПанельВозвратнаяТараДействиеУстановитьФлажки(Кнопка)

	УстановитьСнятьФлажки(Истина, ВозвратнаяТара);

КонецПроцедуры // КоманднаяПанельВозвратнаяТараДействиеУстановитьФлажки()

// Обработчик события Нажатие кнопки "Снять все флажки" командной панели табличного поля ВозвратнаяТара.
// командной панели табличного поля "Возвратная тара".
//
Процедура КоманднаяПанельВозвратнаяТараДействиеСнятьФлажки(Кнопка)

	УстановитьСнятьФлажки(Ложь, ВозвратнаяТара);

КонецПроцедуры // КоманднаяПанельВозвратнаяТараДействиеСнятьФлажки()

// Обработчик события Нажатие кнопки Инвертировать командной панели табличного поля ВозвратнаяТара.
//
Процедура КоманднаяПанельВозвратнаяТараДействиеИнвертировать(Кнопка)

	// Инвертиртируем отметки в таблице ВозвратнаяТара формы.
	Для Каждого Строка Из ВозвратнаяТара Цикл
		Строка.Пометка = НЕ Строка.Пометка;
	КонецЦикла;

КонецПроцедуры

// Обработчик события Нажатие кнопки Анализ командной панели табличного поля ВозвратнаяТара.
// вызывает анализ текущего состояния заказа.
//
Процедура КоманднаяПанельВозвратнаяТараДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.ВозвратнаяТара.ТекущаяСтрока <> Неопределено Тогда
		ОткрытьОтчетАнализЗаказа(ЭлементыФормы.ВозвратнаяТара.ТекущаяСтрока.ЗаказПокупателя);
	КонецЕсли;

КонецПроцедуры

// Процедура вызывается при выборе пункта меню "Обнулить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Возвратная тара".
//
Процедура КоманднаяПанельВозвратнаяТараОбнулить(Кнопка)

	ОбнулитьСтолбецДобавитьВДокумент(ВозвратнаяТара);

КонецПроцедуры // КоманднаяПанельВозвратнаяТараОбнулить()

// Процедура вызывается при выборе пункта меню "Заполнить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Возвратная тара".
//
Процедура КоманднаяПанельВозвратнаяТараЗаполнить(Кнопка)

	ЗаполнитьСтолбецДобавитьВДокумент(ВозвратнаяТара, "ОсталосьОтгрузить");

КонецПроцедуры // КоманднаяПанельВозвратнаяТараЗаполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ТЧ УСЛУГИ

// Обработчик события Нажатие кнопки "Установить все флажки" командной панели табличного поля Услуги.
// командной панели табличного поля "Услуги".
//
Процедура КоманднаяПанельУслугиДействиеУстановитьФлажки(Кнопка)

	УстановитьСнятьФлажки(Истина, Услуги);

КонецПроцедуры // КоманднаяПанельУслугиДействиеУстановитьФлажки()

// Обработчик события Нажатие кнопки "Снять все флажки" командной панели табличного поля Услуги.
// командной панели табличного поля "Услуги".
//
Процедура КоманднаяПанельУслугиДействиеСнятьФлажки(Кнопка)

	УстановитьСнятьФлажки(Ложь, Услуги);

КонецПроцедуры // КоманднаяПанельУслугиДействиеСнятьФлажки()

// Обработчик события Нажатие кнопки Инвертировать командной панели табличного поля Услуги.
//
Процедура КоманднаяПанельУслугиДействиеИнвертировать(Кнопка)

	// Инвертиртируем отметки в таблице Услуги формы.
	Для Каждого Строка Из Услуги Цикл
		Строка.Пометка = НЕ Строка.Пометка;
	КонецЦикла;

КонецПроцедуры

// Обработчик события Нажатие кнопки Анализ командной панели табличного поля Услуги.
// вызывает анализ текущего состояния заказа.
//
Процедура КоманднаяПанельУслугиДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.Услуги.ТекущаяСтрока <> Неопределено Тогда
		ОткрытьОтчетАнализЗаказа(ЭлементыФормы.Услуги.ТекущаяСтрока.ЗаказПокупателя);
	КонецЕсли;

КонецПроцедуры

// Процедура вызывается при выборе пункта меню "Обнулить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Услуги".
//
Процедура КоманднаяПанельУслугиОбнулить(Кнопка)

	ОбнулитьСтолбецДобавитьВДокумент(Услуги);

КонецПроцедуры // КоманднаяПанельУслугиОбнулить()

// Процедура вызывается при выборе пункта меню "Заполнить столбец добавляемых в документ" 
// кнопки "Изменить" командной панели табличного поля "Услуги".
//
Процедура КоманднаяПанельУслугиЗаполнить(Кнопка)

	ЗаполнитьСтолбецДобавитьВДокумент(Услуги, "ОсталосьОтгрузить");

КонецПроцедуры // КоманднаяПанельУслугиЗаполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОСНОВНЫЕ ДЕЙСТВИЯ ФОРМЫ

// Обработчик события Нажатие кнопки Далее.
//
Процедура ОсновныеДействияФормыДействиеДалее(Кнопка)
	
	// Проверим наличие строк в таблице.
	Если ТабличноеПолеЗаказы.Количество() = 0 Тогда
		Предупреждение("Не выбраны документы!");
		Возврат;
	КонецЕсли;
	
	//Перенесем заказы в таблицу заполнения
	
	// Выберем отмеченные
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Переносить", Истина);
	НайденныеСтроки = ТабличноеПолеЗаказы.НайтиСтроки(СтруктураОтбора);
	
	// Если есть отмеченные 
	Если НайденныеСтроки.Количество() = 0 Тогда
		Предупреждение("Не выбраны документы!");
		Возврат;
	КонецЕсли;
	
	СписокЗаказов.Очистить();
	Для Каждого Строка Из НайденныеСтроки Цикл
		СписокЗаказов.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	// Установить доступные способы заполнения документа счет
	УстановитьСпособЗаполнения();
	
	// Заполнить табличные поля номенклатурой
	ЗаполнитьПанельОтбораНоменклатуры();
	
	Если Товары.Количество() = 0
		И Услуги.Количество() = 0
		И ВозвратнаяТара.Количество() = 0 Тогда
		// Заполнять нечем
		Предупреждение("По выбранным заказам покупателей отсутствует номенклатура для заполнения.");
		Возврат;
		
	КонецЕсли;
	
	ПерейтиНаСтраницу("Страница2");
	
	// Установить режим доступности и видимости для элементов страницы
	УстановитьДоступностьЭлементовПанелиОтбораНоменклатуры();
	
	// Установить доступность кнопок командных панелей
	УстановитьДоступностьКнопокПанелейТабЧастей();
	
	ЭлементыФормы.ОсновныеДействияФормы1.Кнопки.ДействиеПеренести.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры // ОсновныеДействияФормыДействиеДалее()

// Обработчик события Нажатие кнопки Назад.
//
Процедура ОсновныеДействияФормыДействиеНазад(Кнопка)
	
	ПерейтиНаСтраницу("Страница1");
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ДействиеДалее.КнопкаПоУмолчанию = Истина;

КонецПроцедуры // ОсновныеДействияФормыДействиеНазад()

// Обработчик события Нажатие кнопки Перенести.
//
Процедура ОсновныеДействияФормыДействиеПеренести(Кнопка)

	// Проверим наличие строк в таблице Товары.
	ЕстьТовары = Ложь;
	Если ПоказыватьТовары И НЕ Товары.Количество() = 0 Тогда
		Таблица = Товары.Скопировать();
		Таблица.Свернуть("Пометка", "ДобавитьВДокумент");
		Для Каждого Строка Из Таблица Цикл
			Если Строка.ДобавитьВДокумент > 0 Тогда
				ЕстьТовары = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Проверим наличие строк в таблице Услуги.
	ЕстьУслуги = Ложь;
	Если ПоказыватьУслуги И НЕ Услуги.Количество() = 0 Тогда
		Таблица = Услуги.Скопировать();
		Таблица.Свернуть("Пометка", "ДобавитьВДокумент");
		Для Каждого Строка Из Таблица Цикл
			Если Строка.ДобавитьВДокумент > 0 Тогда
				ЕстьУслуги = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Проверим наличие строк в таблице Тара.
	ЕстьТара   = Ложь;
	Если ПоказыватьТару И НЕ ВозвратнаяТара.Количество() = 0 Тогда
		Таблица = ВозвратнаяТара.Скопировать();
		Таблица.Свернуть("Пометка", "ДобавитьВДокумент");
		Для Каждого Строка Из Таблица Цикл
			Если Строка.ДобавитьВДокумент > 0 Тогда
				ЕстьТара = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЕстьТовары И НЕ ЕстьУслуги И НЕ ЕстьТара Тогда
		Предупреждение("Нет номенклатуры для добавления!");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиДокумента();

КонецПроцедуры // ОсновныеДействияФормыДействиеПеренести()

НП = Новый НастройкаПериода;

мКрасныйЦвет  = ЦветаСтиля.ЦветОтрицательногоЧисла;
мСинийЦвет    = ЦветаСтиля.ТекстИнформационнойНадписи;
мЦветГруппы   = ЦветаСтиля.ЦветФонаФормы;
мЦветТекста   = ЦветаСтиля.ЦветТекстаПоля;
мОбычныйШрифт = Новый Шрифт(,,);
мЖирныйШрифт  = Новый Шрифт(,, Истина);

мИспользоватьТару = глЗначениеПеременной("ИспользоватьВозвратнуюТару");

мЧастичноеЗаполнение = Ложь;
мКонтролироватьПревышениеОбъемаЗаказа = НЕ УправлениеДопПравамиПользователей.РазрешеноПревышениеОбъемаЗаказаПриОтгрузке();

