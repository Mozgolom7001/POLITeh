
// Процедура - обаботчик события "ПередОткрытием" Формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПостроительОтчета.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		Номенклатура.Ссылка КАК Номенклатура,
	|		&ПустаяХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|	ИЗ
	|		Справочник.Номенклатура КАК Номенклатура
	|
	|		Внутреннее соединение РегистрСведений.ИдентификаторыЭлектронногоОбмена КАК ИдентификаторыЭлектронногоОбмена
	|			ПО (ИдентификаторыЭлектронногоОбмена.Объект = Номенклатура.Ссылка)
	|	ГДЕ
	|		(НЕ Номенклатура.ЭтоГруппа)		
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХарактеристикиНоменклатуры.Владелец,
	|		ХарактеристикиНоменклатуры.Ссылка
	|	ИЗ
	|		Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|
	| 		Внутреннее соединение РегистрСведений.ИдентификаторыЭлектронногоОбмена КАК ИдентификаторыЭлектронногоОбмена
	|			ПО (ИдентификаторыЭлектронногоОбмена.Объект = ХарактеристикиНоменклатуры.Ссылка)
	|
	|	ГДЕ
	|		(НЕ ХарактеристикиНоменклатуры.Владелец.ЭтоГруппа)
	|		И ХарактеристикиНоменклатуры.Владелец.ВестиУчетПоХарактеристикам) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры";
	
	ПостроительОтчета.Параметры.Вставить("ПустаяХарактеристикаНоменклатуры", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	ПостроительОтчета.ЗаполнитьНастройки();
	
	Отбор = ПостроительОтчета.Отбор.Добавить("Номенклатура");
	Отбор.ВидСравнения = ВидСравнения.ВИерархии;
		
	Отбор = ПостроительОтчета.Отбор.Добавить("Номенклатура.ПометкаУдаления");
	Отбор.Установить(Ложь);		
		
КонецПроцедуры

// Процедура - обаботчик события, при нажатии на кнопку "ОК" Командной панели "ОсновныеДействияФормы"
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	ПостроительОтчета.Выполнить();
	Настройки = ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь);
	СохранитьЗначение("ЭлектронныйДокументИсходящий.ФормаЗаполненияНоменклатуры.НастройкиПостроителя", Настройки);
	Закрыть(ПостроительОтчета.Результат.Выгрузить());
	
КонецПроцедуры

