////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ОбновитьСтроку(СтрокаТабличнойЧасти)
	
	ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	Если Не ЗначениеЗаполнено(ТипЦен) Тогда
		СтрокаТабличнойЧасти.Цена     = 0;
		СтрокаТабличнойЧасти.СуммаНДС = 0;
	Иначе
		СтрокаТабличнойЧасти.Цена     = Ценообразование.ПолучитьЦенуНоменклатуры(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, ТипЦен, Дата);
		СтрокаТабличнойЧасти.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаТабличнойЧасти.Цена,
		                                                   Истина, ТипЦен.ЦенаВключаетНДС,
		                                                   УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры  

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обаботчик события "ПередОткрытием" Формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлектронныеДокументы.ПередОткрытиемФормыДокументаВведенногоНаОсновании(ЭтотОбъект, Отказ, ЭтаФорма);	
	ЭлектронныеДокументы.УстановитьЗаголовокФормыЭлектронногоДокумента(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

// Процедура - обаботчик события "ПослеЗаписи" Формы
//
Процедура ПослеЗаписи() Экспорт
	
	ЭлектронныеДокументы.ПослеЗаписиФормыДокумента(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обаботчик события, при нажатии на кнопку "Структура подчиненности документа" Командной панели "ДействияФормы"
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры

// Процедура - обаботчик события, при нажатии на кнопку "Отправить" Командной панели "ОсновныеДействияФормы"
//
Процедура ОсновныеДействияФормыОтправить(Кнопка)
	
	Если Не ЭлектронныеДокументы.ПроверитьПередОтправкойЭлектронногоДокумента(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ     = Ложь;
	Сообщение = "";
	ЭлектронныеДокументы.ОтправитьИсходящийЭлектронныйДокумент(ЭтотОбъект, Отказ, Сообщение);
	
	ЭлектронныеДокументы.ДействияПослеОтправкиДокумента(Ссылка, Отказ, Сообщение, ЭтаФорма);
	
КонецПроцедуры

// Процедура - обаботчик события "ПриИзменении" в: Поле ввода "Организация"
//
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЭлектронныеДокументы.ПриИзмененииОрганизацииВДокументе(ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обаботчик события "ПриИзменении" в: Поле ввода "УчетнаяЗаписьЭлектронногоОбмена"
//
Процедура УчетнаяЗаписьЭлектронногоОбменаПриИзменении(Элемент)
	
	ЭлектронныеДокументы.ПриИзмененииУчетнойЗаписиВДокументе(ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обаботчик события, при нажатии на кнопку "Заполнить" Командной панели "КоманднаяПанель1"
//
Процедура КоманднаяПанельТоварыПрайсЗаполнить(Кнопка)
	
	ФормаЗаполненияНоменклатуры = ПолучитьФорму("ФормаЗаполненияНоменклатуры");
	ФормаЗаполненияНоменклатуры.Контрагент = Контрагент;
	Результат = ФормаЗаполненияНоменклатуры.ОткрытьМодально();
	Если Результат = Неопределено Тогда
		Возврат;		
	КонецЕсли;
	
	Товары.Загрузить(Результат);
	ОбновитьЦеныТабличнойЧасти();
	
КонецПроцедуры

Процедура ОбновитьЦеныТабличнойЧасти()
	
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		ОбновитьСтроку(СтрокаТабличнойЧасти)
	КонецЦикла;	
	
КонецПроцедуры

// Процедура - обаботчик события "ПриИзменении" в: Поле ввода "ТипЦен"
//
Процедура ТипЦенПриИзменении(Элемент)
	
	ОбновитьЦеныТабличнойЧасти();

КонецПроцедуры // ДоговорКонтрагентаПриИзменении()

// Процедура - обаботчик события "ПриИзменении"  колонки "Номенклатура" табличного поля "Товары"
//
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ОбновитьСтроку(ЭлементыФормы.Товары.ТекущиеДанные)
	
КонецПроцедуры

Процедура УчетнаяЗаписьЭлектронногоОбменаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ЭлектронныеДокументы.НачалоВыбораУчетнойЗаписиВДокументе(Элемент, СтандартнаяОбработка, ЭтотОбъект, ЭтаФорма);	
	
КонецПроцедуры

// обработчик показа состава файла обмена
Процедура ДействияФормыПоказатьДанныеОбмена(Кнопка)
	
	ЭлектронныеДокументы.ПоказатьДанныеОбмена(ЭтотОбъект);	
	
КонецПроцедуры

// показ примечания
Процедура ПримечаниеОткрытие(Элемент, СтандартнаяОбработка)
	
	ЭлектронныеДокументы.ПоказатьДлинныеСтроковыеДанные(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	ЭлектронныеДокументы.ПоказатьДлинныеСтроковыеДанные(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриОткрытии()
	Если НЕ ЭтоНовый() Тогда
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры
