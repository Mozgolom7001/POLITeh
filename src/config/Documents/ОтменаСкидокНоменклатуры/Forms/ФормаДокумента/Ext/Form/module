
// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.Документы,ЭлементыФормы.КоманднаяПанель1);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры


// Процедура - обработчик события "ПриВыводеСтроки" ТЧ документа
//
Процедура ДокументыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если Элемент.Колонки.ВидСкидки.Видимость Тогда
		ОформлениеСтроки.Ячейки.ВидСкидки.УстановитьТекст(ДанныеСтроки.УстановкаСкидокНоменклатуры.ВидСкидки);
	КонецЕсли;
	Если Элемент.Колонки.ДатаНачала.Видимость Тогда
		ОформлениеСтроки.Ячейки.ДатаНачала.УстановитьТекст(Формат(ДанныеСтроки.УстановкаСкидокНоменклатуры.ДатаНачала, "ДЛФ = Д"));
	КонецЕсли;
	Если Элемент.Колонки.ДатаОкончания.Видимость Тогда
		ОформлениеСтроки.Ячейки.ДатаОкончания.УстановитьТекст(Формат(ДанныеСтроки.УстановкаСкидокНоменклатуры.ДатаОкончания, "ДЛФ = Д"));
	КонецЕсли;
	Если Элемент.Колонки.Условие.Видимость Тогда
		ОформлениеСтроки.Ячейки.Условие.УстановитьТекст(ДанныеСтроки.УстановкаСкидокНоменклатуры.Условие);
	КонецЕсли;
	Если Элемент.Колонки.ТипСкидкиНаценки.Видимость Тогда
		ОформлениеСтроки.Ячейки.ТипСкидкиНаценки.УстановитьТекст(ДанныеСтроки.УстановкаСкидокНоменклатуры.ТипСкидкиНаценки);
	КонецЕсли;
	Если Элемент.Колонки.ВидОперации.Видимость Тогда
		ОформлениеСтроки.Ячейки.ВидОперации.УстановитьТекст(ДанныеСтроки.УстановкаСкидокНоменклатуры.ВидОперации);
	КонецЕсли;
	Если Элемент.Колонки.ЗначениеУсловия.Видимость Тогда
		ОформлениеСтроки.Ячейки.ЗначениеУсловия.УстановитьТекст(ДанныеСтроки.УстановкаСкидокНоменклатуры.ЗначениеУсловия);
	КонецЕсли;

КонецПроцедуры //ДокументыПриВыводеСтроки()

//Процедура - обработчик события "ПриИзменении" реквизита "УстановкаСкидокНоменклатуры" ТЧ документа
//
Процедура ДокументыУстановкаСкидокНоменклатурыПриИзменении(Элемент)
	
	Если Документы.НайтиСтроки(Новый Структура("УстановкаСкидокНоменклатуры", Элемент.Значение)).Количество()>1 Тогда
		Сообщить ("Документ: " + Элемент.Значение + " уже выбран в табличной части");
		Элемент.Значение = Неопределено;
	КонецЕсли;
	
КонецПроцедуры //ДокументыУстановкаСкидокНоменклатурыПриИзменении()

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
	КонецЕсли;

	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	// Установить печатную форму по умолчанию.
	РаботаСДиалогами.УстановитьКнопкуПечати(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ПриОткрытии()

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)

	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

КонецПроцедуры

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры //ДействияФормыСтруктураПодчиненностиДокумента()

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ОсновныеДействияФормыДействиеПечать(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()


