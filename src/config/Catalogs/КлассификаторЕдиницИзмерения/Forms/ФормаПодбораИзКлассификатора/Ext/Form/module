////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.КлассификаторЕдиницИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;

	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);
	// пользователю не показываем весовая это или нет единица измерения
	ТабличныйДокумент.Области.ВесоваяЕдиница.Очистить();

	ТабличныйДокумент.ФиксацияСверху      = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	// Получение значений полей выбранной строки.
	
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;	
	ОбластьКодЧисловой         = ТабличныйДокумент.Области.КодЧисловой;
	ОбластьНаименованиеКраткое = ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  = ТабличныйДокумент.Области.НаименованиеПолное;
	ОбластьМеждународноеСокращение = ТабличныйДокумент.Области.МеждународноеСокращение;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
			
		КодЧисловой         = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.        Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.        Право).Текст;
		НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
		НаименованиеПолное  = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст;
		МеждународноеСокращение = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьМеждународноеСокращение.Лево, ТекущаяОбласть.Низ, ОбластьМеждународноеСокращение.Право).Текст;
		// Проверка наличия выбранной единицы измерения.
		
		Ссылка = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(КодЧисловой);
		
		Если НЕ Ссылка.Пустая() Тогда
			Вопрос = "В справочнике ""Классификатор единиц измерения"" уже существует элемент с кодом """ + КодЧисловой + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;
				
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Создание новой единицы измерения.
		
		ФормаНовогоЭлемента = Справочники.КлассификаторЕдиницИзмерения.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		
		ФормаНовогоЭлемента.Код                = КодЧисловой;
		ФормаНовогоЭлемента.Наименование       = СтрПолучитьСтроку(НаименованиеКраткое, 1);
		ФормаНовогоЭлемента.НаименованиеПолное = СтрПолучитьСтроку(НаименованиеПолное, 1);
		ФормаНовогоЭлемента.МеждународноеСокращение = Лев(МеждународноеСокращение, 3);
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтруктураКолонок = Новый Структура();
			СтруктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "Код", 5));
			СтруктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтруктураКолонок.Вставить("НаименованиеПолное", Новый Структура("Заголовок, Ширина", "Полное наименование"));
			СтруктураКолонок.Вставить("МеждународноеСокращение", Новый Структура("Заголовок, Ширина", "Международное сокращение", 5));
			СтруктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			Форма.ТипСправочника = "КлассификаторЕдиницИзмерения";
			Форма.НастроитьФорму(СтруктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			КодЧисловой         = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодЧисловой.        Лево, ТекущаяСтрока, ОбластьКодЧисловой.        Право).Текст;
			НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименованиеКраткое.Лево, ТекущаяСтрока, ОбластьНаименованиеКраткое.Право).Текст;
			НаименованиеПолное  = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименованиеПолное. Лево, ТекущаяСтрока, ОбластьНаименованиеПолное. Право).Текст;				
			МеждународноеСокращение = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьМеждународноеСокращение.Лево, ТекущаяСтрока, ОбластьМеждународноеСокращение.Право).Текст;

			Если ОбщегоНазначения.ЕстьНеЦифры(КодЧисловой) Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Код = КодЧисловой;				
				СтрокаПодбора.Наименование = СтрПолучитьСтроку(НаименованиеКраткое, 1);
				СтрокаПодбора.НаименованиеПолное = СтрПолучитьСтроку(НаименованиеПолное, 1);
				СтрокаПодбора.МеждународноеСокращение = МеждународноеСокращение;
				СтрокаПодбора.Видимость = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(КодЧисловой).Пустая();
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();		
		
	КонецЕсли;

КонецПроцедуры
