// Получить массив с ссылкой на файл
Функция ПолучитьСсылкуНаФайл()
	СсылкаФайл = Новый Структура;
	СсылкаФайл.Вставить("ИмяФайла",  ИмяФайла);
	СсылкаФайл.Вставить("Хранилище", Хранилище);
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(СсылкаФайл);

	Возврат МассивСсылок;
КонецФункции // ПолучитьСсылкуНаФайл()

// Сохранить файл
Процедура ДействияФормыКнопкаСохранить(Кнопка)
	Если Не ПроверитьДанныеХранилища(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = "Выберите файл";
	Диалог.ПолноеИмяФайла = ИмяФайла;
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Хранилище.Получить();
	Попытка
		Данные.Записать(Диалог.ПолноеИмяФайла);
		Предупреждение("Файл успешно записан!");
	Исключение
		Предупреждение(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

// Открыть файл на просмотр
Процедура ДействияФормыКнопкаОткрыть(Кнопка)
	Если Не ПроверитьДанныеХранилища(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Хранилище.Получить();
	Если ТипЗнч(Данные) = Тип("Картинка") Тогда
		РаботаСФайлами.ОткрытьФормуИзображения(ЭтаФорма, Ссылка, Неопределено);
		
	ИначеЕсли ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
		Если ПустаяСтрока(ИмяФайла) Тогда
			Предупреждение("Просьба заполнить имя и расширение файла");
			Возврат;
		КонецЕсли;
		
		РаботаСФайлами.ОткрытьФайлы(ТекущийЭлемент, ПолучитьСсылкуНаФайл(), Ложь);
	Иначе
		Предупреждение("Не верный тип данных: " + ТипЗнч(Данные));
		
	КонецЕсли;
КонецПроцедуры

// Изменить данные
Процедура ДействияФормыКнопкаИзменить(Кнопка)
	// Проверим, есть ли какие-нибудь данные
	Если ПроверитьДанныеХранилища(Ложь) Тогда
		Ответ = Вопрос("Заменить данные?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Определим, что будет выбираться, картинка или произвольный файл
	ВыбираемИзображение = (ВидДанных = Перечисления.ВидыДополнительнойИнформацииОбъектов.Изображение);
	
	// Подготовим диалог выбора файла
	Диалог = РаботаСФайлами.ПолучитьДиалогВыбораФайлов(Ложь);
	Если ВыбираемИзображение Тогда
		// Для изображений в фильтре будут только картинки
		Диалог.Фильтр = РаботаСФайлами.ПолучитьФильтрИзображений();
	Иначе
		// Для произвольных файлов отключим просмотр
		Диалог.ПредварительныйПросмотр = Ложь;
	КонецЕсли;
	
	// Откроем диалог выбора
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	// Запишем в хранилище выбранный файл
	Попытка
		Если ВыбираемИзображение Тогда
			Хранилище = Новый ХранилищеЗначения(Новый Картинка(Диалог.ПолноеИмяФайла));
		Иначе
			Хранилище = Новый ХранилищеЗначения(Новый ДвоичныеДанные(Диалог.ПолноеИмяФайла), Новый СжатиеДанных());
		КонецЕсли;
		ВыбранныйФайл = Новый Файл(Диалог.ПолноеИмяФайла);
		ИмяФайла = ВыбранныйФайл.Имя;
	Исключение
		Предупреждение(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

// Проверить данные хранилища
//
// Параметры
//  Предупреждать  – Булево – Выводить или нет предупреждение о том, что хранилище пусто
//
// Возвращаемое значение:
//   Булево   – Истина, если хранилище не пусто, Ложь - иначе
//
Функция ПроверитьДанныеХранилища(Предупреждать)
	Если Хранилище.Получить() <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Предупреждать Тогда
		Предупреждение("Хранилище пусто!");
	КонецЕсли;
	Возврат Ложь;
КонецФункции // ПроверитьДанныеХранилища()

