Процедура УстановитьВладельца(Значение, Отказ)

	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отбор.Владелец.ТипЗначения.СодержитТип(ТипЗнч(Значение)) Тогда
		Отбор.Владелец.Установить(Значение, Истина);
	КонецЕсли;
	
КонецПроцедуры // УстановитьВладельца(Значение, Отказ)

Процедура ВыбратьВладельцаПоВиду(ВидВладельца, Отказ)

	Если ВидВладельца <> Неопределено Тогда
	
		ТекстЗаголовка = Новый Соответствие;
		ТекстЗаголовка.Вставить("Контрагенты", "Выберите контрагента, счет которого Вы хотите выбрать.");
		ТекстЗаголовка.Вставить("Организации", "Выберите организацию, счет которой Вы хотите выбрать.");
		
		ФормаВыбораВладельца = Справочники[ВидВладельца].ПолучитьФормуВыбора();
		ФормаВыбораВладельца.Заголовок = ТекстЗаголовка[ВидВладельца];
		
		УстановитьВладельца(ФормаВыбораВладельца.ОткрытьМодально(), Отказ);
	
	КонецЕсли;

КонецПроцедуры // ВыбратьВладельцаПоВиду((ВидВладельца, Отказ))

Процедура ВыбратьВладельца(Отказ)

	ВыбратьВладельцаПоВиду(ПолучитьВидВладельца(), Отказ);

КонецПроцедуры // ВыбратьВладельца()

Функция ПолучитьВидВладельца()

	СписокВидов = Новый СписокЗначений;
	СписокВидов.Добавить("Контрагенты");
	СписокВидов.Добавить("Организации");
	
	Элемент = СписокВидов.ВыбратьЭлемент("Вид владельца");
	
	Возврат ?(Элемент = Неопределено, Неопределено, Элемент.Значение);

КонецФункции // ВыбратьВидВладельца()

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если РежимВыбора И НЕ ЗначениеЗаполнено(Отбор.Владелец.Значение) И НЕ ЗначениеЗаполнено(ПараметрОтборПоВладельцу) Тогда
		
		Если ТипЗнч(ВладелецФормы) = Тип("ПолеВвода") Тогда
			
			Если ТипЗнч(ВладелецФормы.ВыборПоВладельцу)=Тип("СправочникСсылка.Контрагенты") Тогда
				ВыбратьВладельцаПоВиду("Контрагенты", Отказ);
			ИначеЕсли ТипЗнч(ВладелецФормы.ВыборПоВладельцу)=Тип("СправочникСсылка.Организации") Тогда
				ВыбратьВладельцаПоВиду("Организации", Отказ);
			Иначе
				ВыбратьВладельца(Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

