
Процедура ПередЗаписью(Отказ)
	
	ТекстОшибки = НаличиеОшибокВРегистрационномНомере(РегистрационныйНомер);
	
	Если ТекстОшибки <> "" Тогда
		Отказ = Истина;
		Предупреждение(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура КодПриИзменении(Элемент)
	
	Реквизиты = Справочники.НомераГТД.РегистрационныйНомерИСтранаВвоза(Код);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
КонецПроцедуры

Функция НаличиеОшибокВРегистрационномНомере(РегистрационныйНомер)
	
	РегистрационныйНомер = СокрЛП(РегистрационныйНомер);
	
	Если НЕ ЗначениеЗаполнено(РегистрационныйНомер) Тогда
		// Пользователь еще ничего не ввел.
		Возврат "";
	КонецЕсли;
	
	МассивТД = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(РегистрационныйНомер, "/");
	
	Если МассивТД.Количество() > 3
	 ИЛИ МассивТД.Количество() < 2 Тогда
		// Ошибочное количество элементов.
		Возврат НСтр("ru='Ошибочно указан регистрационный номер: он должен состоять из двух или трех блоков, разделенных дробью ""/""'");
	КонецЕсли;
	
	КодТаможенногоОргана = СокрЛП(МассивТД[0]);
	
	Если СтрДлина(КодТаможенногоОргана) <> 8 Тогда
		// Ошибочная длина кода таможенного органа.
		Возврат НСтр("ru='Ошибочно указан регистрационный номер: первый блок (код таможенного органа) должен состоять из 8 цифр'"); ;
	КонецЕсли;
	
	ДатаПринятияДекларацииНаТовары = СокрЛП(МассивТД[1]);
	
	Если СтрДлина(ДатаПринятияДекларацииНаТовары) <> 6 Тогда
		// Ошибочная длина поля дата.
		Возврат НСтр("ru='Ошибочно указан регистрационный номер: второй блок (дата регистрации декларации) должен быть в формате ДДММГГ'");
	Иначе
		// Проверим корректность указания даты.
		СтрокаВДату = Справочники.НомераГТД.СтрокаВДату(ДатаПринятияДекларацииНаТовары);
		Если НЕ ЗначениеЗаполнено(СтрокаВДату) Тогда
			// Длина поля верная, но дата указана ошибочно.
			Возврат НСтр("ru='Ошибочно указан регистрационный номер: второй блок (дата регистрации декларации) должен быть в формате ДДММГГ'");
		КонецЕсли;
	КонецЕсли;

	ПорядковыйНомерДекларацииНаТовары = СокрЛП(МассивТД[2]);
	
	Если СтрДлина(ПорядковыйНомерДекларацииНаТовары) <> 7 Тогда
		// Ошибочная длина поля порядковый номер.
		Возврат НСтр("ru='Ошибочно указан регистрационный номер: третий блок (порядковый номер декларации) должен состоять из 7 цифр'"); 
	КонецЕсли;
	
	Возврат "";
	
КонецФункции
