
Процедура КоманднаяПанельПользователиГруппыПодбор(Кнопка)
	
	ФормаПодбора = Справочники.Пользователи.ПолучитьФормуВыбора(, ЭтаФорма);
	
	ФормаПодбора.ПараметрВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
	ФормаПодбора.ЗакрыватьПриВыборе           = Ложь;
	
	ФормаПодбора.Открыть();
	
КонецПроцедуры

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.Пользователи") Тогда
		Если ПользователиГруппы.Найти(ЗначениеВыбора, "Пользователь") = Неопределено Тогда
			ПользователиГруппы.Добавить().Пользователь = ЗначениеВыбора;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры

Процедура ПриОткрытии()

	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);

	Если ЭтоНовый() Тогда
		АдминистраторГруппы = глЗначениеПеременной("глТекущийПользователь");
	ИначеЕсли Ссылка = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		ЭлементыФормы.ПользователиГруппы.ТолькоПросмотр = Истина;
	КонецЕсли;

	Если ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		
		// Заполним данные по видам объектов доступа
		Запрос = Новый Запрос;
		Если НЕ ЭтоНовый() Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыОбъектовДоступа.Ссылка КАК ВидОбъектаДоступа,
			|	ВЫБОР КОГДА НазначениеВидовОбъектовДоступа.ВидОбъектаДоступа ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ КАК Использование
			|ИЗ
			|	Перечисление.ВидыОбъектовДоступа КАК ВидыОбъектовДоступа
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначениеВидовОбъектовДоступа КАК НазначениеВидовОбъектовДоступа
			|		ПО НазначениеВидовОбъектовДоступа.ВидОбъектаДоступа = ВидыОбъектовДоступа.Ссылка
			|		 И НазначениеВидовОбъектовДоступа.ГруппаПользователей = &Ссылка
			|";
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыОбъектовДоступа.Ссылка КАК ВидОбъектаДоступа,
			|	ИСТИНА КАК Использование
			|ИЗ
			|	Перечисление.ВидыОбъектовДоступа КАК ВидыОбъектовДоступа
			|";
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ВидыОбъектовДоступа.Добавить();
			НоваяСтрока.ВидОбъектаДоступа = Выборка.ВидОбъектаДоступа;
			НоваяСтрока.Использование = Выборка.Использование;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанельФормыПрава(Кнопка)
	
	Если НЕ РаботаСДиалогами.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(Ссылка);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.Права);
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.РазделительПрава);
		ЭлементыФормы.ПанельВидыОбъектовДоступа.Свертка = РежимСверткиЭлементаУправления.Лево;
	КонецЕсли;
	
	Если Ссылка = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		
		// Запишем НазначениеВидовОбъектовДоступа
		НаборЗаписей = РегистрыСведений.НазначениеВидовОбъектовДоступа.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ГруппаПользователей.Использование = Истина;
		НаборЗаписей.Отбор.ГруппаПользователей.Значение = Ссылка;
		
		Для каждого Строка Из ВидыОбъектовДоступа Цикл
			Если Строка.Использование Тогда
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.ВидОбъектаДоступа = Строка.ВидОбъектаДоступа;
				НоваяЗапись.ГруппаПользователей = Ссылка;
			КонецЕсли; 
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ВызватьИсключение ОписаниеОшибки();
		КонецПопытки;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура КоманднаяПанельВидыОбъектовДоступаУстановитьФлажки(Кнопка)
	
	ИзменитьФлажкиВидовОбъектовДоступа(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельВидыОбъектовДоступаСнятьФлажки(Кнопка)
	
	ИзменитьФлажкиВидовОбъектовДоступа(Ложь);
	
КонецПроцедуры

Процедура ИзменитьФлажкиВидовОбъектовДоступа(Значение)

	Для каждого Строка Из ВидыОбъектовДоступа Цикл
		Строка.Использование = Значение;
	КонецЦикла; 

КонецПроцедуры

Процедура ПослеЗаписи()
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры

