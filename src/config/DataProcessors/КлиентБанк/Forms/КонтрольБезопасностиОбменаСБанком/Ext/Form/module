
Процедура ОсновныеДействияФормыОсновныеДействияФормыПроверить(Кнопка)
	
	ФайлВыгрузкиФайл = Новый Файл(ПутьКФайлуВыгрузки);
	Если НЕ ФайлВыгрузкиФайл.Существует() Тогда
		ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ФайлУдален;
		Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл выгрузки платежей ""%1"" удален другой программой.'"),
			ПутьКФайлуВыгрузки);
		ЗаписатьВЖурналРегистрации("ФайлУдален", Комментарий);
	Иначе
		ТектовыйДокументВыгрузки = Новый ТекстовыйДокумент();
		Если Кодировка = "DOS" Тогда
			ТектовыйДокументВыгрузки.Прочитать(ПутьКФайлуВыгрузки, КодировкаТекста.OEM);
		Иначе
			ТектовыйДокументВыгрузки.Прочитать(ПутьКФайлуВыгрузки, КодировкаТекста.ANSI);
		КонецЕсли;
		
		Если ТектовыйДокументВыгрузки.ПолучитьТекст() <> ТекстВыгрузкиСтрокой Тогда
			ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ФайлИзменен;
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл выгрузки платежей ""%1"" изменен другой программой.'"),
				ПутьКФайлуВыгрузки);
			ЗаписатьВЖурналРегистрации("ФайлИзменен", Комментарий)
		Иначе
			ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ПроверкаУспешна;
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл выгрузки платежей ""%1"" не изменен.'"),
				ПутьКФайлуВыгрузки);
			ЗаписатьВЖурналРегистрации("ПроверкаВыполненаУспешно", Комментарий)
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(0);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ФайлИзменен
			ИЛИ ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ФайлУдален Тогда
		
		Если НЕ ЗагруженнаяВыпискаПроверена Тогда
			Отказ = Истина;
			
			ТекстПредупрежденияОбУгрозе = НСтр("ru = 'Подтвердите, что Клиент-банк проверен и в нем все платежи корректны'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупрежденияОбУгрозе, , , "ЗагруженнаяВыпискаПроверена");
			
			ЭтаФорма.ТекущийЭлемент = ?(ЭлементыФормы.ПанельСообщение.ТекущаяСтраница = ЭлементыФормы.ПанельСообщение.Страницы.ФайлИзменен,
				ЭлементыФормы.ЗагруженнаяВыпискаПроверена, ЭлементыФормы.ЗагруженнаяВыпискаПроверенаФайлУдален);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		УдалитьФайлы(ПутьКФайлуВыгрузки);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьВЖурналРегистрации(РезультатПроверки, Комментарий)
	
	Если РезультатПроверки = "ПроверкаВыполненаУспешно" Тогда
		Уровень = УровеньЖурналаРегистрации.Информация;
	Иначе
		Уровень = УровеньЖурналаРегистрации.Ошибка;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("Проверка выгрузки платежей", Уровень, Метаданные.Обработки.КлиентБанк, , Комментарий);
	
КонецПроцедуры
