
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаСчетФактураПолученный();
	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок+" завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры


// Обновление реквизитов документа СчетФактураПолученный 
// Заполнение ТЧ ДокументыОснования по реквизиту шапки документа
//
Процедура ОбновлениеРеквизитовДокументаСчетФактураПолученный()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойВидСчетаФактуры", Перечисления.ВидСчетаФактурыПолученного.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаПостановления", '20120101');
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураПолученный.Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактураПолученный.Дата >= &ДатаПостановления
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДозаполнятьДокумент,
		|	СчетФактураПолученный.Проведен
		|ИЗ
		|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|ГДЕ
		|	СчетФактураПолученный.ВидСчетаФактуры = &ПустойВидСчетаФактуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураПолученный.Дата";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	    СФОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если СФОбъект.УдалитьНаАванс Тогда
			СФОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс;
			Если Выборка.ДозаполнятьДокумент Тогда
				СФОбъект.КодСпособаПолучения = 1;
				СФОбъект.КодВидаОперации = "02";
				СФОбъект.СуммаНДСДокумента = СФОбъект.Авансы.Итог("СуммаНДС");
			КонецЕсли;
		Иначе
			СФОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
			Если Выборка.ДозаполнятьДокумент Тогда
				СФОбъект.КодСпособаПолучения = 1;
				СФОбъект.ОпределениеПараметровСчетаФактуры();
				мКодВидаОперации = "";
				Для каждого СтрокаТабличнойЧасти Из СФОбъект.ДокументыОснования Цикл
					Если ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
						Если Найти(мКодВидаОперации, "03") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + "03;";
						КонецЕсли;
					Иначе
						Если Найти(мКодВидаОперации, "01") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + "01;";
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если ПустаяСтрока(мКодВидаОперации) Тогда
					мКодВидаОперации = "01";
				Иначе
					мКодВидаОперации = Лев(мКодВидаОперации, СтрДлина(мКодВидаОперации)-1);
				КонецЕсли;
				СФОбъект.КодВидаОперации = мКодВидаОперации;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			
			Если Выборка.ДозаполнятьДокумент И Выборка.Проведен Тогда
				СФОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			Иначе
				СФОбъект.ОбменДанными.Загрузка = Истина;
				СФОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
		Исключение
			
			Попытка
				
				Если Выборка.ДозаполнятьДокумент И Выборка.Проведен Тогда
					СФОбъект.ОбменДанными.Загрузка = Истина;
					СФОбъект.Записать(РежимЗаписиДокумента.Запись);
				Иначе
					Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
				КонецЕсли;
			Исключение
				Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
			КонецПопытки;
			
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
