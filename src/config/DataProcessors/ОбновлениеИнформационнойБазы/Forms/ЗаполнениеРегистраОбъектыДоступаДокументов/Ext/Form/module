
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьОбработку();
	
КонецПроцедуры

Процедура ВыполнитьОбработку() Экспорт

	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("","Заполнение регистра сведений Объекты доступа документов");

	ЗаполнениеРегистраСведенийОбъектыДоступаДокументов();

	СообщитьОСостоянииОбновления("","Заполнение регистра сведений Объекты доступа документов завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

Процедура ЗаполнениеРегистраСведенийОбъектыДоступаДокументов()

	Отказ = Ложь;

	Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
		ТаблицаСвойств = НастройкаПравДоступа.ПолучитьТаблицуСвойствДляРегистрацииОбъектовДоступаДокумента(МетаданныеДокумента);

		Если ТаблицаСвойств.Количество() <> 0 Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	Док.Ссылка КАК Ссылка
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + " КАК Док
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ОбъектыДоступаДокументов КАК ОбъектыДоступа
			|ПО
			|	Док.Ссылка = ОбъектыДоступа.ДокументСсылка
			|ГДЕ
			|	ОбъектыДоступа.ОбъектДоступа ЕСТЬ NULL
			|";

			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НастройкаПравДоступа.ПриЗаписиДокументаРегистрацияОбъектовПравДоступа(Выборка.Ссылка.ПолучитьОбъект(), Отказ);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры


Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
