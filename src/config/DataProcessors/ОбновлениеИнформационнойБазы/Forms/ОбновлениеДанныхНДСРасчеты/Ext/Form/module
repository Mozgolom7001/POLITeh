
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт

	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("","Обновление данных НДС Расчеты");

	ОбновлениеНДСРасчеты();
	СообщитьОСостоянииОбновления("","Обновление данных НДС Расчеты завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры


// Обновление данных в регистрах НДСРасчетыСПокупателями и НДСРасчетыСПоставщиками
// Перенос данных о распределенной оплате в НДСУчетРаспределеннойОплатыПокупателей и
// НДСУчетРаспределеннойОплатыПоставщикам
//
Процедура ОбновлениеНДСРасчеты()
	
	ЗаписиКУдалению = Новый Массив;
	
	// НДСРасчетыСПокупателями
	СообщитьОСостоянииОбновления("","Реорганизация данных в регистре ""НДС расчеты с покупателями""", СтатусСообщения.Информация);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	НДСРасчетыСПокупателями.Регистратор.Ссылка КАК Ссылка,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА НДСРасчетыСПокупателями.УдалитьРаспределеннаяСумма = 0
	               |					И НДСРасчетыСПокупателями.УдалитьКурсоваяРазница = 0
	               |				ТОГДА ЛОЖЬ
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ) КАК ЕстьРаспределеннаяСумма,
	               |	КОНЕЦПЕРИОДА(НДСРасчетыСПокупателями.Период, ДЕНЬ) КАК Период
	               |ИЗ
	               |	РегистрНакопления.НДСРасчетыСПокупателями КАК НДСРасчетыСПокупателями
	               |ГДЕ
	               |	((НЕ(НДСРасчетыСПокупателями.УдалитьДокументОплаты = НЕОПРЕДЕЛЕНО
	               |					И НДСРасчетыСПокупателями.УдалитьСчетФактура = НЕОПРЕДЕЛЕНО))
	               |			ИЛИ НДСРасчетыСПокупателями.УдалитьРаспределеннаяСумма <> 0
	               |			ИЛИ НДСРасчетыСПокупателями.УдалитьКурсоваяРазница <> 0)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	КОНЕЦПЕРИОДА(НДСРасчетыСПокупателями.Период, ДЕНЬ),
	               |	НДСРасчетыСПокупателями.Регистратор.Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период
	               |ИТОГИ ПО
	               |	Период";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		НаборЗаписейНДСРасчетыСПокупателями = РегистрыНакопления.НДСРасчетыСПокупателями.СоздатьНаборЗаписей();
		НаборЗаписейНДСУчетРаспределенныхОплатПокупателей = РегистрыНакопления.НДСУчетРаспределенныхОплатПокупателей.СоздатьНаборЗаписей();
	КонецЕсли;
	
	РезультатЗапросаПоПериодам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока РезультатЗапросаПоПериодам.Следующий() Цикл
	
		Состояние("Реорганизация данных за период """ + Формат(РезультатЗапросаПоПериодам.Период, "ДФ=dd.MM.yyyy") + """");
		РезультатЗапроса = РезультатЗапросаПоПериодам.Выбрать();
		
		Пока РезультатЗапроса.Следующий() Цикл
			
			ЗаписиКУдалению.Очистить();
			
			НаборЗаписейНДСРасчетыСПокупателями.Отбор.Регистратор.Значение = РезультатЗапроса.Ссылка;
			НаборЗаписейНДСРасчетыСПокупателями.Прочитать();
			
			Если РезультатЗапроса.ЕстьРаспределеннаяСумма Тогда
				НаборЗаписейНДСУчетРаспределенныхОплатПокупателей.Отбор.Регистратор.Значение = РезультатЗапроса.Ссылка;
				НаборЗаписейНДСУчетРаспределенныхОплатПокупателей.Прочитать();
				НоваяЗаписьРаспределеннаяСумма = Неопределено;
			КонецЕсли;
			
			Для Каждого Запись из НаборЗаписейНДСРасчетыСПокупателями Цикл
				
				// Перенос распределенной суммы и курсовой разницы
				Если РезультатЗапроса.ЕстьРаспределеннаяСумма И (Запись.УдалитьРаспределеннаяСумма <> 0 Или Запись.УдалитьКурсоваяРазница <> 0) Тогда
					
					НоваяЗаписьРаспределеннаяСумма = НаборЗаписейНДСУчетРаспределенныхОплатПокупателей.Добавить();
					
					НоваяЗаписьРаспределеннаяСумма.ВидДвижения			= Запись.ВидДвижения;
					НоваяЗаписьРаспределеннаяСумма.Регистратор			= Запись.Регистратор;
					НоваяЗаписьРаспределеннаяСумма.Период 				= Запись.Период;
					НоваяЗаписьРаспределеннаяСумма.Организация			= Запись.Организация;
					НоваяЗаписьРаспределеннаяСумма.СчетФактура 			= Запись.УдалитьСчетФактура;
					НоваяЗаписьРаспределеннаяСумма.ДокументОплаты 		= Запись.УдалитьДокументОплаты;
					НоваяЗаписьРаспределеннаяСумма.РаспределеннаяСумма 	= Запись.УдалитьРаспределеннаяСумма;
					НоваяЗаписьРаспределеннаяСумма.КурсоваяРазница		= Запись.УдалитьКурсоваяРазница;
					НоваяЗаписьРаспределеннаяСумма.ДатаСобытия			= Запись.ДатаСобытия;
					НоваяЗаписьРаспределеннаяСумма.СписаниеПартий		= Запись.СписаниеПартий;
					
				КонецЕсли;
				
				//Перенос ДокументОплаты и СчетФактура в измерение Документ
				Если (Запись.Сумма <> 0 Или Запись.ВалютнаяСумма <> 0) 
					И Не (Запись.УдалитьДокументОплаты = Неопределено и Запись.УдалитьСчетФактура = Неопределено) Тогда
					
					Если Запись.УдалитьСчетФактура = Неопределено Тогда
						Запись.Документ = Запись.УдалитьДокументОплаты;
						Запись.УдалитьДокументОплаты 		= Неопределено;
					ИначеЕсли Запись.УдалитьДокументОплаты = Неопределено Тогда
						Запись.Документ = Запись.УдалитьСчетФактура;
						Запись.УдалитьСчетФактура = Неопределено;
					Иначе
						СообщитьОСостоянииОбновления("","Среди движений документа «" + Запись.Регистратор + "» по регистру «НДС расчеты с покупателями» присутствуют некорректные:
								|Одновременно заполнены измерения «(удален) Счет-фактура» и «(удален) Документ оплаты» при заполненных ресурсах «Сумма» и/или «Валютная сумма»", СтатусСообщения.Внимание);
					КонецЕсли;
							
				КонецЕсли;
				
				// Очистка удаленных реквизитов или удаление записи, если ресурсы не заполнены
				Если Запись.Сумма = 0 Или Запись.ВалютнаяСумма = 0 Тогда 
					ЗаписиКУдалению.Добавить(Запись);
				Иначе
					Запись.УдалитьРаспределеннаяСумма    = 0;
					Запись.УдалитьКурсоваяРазница		= 0;
				КонецЕсли;
			
			КонецЦикла;
			
			// Удалитьие лишних записей из НДСРасчетыСПокупателями
			Для Каждого УдаленнаяЗапись из ЗаписиКУдалению Цикл
				НаборЗаписейНДСРасчетыСПокупателями.Удалить(УдаленнаяЗапись);
			КонецЦикла;
			
			// Запись наборов в регистры
			Если РезультатЗапроса.ЕстьРаспределеннаяСумма И НоваяЗаписьРаспределеннаяСумма <> Неопределено Тогда
				НаборЗаписейНДСУчетРаспределенныхОплатПокупателей.Записать();
			КонецЕсли;
			
			НаборЗаписейНДСРасчетыСПокупателями.Записать();
				
		КонецЦикла;
		
	КонецЦикла;
	
	// НДСРасчетыСПоставщиками
	
	СообщитьОСостоянииОбновления("","Реорганизация данных в регистре ""НДС расчеты с поставщиками""", СтатусСообщения.Информация);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НДСРасчетыСПоставщиками.Регистратор.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НДСРасчетыСПоставщиками.УдалитьРаспределеннаяСумма = 0
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ) КАК ЕстьРаспределеннаяСумма,
		|	КОНЕЦПЕРИОДА(НДСРасчетыСПоставщиками.Период, ДЕНЬ) КАК Период
		|ИЗ
		|	РегистрНакопления.НДСРасчетыСПоставщиками КАК НДСРасчетыСПоставщиками
		|ГДЕ
		|	((НЕ(НДСРасчетыСПоставщиками.УдалитьДокументОплаты = НЕОПРЕДЕЛЕНО
		|					И НДСРасчетыСПоставщиками.УдалитьСчетФактура = НЕОПРЕДЕЛЕНО))
		|			ИЛИ НДСРасчетыСПоставщиками.УдалитьРаспределеннаяСумма <> 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	КОНЕЦПЕРИОДА(НДСРасчетыСПоставщиками.Период, ДЕНЬ),
		|	НДСРасчетыСПоставщиками.Регистратор.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ ПО
		|	Период";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		НаборЗаписейНДСРасчетыСПоставщиками = РегистрыНакопления.НДСРасчетыСПоставщиками.СоздатьНаборЗаписей();
		НаборЗаписейНДСУчетРаспределенныхОплатПоставщикам = РегистрыНакопления.НДСУчетРаспределенныхОплатПоставщикам.СоздатьНаборЗаписей();
	КонецЕсли;
	
	РезультатЗапросаПоПериодам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Пока РезультатЗапросаПоПериодам.Следующий() Цикл
	
		Состояние("Реорганизация данных за период """ + Формат(РезультатЗапросаПоПериодам.Период, "ДФ=dd.MM.yyyy") + """");
		РезультатЗапроса = РезультатЗапросаПоПериодам.Выбрать();
		
		Пока РезультатЗапроса.Следующий() Цикл
			
			ЗаписиКУдалению.Очистить();
			
			НаборЗаписейНДСРасчетыСПоставщиками.Отбор.Регистратор.Значение = РезультатЗапроса.Ссылка;
			НаборЗаписейНДСРасчетыСПоставщиками.Прочитать();
			
			Если РезультатЗапроса.ЕстьРаспределеннаяСумма Тогда
				НаборЗаписейНДСУчетРаспределенныхОплатПоставщикам.Отбор.Регистратор.Значение = РезультатЗапроса.Ссылка;
				НаборЗаписейНДСУчетРаспределенныхОплатПоставщикам.Прочитать();
				НоваяЗаписьРаспределеннаяСумма = Неопределено;
			КонецЕсли;
			
			Для Каждого Запись из НаборЗаписейНДСРасчетыСПоставщиками Цикл
				
				// Перенос распределенной суммы и курсовой разницы
				Если РезультатЗапроса.ЕстьРаспределеннаяСумма И Запись.УдалитьРаспределеннаяСумма <> 0 Тогда
					
					НоваяЗаписьРаспределеннаяСумма = НаборЗаписейНДСУчетРаспределенныхОплатПоставщикам.Добавить();
					
					НоваяЗаписьРаспределеннаяСумма.ВидДвижения			= Запись.ВидДвижения;
					НоваяЗаписьРаспределеннаяСумма.Регистратор			= Запись.Регистратор;
					НоваяЗаписьРаспределеннаяСумма.Период 				= Запись.Период;
					НоваяЗаписьРаспределеннаяСумма.Организация			= Запись.Организация;
					НоваяЗаписьРаспределеннаяСумма.СчетФактура 			= Запись.УдалитьСчетФактура;
					НоваяЗаписьРаспределеннаяСумма.ДокументОплаты 		= Запись.УдалитьДокументОплаты;
					НоваяЗаписьРаспределеннаяСумма.РасчетыСБюджетом		= Запись.РасчетыСБюджетом;
					НоваяЗаписьРаспределеннаяСумма.РаспределеннаяСумма 	= Запись.УдалитьРаспределеннаяСумма;
					НоваяЗаписьРаспределеннаяСумма.ДатаСобытия			= Запись.ДатаСобытия;
					
				КонецЕсли;
				
				//Перенос УдалитьДокументОплаты и УдалитьСчетФактура в измерение Документ
				Если (Запись.Сумма <> 0 Или Запись.ВалютнаяСумма <> 0) 
					И Не (Запись.УдалитьДокументОплаты = Неопределено и Запись.УдалитьСчетФактура = Неопределено) Тогда
					
					Если Запись.УдалитьСчетФактура = Неопределено Тогда
						Запись.Документ = Запись.УдалитьДокументОплаты;
						Запись.УдалитьДокументОплаты 		= Неопределено;
					ИначеЕсли Запись.УдалитьДокументОплаты = Неопределено Тогда
						Запись.Документ = Запись.УдалитьСчетФактура;
						Запись.УдалитьСчетФактура = Неопределено;
					Иначе
						СообщитьОСостоянииОбновления("","Среди движений документа «" + Запись.Регистратор + "» по регистру «НДС расчеты с поставщиками» присутствуют некорректные:
								|Одновременно заполнены измерения «(удален) Счет-фактура» и «(удален) Документ оплаты» при заполненных ресурсах «Сумма» и/или «Валютная сумма»", СтатусСообщения.Внимание);
					КонецЕсли;
							
				КонецЕсли;
				
				// Очистка удаленных реквизитов или удаление записи, если ресурсы не заполнены
				Если Запись.Сумма = 0 Или Запись.ВалютнаяСумма = 0 Тогда 
					ЗаписиКУдалению.Добавить(Запись);
				Иначе
					Запись.УдалитьРаспределеннаяСумма    = 0;
				КонецЕсли;
			
			КонецЦикла;
			
			// Удалитьие лишних записей из НДСРасчетыСПоставщиками
			Для Каждого УдаленнаяЗапись из ЗаписиКУдалению Цикл
				НаборЗаписейНДСРасчетыСПоставщиками.Удалить(УдаленнаяЗапись);
			КонецЦикла;
			
			// Запись наборов в регистры
			Если РезультатЗапроса.ЕстьРаспределеннаяСумма И НоваяЗаписьРаспределеннаяСумма <> Неопределено Тогда
				НаборЗаписейНДСУчетРаспределенныхОплатПоставщикам.Записать();
			КонецЕсли;
			
			НаборЗаписейНДСРасчетыСПоставщиками.Записать();
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
