
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаСчетФактураВыданный();
	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок+" завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры


// Обновление реквизитов документа СчетФактураВыданный 
// Заполнение ТЧ ДокументыОснования по реквизиту шапки документа
// Обновление реквизита СуммаДокумента для счетов-фактур на аванс
//
Процедура ОбновлениеРеквизитовДокументаСчетФактураВыданный()
    	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойВидСчетаФактуры", Перечисления.ВидСчетаФактурыВыставленного.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаПостановления", '20120101');
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактураВыданный.Дата >= &ДатаПостановления
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДозаполнятьДокумент,
		|	СчетФактураВыданный.Проведен
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.ВидСчетаФактуры = &ПустойВидСчетаФактуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураВыданный.Дата";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	    СФОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если СФОбъект.УдалитьНаАванс Тогда
			СФОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс;
			Если Выборка.ДозаполнятьДокумент Тогда
				СФОбъект.КодСпособаВыставления = 1;
				СФОбъект.КодВидаОперации = "02";				
				СФОбъект.СуммаНДСДокумента = СФОбъект.Авансы.Итог("СуммаНДС");
			КонецЕсли;
		Иначе				
			СФОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
			Если Выборка.ДозаполнятьДокумент Тогда
				СФОбъект.ОпределениеПараметровСчетаФактуры();
				СФОбъект.КодСпособаВыставления = 1;
				Для Каждого СтрокаТабличнойЧасти ИЗ СФОбъект.ДокументыОснования Цикл
					мКодВидаОперации = "";
					Если ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
						ИЛИ ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщикуИзНТТ") Тогда
						Если Найти(мКодВидаОперации, "03") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + ?(мКодВидаОперации = "","",";") + "04";
						КонецЕсли;
					ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
						Если Найти(мКодВидаОперации, "04") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + ?(мКодВидаОперации = "","",";") + "06";			
						КонецЕсли;
					ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
						Если Найти(мКодВидаОперации, "01") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + ?(мКодВидаОперации = "","",";") + "01";
						КонецЕсли;
					Иначе
						Если Найти(мКодВидаОперации, "01") = 0 Тогда
							мКодВидаОперации = мКодВидаОперации + ?(мКодВидаОперации = "","",";") + "01";
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
				Если мКодВидаОперации = "" Тогда
					мКодВидаОперации = "01";
				КонецЕсли;
				СФОбъект.КодВидаОперации = мКодВидаОперации;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			
			Если Выборка.ДозаполнятьДокумент И Выборка.Проведен Тогда
				СФОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			Иначе
				СФОбъект.ОбменДанными.Загрузка = Истина;
				СФОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
		Исключение
			
			Попытка
				
				Если Выборка.ДозаполнятьДокумент И Выборка.Проведен Тогда
					СФОбъект.ОбменДанными.Загрузка = Истина;
					СФОбъект.Записать(РежимЗаписиДокумента.Запись);
				Иначе
					Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
				КонецЕсли;
			Исключение
				Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
			КонецПопытки;
			
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
