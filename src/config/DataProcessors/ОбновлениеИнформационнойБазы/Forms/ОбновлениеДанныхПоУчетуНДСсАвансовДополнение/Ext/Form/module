
Процедура ВыполнитьОбработку()Экспорт
	
	ОбновитьУчетнуюПолитикуНУ();
	ОбновитьСчетаФактурыВыданныеНаАванс();
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьОбработку();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

Процедура ОбновитьУчетнуюПолитикуНУ() Экспорт
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление учетной политики в связи с уточнениями законодательства в части учета НДС, внесенными письмом Минфина от 6 марта 2009 г. № 03-07-15/39");
	
	Выборка = РегистрыСведений.УчетнаяПолитикаНалоговыйУчет.Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = Выборка.ПолучитьМенеджерЗаписи();
		Запись.Прочитать();
		Если Запись.ПорядокРегистрацииСчетовФактурНаАванс = Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.ПустаяСсылка() Тогда
			Запись.ПорядокРегистрацииСчетовФактурНаАванс = Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.НаВсеАвансы;
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;		
		
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление учетной политики в связи с уточнениями законодательства в части учета НДС, внесенными письмом Минфина от 6 марта 2009 г. № 03-07-15/39 завершено");
	
КонецПроцедуры

Процедура ОбновитьСчетаФактурыВыданныеНаАванс() Экспорт
	
	//Обновление документов "Счет-фактура выданный" на аванс - перенос данных о сумме аванса из шапки в ТЧ.
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура выданный"" в связи с уточнениями законодательства в части учета НДС, внесенными письмом Минфина от 6 марта 2009 г. № 03-07-15/39");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	СчетФактураВыданныйАвансы.Ссылка
	               |ИЗ
	               |	Документ.СчетФактураВыданный.Авансы КАК СчетФактураВыданныйАвансы
	               |ГДЕ
	               |	СчетФактураВыданныйАвансы.УдалитьСчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	               |	И СчетФактураВыданныйАвансы.Ссылка.Дата >= ДАТАВРЕМЯ(2009, 1, 1)";
				   
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
	
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			СчетФактура = Выборка.Ссылка.ПолучитьОбъект();
			ТаблицаАвансов = СчетФактура.Авансы.ВыгрузитьКолонки("Номенклатура, Содержание, Сумма, СуммаНДС, СтавкаНДС");
		
			Для Каждого СтрокаАвансы Из СчетФактура.Авансы Цикл
				Если Не ЗначениеЗаполнено(СтрокаАвансы.УдалитьСчетНаОплату) Тогда
					НоваяСтрока = ТаблицаАвансов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАвансы);
				Иначе
					ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(СчетФактура.ПолучитьТаблицуАвансовПоСчетуНаОплату(СтрокаАвансы.Сумма, СтрокаАвансы.СуммаНДС, СтрокаАвансы.СтавкаНДС, СтрокаАвансы.УдалитьСчетНаОплату),
																ТаблицаАвансов);
				КонецЕсли;
			КонецЦикла;
			
			СчетФактура.Авансы.Загрузить(ТаблицаАвансов);
			
			СчетФактура.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура выданный"" в связи с уточнениями законодательства в части учета НДС, внесенными письмом Минфина от 6 марта 2009 г. № 03-07-15/39 завершено");
	
КонецПроцедуры
