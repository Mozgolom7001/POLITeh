
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку(истина);

КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) Экспорт

		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Запущена обработка " + ЭтаФорма.Заголовок);

		СообщитьОСостоянииОбновления("","Обновление данных регистра НДС Партии запасов");

		ОчиститьПустыеСсылкиРеквизитаСчетФактураВРегистреПартий();
		СообщитьОСостоянииОбновления("","Обновление данных регистра НДС Партии запасов завершено");
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

// Обновление данных в регистре НДСПартииТоваров
// Очистка пустых ссылок на документы в реквизите "Счет-фактура" в регистре "НДС по партиям запасов"
Процедура ОчиститьПустыеСсылкиРеквизитаСчетФактураВРегистреПартий()
	
	ТипыИзмеренияСчетФактура = Метаданные.РегистрыНакопления.НДСПартииТоваров.Измерения.СчетФактура.Тип.Типы();
	МассивПустыхСсылок = Новый Массив();
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	Счетчик = 0;
	Для каждого ТипИзмерения Из ТипыИзмеренияСчетФактура Цикл
		Если не ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			| Объединить все
			|";
		КонецЕсли; 
		Счетчик = Счетчик + 1;
		ТекстЗапроса = ТекстЗапроса +СтрЗаменить(
		"ВЫБРАТЬ
		|	НДСПартииТоваровОбороты.Регистратор КАК Регистратор,
		|	НДСПартииТоваровОбороты.НомерСтроки,
		|	НДСПартииТоваровОбороты.СчетФактура
		|ИЗ
		|	РегистрНакопления.НДСПартииТоваров.Обороты(, , Запись, СчетФактура = &ПустаяСсылка) КАК НДСПартииТоваровОбороты"
		,"ПустаяСсылка","ПустаяСсылка"+Счетчик);
		Запрос.УстановитьПараметр("ПустаяСсылка"+Счетчик, Новый(ТипИзмерения));
	КонецЦикла; 
	
	ТекстЗапроса = ТекстЗапроса +
	"
	|ИТОГИ ПО
	|	Регистратор";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаПоРегистраторам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НаборЗаписей = РегистрыНакопления.НДСПартииТоваров.СоздатьНаборЗаписей();
	Пока ВыборкаПоРегистраторам.Следующий() Цикл
		НаборЗаписей.Отбор.Регистратор.Значение = ВыборкаПоРегистраторам.Регистратор;
		НаборЗаписей.Прочитать();
			
		ВыборкаПоЗаписям = ВыборкаПоРегистраторам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоЗаписям.Следующий() Цикл
			 НаборЗаписей[ВыборкаПоЗаписям.НомерСтроки -1].СчетФактура = Неопределено; 
		КонецЦикла; 
		НаборЗаписей.Записать();
	КонецЦикла;

КонецПроцедуры



 Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры



