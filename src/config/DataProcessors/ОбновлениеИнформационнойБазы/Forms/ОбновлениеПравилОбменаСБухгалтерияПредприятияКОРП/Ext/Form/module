
Перем мЗаполнениеВыполнено;

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки является необратимой операцией.
	|Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() Экспорт
	
	СообщитьОСостоянииОбновления("Обмен данными","Обновление правил обмена с конфигурацией ""Бухгалтерия предприятия, редакция 2.0""");

	ОбновитьПравилаОбмена();
	
	СообщитьОСостоянииОбновления("Обмен данными","Обновление правил обмена с конфигурацией ""Бухгалтерия предприятия, редакция 2.0"" завершено");
	
	
КонецПроцедуры

Процедура КоманднаяПанельНастройкиОбменаДействиеУстановитьФлажки(Кнопка)
	
	НастройкиОбмена.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельНастройкиОбменаДействиеСнятьФлажки(Кнопка)
	
	НастройкиОбмена.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьСписокНастроек()
	
	Если мЗаполнениеВыполнено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменаДанными.Ссылка
	|ИЗ
	|	Справочник.НастройкиОбменаДанными КАК НастройкиОбменаДанными
	|ГДЕ
	|	НастройкиОбменаДанными.УзелИнформационнойБазы ССЫЛКА ПланОбмена.ОбменУправлениеТорговлейБухгалтерияКОРП";
	
	НастройкиОбмена.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	НастройкиОбмена.ЗаполнитьПометки(Истина);
	
	мЗаполнениеВыполнено = Истина;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = Не ПолучитьРазрешениеНаОткрытиеФормы();
	
	ЗаполнитьСписокНастроек();
	
КонецПроцедуры

// Процедура выполняет обновление правила обмена
// Запускается при обновлении на 10.3.4
Процедура ОбновитьПравилаОбмена() Экспорт
	
	ЗаполнитьСписокНастроек();
	
	Макет = ПолучитьОбщийМакет("ПравилаОбмена_УТ_БПКОРП");
	МакетБП_УТ = ПолучитьОбщийМакет("ПравилаОбмена_БПКОРП_УТ");
	
	ТекстПравилОбмена = Макет.ПолучитьТекст();
	ТекстПравилОбменаБП_УТ = МакетБП_УТ.ПолучитьТекст();
	
	Для каждого Настройка Из НастройкиОбмена Цикл
		Если Настройка.Пометка Тогда
			Объект = Настройка.Значение.ПолучитьОбъект();
			Объект.ПравилаОбмена = Новый ХранилищеЗначения(ТекстПравилОбмена);
			Если Объект.ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезComСоединение Тогда
				// Обновление правил обмена для COM настройки.
				ТекстПравилПриемника = Объект.ПравилаОбменаДляПриемника.Получить();
				Если ТипЗнч(ТекстПравилПриемника) = Тип("Строка") И СтрЧислоСтрок(ТекстПравилПриемника) > 50 Тогда
					// Правила обмена необходимо обновить только если, настройка двухстороннего обмена.
					// Если настройка двухсторонняя, то в реквизите ПравилаОбменаДляПриемника хранятся не пустые правила,
					// количество строк которых больше 50.
					// В противном случае, настройка односторонняя, обновление правил не требуется.
					Объект.ПравилаОбменаДляПриемника = Новый ХранилищеЗначения(ТекстПравилОбменаБП_УТ);
				КонецЕсли;
			КонецЕсли;
			Объект.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

мЗаполнениеВыполнено = Ложь;
