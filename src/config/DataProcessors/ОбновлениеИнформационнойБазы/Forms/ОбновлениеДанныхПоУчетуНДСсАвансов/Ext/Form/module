
Процедура ВыполнитьОбработку()Экспорт
	
	ОбновитьДвиженияНДСсАвансовВидЦенности();
	ОбновитьСчетаФактурыВыданныеНаАванс();
	ОбновитьСчетаФактурыПолученные();
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьОбработку();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

Процедура ОбновитьДвиженияНДСсАвансовВидЦенности() Экспорт
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных регистра ""НДС с авансов"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ");
	
	НаборЗаписей = РегистрыНакопления.НДСсАвансов.СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НДСсАвансов.Регистратор
	               |ИЗ
	               |	РегистрНакопления.НДСсАвансов КАК НДСсАвансов
	               |ГДЕ
	               |	НДСсАвансов.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)";
				   
	ТаблицаРегистраторов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаРегистраторов Цикл
	
		НаборЗаписей.Отбор.Регистратор.Установить(СтрокаТаблицы.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Если Запись.ВидЦенности = Перечисления.ВидыЦенностей.ПустаяСсылка() Тогда
				Запись.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные;
			КонецЕсли;
		КонецЦикла;
		
		Если НаборЗаписей.Модифицированность() Тогда
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных регистра ""НДС с авансов"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ завершено");
	
КонецПроцедуры

Процедура ОбновитьСчетаФактурыВыданныеНаАванс() Экспорт
	
	//Обновление документов "Счет-фактура выданный" на аванс - перенос данных о сумме аванса из шапки в ТЧ.
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура выданный"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетФактураВыданный.Ссылка
	               |ИЗ
	               |	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	               |ГДЕ
	               |	СчетФактураВыданный.ВидСчетаФактуры = Значение(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)";
				   
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		СчетФактура = СтрокаТаблицы.Ссылка.ПолучитьОбъект();
		Если СчетФактура.Авансы.Количество() = 0 Тогда
			
			// Новая строка ТЧ Авансы по данным шапки
			НоваяСтрока = СчетФактура.Авансы.Добавить();
			НоваяСтрока.Сумма = СчетФактура.Сумма;
			НоваяСтрока.СуммаНДС = СчетФактура.СуммаНДС;
			НоваяСтрока.СтавкаНДС = СчетФактура.СтавкаНДС;
			
			СчетФактура.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура выданный"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ завершено");
	
КонецПроцедуры
	
Процедура ОбновитьСчетаФактурыПолученные() Экспорт
	
	//Обновление документов "Счет-фактура выданный" на аванс - перенос данных о сумме аванса из шапки в ТЧ.
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура полученный"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетФактураПолученный.Ссылка
	               |ИЗ
	               |	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	               |ГДЕ
	               |	СчетФактураПолученный.ДоговорКонтрагента = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	               |	И СчетФактураПолученный.ВидСчетаФактуры <> Значение(Перечисление.ВидСчетаФактурыПолученного.НаАванс)";
				   
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		СчетФактура = СтрокаТаблицы.Ссылка.ПолучитьОбъект();
		СчетФактура.ОпределениеПараметровСчетаФактуры();
		СчетФактура.Записать();
			
	КонецЦикла;
	
	СообщитьОСостоянииОбновления("Учет НДС", "Обновление данных документов ""Счет-фактура полученный"" в связи с изменениями законодательства в части учета НДС, внесенными Федеральным законом от 26.11.2008 № 224-ФЗ завершено");
	
КонецПроцедуры
	
