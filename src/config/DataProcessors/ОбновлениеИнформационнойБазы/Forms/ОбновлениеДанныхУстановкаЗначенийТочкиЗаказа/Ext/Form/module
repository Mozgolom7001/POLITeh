Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = НЕ ПолучитьРазрешениеНаОткрытиеФормы();

КонецПроцедуры // ПередОткрытием()

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		
		Возврат;
		
	КонецЕсли;
	
    ВыполнитьОбработку();
	
КонецПроцедуры // КнопкаВыполнитьНажатие()

Процедура ВыполнитьОбработку() Экспорт
	
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("", ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаУстановкаЗначенийТочкиЗаказа();
	
	СообщитьОСостоянииОбновления("", ЭтаФорма.Заголовок + " завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

// Обновление реквизитов документа "УстановкаЗначенийТочкиЗаказа".
// Заполнение реквизитов "ЕдиницаИзмерения" и "Коэффициент".
//
Процедура ОбновлениеРеквизитовДокументаУстановкаЗначенийТочкиЗаказа()
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ
	|	УстановкаЗначенийТочкиЗаказаТовары.НомерСтроки,
	|	УстановкаЗначенийТочкиЗаказаТовары.Ссылка КАК ДокументСсылка,
	|	УстановкаЗначенийТочкиЗаказаТовары.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	УстановкаЗначенийТочкиЗаказаТовары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент
	|ИЗ
	|	Документ.УстановкаЗначенийТочкиЗаказа.Товары КАК УстановкаЗначенийТочкиЗаказаТовары
	|ГДЕ
	|	(УстановкаЗначенийТочкиЗаказаТовары.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ИЛИ УстановкаЗначенийТочкиЗаказаТовары.Коэффициент = 0)");
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущийОбновляемыйДокументСсылка = Неопределено;
	ТекущийОбновляемыйДокументОбъект = Неопределено;
	
	ВыборкаСтрок = Результат.Выбрать();
	
	Пока ВыборкаСтрок.Следующий() Цикл

		Если ТекущийОбновляемыйДокументСсылка <> ВыборкаСтрок.ДокументСсылка Тогда
			
			Если ТекущийОбновляемыйДокументОбъект <> Неопределено Тогда
				
				ТекущийОбновляемыйДокументОбъект.Записать();
				
			КонецЕсли;

			ТекущийОбновляемыйДокументСсылка = ВыборкаСтрок.ДокументСсылка;
			ТекущийОбновляемыйДокументОбъект = ВыборкаСтрок.ДокументСсылка.ПолучитьОбъект();
			
		КонецЕсли;
		
		ТекущийОбновляемыйДокументОбъект.Товары[ВыборкаСтрок.НомерСтроки - 1].ЕдиницаИзмерения = ВыборкаСтрок.ЕдиницаИзмерения;
		ТекущийОбновляемыйДокументОбъект.Товары[ВыборкаСтрок.НомерСтроки - 1].Коэффициент = ВыборкаСтрок.Коэффициент;
		
	КонецЦикла;
	
	Если ТекущийОбновляемыйДокументОбъект <> Неопределено Тогда
		
		ТекущийОбновляемыйДокументОбъект.Записать();
		
	КонецЕсли;
	 
КонецПроцедуры // ОбновлениеРеквизитовДокументаУстановкаЗначенийТочкиЗаказа()
