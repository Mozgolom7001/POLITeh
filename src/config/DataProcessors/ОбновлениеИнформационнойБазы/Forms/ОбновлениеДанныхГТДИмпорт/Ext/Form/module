
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаГТДИмпорт();
	
	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок+" завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры


// Обновление реквизитов документа ГТДИмпорт 
// Установка признака "Учитывать НСД"
//
Процедура ОбновлениеРеквизитовДокументаГТДИмпорт()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГТДИмпорт.Ссылка
		|ИЗ
		|	Документ.ГТДИмпорт КАК ГТДИмпорт
		|ГДЕ
		|	ГТДИмпорт.ОтражатьВБухгалтерскомУчете
		|	И (НЕ ГТДИмпорт.УчитыватьНДС)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГТДИмпорт.Дата";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
	    ГТДИмпорт_Объект = Выборка.Ссылка.ПолучитьОбъект();
		ГТДИмпорт_Объект.УчитыватьНДС = Истина;
		Попытка
			//Реквизиты автоматически обновляются при записи объекта
			ГТДИмпорт_Объект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
		КонецПопытки;
	
	КонецЦикла;
	 
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
