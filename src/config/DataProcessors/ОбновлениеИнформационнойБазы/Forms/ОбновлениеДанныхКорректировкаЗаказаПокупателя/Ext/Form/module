
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаКорректировкаЗаказаПокупателя();
	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок+" завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

// Обновление реквизитов документа КорректировкаЗаказаПокупателя 
// Заполнение реквизита Организация
//
Процедура ОбновлениеРеквизитовДокументаКорректировкаЗаказаПокупателя()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КорректировкаЗаказаПокупателя.Ссылка КАК Ссылка,
		|	КорректировкаЗаказаПокупателя.ЗаказПокупателя.Организация КАК Организация
		|ИЗ
		|	Документ.КорректировкаЗаказаПокупателя КАК КорректировкаЗаказаПокупателя
		|ГДЕ
		|	КорректировкаЗаказаПокупателя.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
	    Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.Организация = Выборка.Организация;
		
		Попытка
			Объект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
