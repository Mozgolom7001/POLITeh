
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку(истина);

КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) экспорт
	
	Если не флЗапускИзФормы Тогда
		флОбновитьДанныеДокументовПрочиеЗатраты = истина;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);
	
	Если флОбновитьДанныеДокументовПрочиеЗатраты Тогда
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Запущен этап 'Обновить данные документов Прочие затраты', обработка " + ЭтаФорма.Заголовок);

		СообщитьОСостоянииОбновления("","Обновление данных документов Прочие затраты");
		ОбновитьДанныеДокументовПрочиеЗатраты();
		СообщитьОСостоянииОбновления("","Обновление данных документов Прочие затраты завершено");
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Выполнен этап 'Обновить данные документов Прочие затраты', обработка " + ЭтаФорма.Заголовок);

	КонецЕсли;
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

// Процедура прописывает значение реквизита УдалитьЗаказ из шапки документа ПрочиеЗатраты
// в реквизит Заказ табличной части
//
Процедура ОбновитьДанныеДокументовПрочиеЗатраты()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПрочиеЗатраты.Ссылка
	|ИЗ
	|	Документ.ПрочиеЗатраты КАК ПрочиеЗатраты
	|ГДЕ
	|	ПрочиеЗатраты.УдалитьЗаказ <> Неопределено
	|	И ПрочиеЗатраты.УдалитьЗаказ <> &ПустойЗаказ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр( "ПустойЗаказ", Документы.ЗаказПокупателя.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить();
	Обход = РезультатЗапроса.Выбрать();
	Пока Обход.Следующий() Цикл
		флИзмененДокумент = ложь;
		ДокОбъект = Обход.Ссылка.ПолучитьОбъект();
		Для Каждого СтрокаТЧ Из ДокОбъект.Затраты Цикл
			Если СтрокаТЧ.Заказ <> ДокОбъект.УдалитьЗаказ Тогда
				СтрокаТЧ.Заказ = ДокОбъект.УдалитьЗаказ;
				флИзмененДокумент = истина;
			КонецЕсли;	
		КонецЦикла;
		Если флИзмененДокумент Тогда
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры // ОбновитьДанныеДокументовПрочиеЗатраты()

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	флОбновитьДанныеДокументовПрочиеЗатраты = истина;

КонецПроцедуры


