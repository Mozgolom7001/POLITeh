
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьОбработку();
	
КонецПроцедуры

Процедура ВыполнитьОбработку() Экспорт
	
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("","Заполнение регистра сведений Установка проектов для номенклатуры");
		
	ЗаполнитьСоответствиеПроектовНоменклатуре(ТолькоАктуальные,ДатаУстановки,ДатаНач,ДатаКон);
	
	СообщитьОСостоянииОбновления("","Заполнение регистра сведений Установка проектов для номенклатуры завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

// Заполняет регистр сведений "УстановкаПроектовДляНоменклатуры" по данным регистра "Продажи"
//
Процедура ЗаполнитьСоответствиеПроектовНоменклатуре(ТолькоАктуальные,ДатаУстановки,ДатаНач,ДатаКон)
	
	Запрос=Новый Запрос;
	
	Если ТолькоАктуальные Тогда
		
		Запрос.Текст="ВЫБРАТЬ
		|	НоменклатураПериод.Период КАК Период,
		|	НоменклатураПериод.НоменклатураПроекта,
		|	НоменклатураПроектПериод.Проект
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МАКСИМУМ(Продажи.Период) КАК Период,
		|		Продажи.Номенклатура КАК НоменклатураПроекта
		|	ИЗ
		|		РегистрНакопления.Продажи КАК Продажи
		|	ГДЕ
		|		Продажи.Период <= &ДатаУстановки
		|		И (НЕ Продажи.Проект = &ПустойПроект)
		|		И (НЕ Продажи.Номенклатура = &ПустаяНоменклатура)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Продажи.Номенклатура) КАК НоменклатураПериод
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			МАКСИМУМ(Продажи.Период) КАК Период,
		|			Продажи.Номенклатура КАК НоменклатураПроекта,
		|			Продажи.Проект КАК Проект
		|		ИЗ
		|			РегистрНакопления.Продажи КАК Продажи
		|		ГДЕ
		|			Продажи.Период <= &ДатаУстановки
		|			И (НЕ Продажи.Проект = &ПустойПроект)
		|			И (НЕ Продажи.Номенклатура = &ПустаяНоменклатура)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Продажи.Номенклатура,
		|			Продажи.Проект) КАК НоменклатураПроектПериод
		|		ПО НоменклатураПериод.НоменклатураПроекта = НоменклатураПроектПериод.НоменклатураПроекта
		|			И НоменклатураПериод.Период = НоменклатураПроектПериод.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
		
		Запрос.УстановитьПараметр("ПустойПроект",Справочники.Проекты.ПустаяСсылка());
		Запрос.УстановитьПараметр("ПустаяНоменклатура",Справочники.Номенклатура.ПустаяСсылка());
		Запрос.УстановитьПараметр("ДатаУстановки",ДатаУстановки);
		
	Иначе
		
		Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
		             |	Продажи.Период КАК Период,
		             |	Продажи.Номенклатура КАК НоменклатураПроекта,
		             |	Продажи.Проект КАК Проект
		             |ИЗ
		             |	РегистрНакопления.Продажи КАК Продажи
		             |ГДЕ
		             |";	
					 
		ТекстОтборПериод="";
		
		Если НЕ(ДатаНач=Дата('00010101') ИЛИ ДатаКон=Дата('00010101')) Тогда
			
			ТекстОтборПериод="Продажи.Период МЕЖДУ &ДатаНач И &ДатаКон И ";
			
			Запрос.УстановитьПараметр("ДатаНач",ДатаНач);
			Запрос.УстановитьПараметр("ДатаКон",КонецДня(ДатаКон));
			
		ИначеЕсли НЕ ДатаНач=Дата('00010101') Тогда
			
			ТекстОтборПериод="Продажи.Период >= &ДатаНач И ";
			
			Запрос.УстановитьПараметр("ДатаНач",ДатаНач);
			
		ИначеЕсли НЕ ДатаКон=Дата('00010101') Тогда
			
			ТекстОтборПериод="Продажи.Период <= &ДатаКон И ";
			
			Запрос.УстановитьПараметр("ДатаКон",КонецДня(ДатаКон));
			
		КонецЕсли;
		
		Запрос.Текст=Запрос.Текст+ТекстотборПериод+"(НЕ Продажи.Проект = &ПустойПроект)
		             |	И (НЕ Продажи.Номенклатура = &ПустаяНоменклатура)";
	
		Запрос.УстановитьПараметр("ПустойПроект",Справочники.Проекты.ПустаяСсылка());
		Запрос.УстановитьПараметр("ПустаяНоменклатура",Справочники.Номенклатура.ПустаяСсылка());
		
	КонецЕсли;	
	
	НаборЗаписей=РегистрыСведений.УстановкаПроектовДляНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Результат=Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл	
		
		НоваяСтрока=НаборЗаписей.Добавить();
		НоваяСтрока.Период=Результат.Период;
		НоваяСтрока.НоменклатураПроекта=Результат.НоменклатураПроекта;
		НоваяСтрока.Проект=Результат.Проект;
		НоваяСтрока.Активность=Истина;
		
	КонецЦикла;	
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
	КонецПопытки;
	
КонецПроцедуры // ЗаполнитьСоответствиеПроектовНоменклатуре()



Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки настройки периода
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	НП=Новый НастройкаПериода;
	
	НП.УстановитьПериод(НачалоДня(ДатаНач), ?(Не ЗначениеЗаполнено(ДатаКон), ДатаКон, КонецДня(ДатаКон)));
	
	Если НП.Редактировать() Тогда
		
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
			
	КонецЕсли;
	
КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

Процедура ПриОткрытии()
	
	ТолькоАктуальные=Истина;
	ДатаУстановки=РабочаяДата;
	
	ДатаНач=НачалоМесяца(РабочаяДата);
	ДатаКон=КонецМесяца(РабочаяДата);
		
КонецПроцедуры
