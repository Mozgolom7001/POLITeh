Процедура ПеренумерацияДокументов(ДеревоПрефиксов)
	
	//Выберем все последние документы данного вида и с данным префиксом одним запросом
	Запрос = Новый Запрос();
	
	ЭтоПерваяИтерация = Истина;
	Для Каждого СтрокаДеревоДокумент Из ДеревоПрефиксов.Строки Цикл
		Для Каждого СтрокаДеревоПрефикс Из СтрокаДеревоДокумент.Строки Цикл
			Если СтрокаДеревоПрефикс.Пометка ТОгда
				
				//Сначала сформируем список других возможных префиксов данного документа
				СписокДругихПрефиксов = Новый СписокЗначений();
				Для Каждого СтрокаДеревоДругойПрефикс Из СтрокаДеревоДокумент.Строки Цикл
					Если СтрокаДеревоДругойПрефикс.Пометка  И (НЕ СокрЛП(СтрокаДеревоДругойПрефикс.ДокументПрефикс) =СокрЛП(СтрокаДеревоПрефикс.ДокументПрефикс)) ТОгда
						Если НЕ ВРЕГ(СокрЛП(СтрокаДеревоДругойПрефикс.ДокументПрефикс)) =  ВРЕГ(СокрЛП(СтрокаДеревоПрефикс.ДокументПрефикс)) ТОгда
							СписокДругихПрефиксов.Добавить(ВРЕГ(СокрЛП(СтрокаДеревоДругойПрефикс.ДокументПрефикс)));
						КонецЕсли;	
					КонецЕсли;
				КонецЦикла;	
				
				//Теперь сформируем очередной кусочек объединения
				
				ДлиннаПрефикса = СтрДлина(СокрЛП(СтрокаДеревоПрефикс.ДокументПрефикс));
				
				Если Не ЭтоПерваяИтерация Тогда
					Запрос.Текст = Запрос.Текст + "
						|	
						|ОБЪЕДИНИТЬ ВСЕ
						|	
                        |";
				Иначе
					ЭтоПерваяИтерация = Ложь;
				КонецЕсли;	
				
				Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
				               |	ВложенныйЗапрос.Ссылка,
							   |	ВложенныйЗапрос.Номер,
							   |	ВложенныйЗапрос.Префикс
				               |ИЗ
				               |	(ВЫБРАТЬ ПЕРВЫЕ 1
				               |		ТекущаяТаблицаДокумента.Ссылка КАК Ссылка,";
							   Если ДлиннаПрефикса = 0 Тогда
								   //Без префикса
								   Запрос.Текст = Запрос.Текст + "
								   |		ТекущаяТаблицаДокумента.Номер КАК Номер,
								   |		"""" КАК Префикс";
							   Иначе
								   Запрос.Текст = Запрос.Текст + "
				               		|		ПОДСТРОКА(ТекущаяТаблицаДокумента.Номер, "+Строка(ДлиннаПрефикса+1)+", "+Метаданные.Документы[СокрЛП(СтрокаДеревоДокумент.ДокументПрефикс)].ДлинаНомера+") КАК Номер,
									|		ПОДСТРОКА(ТекущаяТаблицаДокумента.Номер, 1, "+Строка(ДлиннаПрефикса)+") КАК Префикс";
								КонецЕсли;
								Запрос.Текст = Запрос.Текст + "
				               |	ИЗ
				               |		Документ."+СокрЛП(СтрокаДеревоДокумент.ДокументПрефикс)+" КАК ТекущаяТаблицаДокумента
				               |	ГДЕ";
							   Если ДлиннаПрефикса = 0 Тогда
								   //Без префикса
								   Запрос.Текст = Запрос.Текст + "
								   |		ТекущаяТаблицаДокумента.Дата >= &ДатаНачала";
							   Иначе
								   Запрос.Текст = Запрос.Текст + "
							   |		ПОДСТРОКА(ТекущаяТаблицаДокумента.Номер, 1,"+ДлиннаПрефикса+") = """ + СокрЛП(СтрокаДеревоПрефикс.ДокументПрефикс) + """
							   |		И ТекущаяТаблицаДокумента.Дата >= &ДатаНачала";
							   КонецЕсли;
							   Если СписокДругихПрефиксов.Количество() > 0 тогда
								   Для Каждого ДругойПрефикс Из СписокДругихПрефиксов Цикл
									   ДлинаДругогоПрефикса = СтрДлина(ДругойПрефикс.Значение);
									   Если ДлинаДругогоПрефикса = 0 Тогда
										   //Без префикса ничего не добавляем
									   Иначе
										   Запрос.Текст = Запрос.Текст + "
						               		|		И НЕ ПОДСТРОКА(ТекущаяТаблицаДокумента.Номер, 1,"+ДлинаДругогоПрефикса+") = """ + ДругойПрефикс.Значение + """";
										КонецЕсли;
									КонецЦикла;
								КонецЕсли;	
								Запрос.Текст = Запрос.Текст + "	   
				               |	УПОРЯДОЧИТЬ ПО
				               |		Номер УБЫВ) КАК ВложенныйЗапрос";
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	Запрос.УстановитьПараметр("ДатаНачала",НачалоГода(ДатаФормированияДокументов));					   
	
	Если СокрЛП(Запрос.Текст) = "" Тогда
		Возврат;//Ничего не делаем, в текущем периоде нет документов
	КонецЕсли;		
	
	ТаблицаДокументовДляПеренумерации = Запрос.Выполнить().Выгрузить();
	
	//Теперь для каждого документа из таблицы создадим документ такого-же вида с такимже префиксом и номером дополненым нулями
	
	Если ПараметрыСеанса.ИспользованиеРИБ Тогда
		ПрефиксУзлаРИБ = ПараметрыСеанса.ПрефиксУзлаРаспределеннойИнформационнойБазы;
	Иначе
		ПрефиксУзлаРИБ = "";
	КонецЕсли;
	ДлинаПрефикса = СтрДлина(ПрефиксУзлаРИБ);
			
	Для Каждого Строка Из ТаблицаДокументовДляПеренумерации Цикл
		
		НовДок = Документы[Строка.Ссылка.Метаданные().Имя].СоздатьДокумент();
		Если НЕ НовДок.Метаданные().Реквизиты.Найти("Комментарий") = Неопределено ТОгда
			НовДок.Комментарий = "Создан обработкой перенумерации документов";
		КонецЕсли;
		ДлинаНомераДок = НовДок.Метаданные().ДлинаНомера - ДлинаПрефикса;
		ЧистыйНомер = СокрЛП(Строка.Номер);
		НовыйНомер = Строка.Префикс;
		
		Если СтрДлина(ЧистыйНомер) + СтрДлина(НовыйНомер) > ДлинаНомераДок Тогда
			Продолжить;
		КонецЕсли;		
		
		Пока СтрДлина(ЧистыйНомер) + СтрДлина(НовыйНомер) < ДлинаНомераДок Цикл
			НовыйНомер = НовыйНомер + "0";
		КонецЦикла;
		НовыйНомер = ПрефиксУзлаРИБ + НовыйНомер + ЧистыйНомер;
		НовДокСсылка = Документы[Строка.Ссылка.Метаданные().Имя].НайтиПоНомеру(НовыйНомер,ДатаФормированияДокументов);
		Если НовДокСсылка.Пустая() Тогда
			НовДок.Номер = НовыйНомер;
			НовДок.Дата = ДатаФормированияДокументов;
			Попытка
				НовДок.Записать();
				НовДокСсылка = НовДок.Ссылка;
				СообщитьОСостоянииОбновления("","Создан документ " + НовДокСсылка);
			Исключение
				СообщитьОСостоянииОбновления("","Не удалось записать документ " + НовДок,СтатусСообщения.Внимание);
				НовДокСсылка = Неопределено;
			КонецПопытки;	
		Иначе
			СообщитьОСостоянииОбновления("","Пропущен документ " + НовДокСсылка);
		КонецЕсли;	
		
		//Теперь добавим информацию в дерево
		УзелДокумента = ДеревоПрефиксов.Строки.Найти(Строка.Ссылка.Метаданные().Имя,"ДокументПрефикс",Ложь);
		Если НЕ УзелДокумента = Неопределено Тогда
			УзелПрефикса = УзелДокумента.Строки.Найти(Строка.Префикс,"ДокументПрефикс",Ложь);
			Если НЕ УзелПрефикса = Неопределено Тогда
				УзелПрефикса.СозданныйДокумент = НовДокСсылка;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

Процедура ОбновитьДеревоДокументов()
	ДеревоДокументов.Строки.Очистить();
	Для Каждого ТекВидДокумена Из Метаданные.Документы Цикл
		Если ТекВидДокумена.ДлинаНомера = 0 ТОгда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ТекВидДокумена.ТипНомера = Метаданные.СвойстваОбъектов.ТипНомераДокумента.Строка Тогда
			Продолжить;
		КонецЕсли;
		
	 	НовСтр = ДеревоДокументов.Строки.Добавить();
		НовСтр.ДокументПрефикс = ТекВидДокумена.Имя;
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТаблицаДокумент.Номер
		               |ИЗ
		               |	Документ."+ТекВидДокумена.Имя+ " КАК ТаблицаДокумент
					   |ГДЕ
					   |	ТаблицаДокумент.Дата >= &ДатаНачала";
		Запрос.УстановитьПараметр("ДатаНачала",НачалоГода(ДатаФормированияДокументов));			   
		ТабПрефиксов = Запрос.Выполнить().Выгрузить();			   
		ЕстьДокументы = ТабПрефиксов.Количество() > 0;
		ТабПрефиксов.Колонки.Добавить("Префикс");
		Для Каждого Строка Из ТабПрефиксов Цикл
			Строка.Номер = СокрЛП(Строка.Номер);
			ТекущийНомерСимвола = 1;
			Строка.Префикс = "";
			Пока (ТекущийНомерСимвола <= СтрДлина(Строка.Номер)) И ((Сред(Строка.Номер,ТекущийНомерСимвола,1) > "9") ИЛИ (Сред(Строка.Номер,ТекущийНомерСимвола,1) < "0")) Цикл
				Строка.Префикс = Строка.Префикс + Сред(Строка.Номер,ТекущийНомерСимвола,1);
				ТекущийНомерСимвола = ТекущийНомерСимвола + 1;
			КонецЦикла;
		КонецЦикла;	
		ТабПрефиксов.Свернуть("Префикс");
		Если ЕстьДокументы И (ТабПрефиксов.Количество() = 0) ТОгда
			НовСтрокаПрефикс = ТабПрефиксов.Добавить();
			НовСтрокаПрефикс.Префикс = "";
		КонецЕсли;
		
		Если ТабПрефиксов.Количество() = 0 Тогда
			НовСтр.Пометка = Ложь;	
		Иначе
			НовСтр.Пометка = Истина;
			Для Каждого Строка Из ТабПрефиксов Цикл
				НовСтрПрефикс = НовСтр.Строки.Добавить();
				НовСтрПрефикс.Пометка = Истина;
				НовСтрПрефикс.ДокументПрефикс = Строка.Префикс;
			КонецЦИкла;	
		КонецЕсли;	
	КонецЦикла;
	ЭлементыФормы.ДеревоДокументов.ОбновитьСтроки();
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработкуПоДереву();

КонецПроцедуры

Процедура ВыполнитьОбработкуПоДереву()
	
	Если НЕ ПараметрыСеанса.ИспользованиеРИБ ТОгда
		Сообщить("Использование РИБ не установлено! Обработка не выполнена!");
		Возврат;
	КонецЕсли;	
	
	ДеревоПрефиксов = ДеревоДокументов.Скопировать();
	ДеревоПрефиксов.Строки.Очистить();
		
	КоличествоЗапросов = 0;
	Для Каждого СтрокаДереваДокументы Из ДеревоДокументов.Строки Цикл
		Если СтрокаДереваДокументы.Строки.Количество() + КоличествоЗапросов > 255 тогда
			ПеренумерацияДокументов(ДеревоПрефиксов);
				
			//Перенесем ссылки на документы обратно
			Для Каждого СтрокаДереваПрефиксовДокументы Из ДеревоПрефиксов.Строки Цикл
				ТекущийУзелОсновногоДерева = ДеревоДокументов.Строки.Найти(СтрокаДереваПрефиксовДокументы.ДокументПрефикс,"ДокументПрефикс",Ложь);
				Если ТекущийУзелОсновногоДерева <> Неопределено Тогда
					Для Каждого СтрокаДереваПрефиксовПрефиксы Из СтрокаДереваПрефиксовДокументы.Строки Цикл
						ТекущийУзелПрефиксаОсновногоДерева = ТекущийУзелОсновногоДерева.Строки.Найти(СтрокаДереваПрефиксовПрефиксы.ДокументПрефикс,"ДокументПрефикс",Ложь);
						Если ТекущийУзелПрефиксаОсновногоДерева <> Неопределено Тогда
							//Нашли нужный узел
							ТекущийУзелПрефиксаОсновногоДерева.СозданныйДокумент = СтрокаДереваПрефиксовПрефиксы.СозданныйДокумент;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
				
			ДеревоПрефиксов.Строки.Очистить();
			КоличествоЗапросов = 0;
		КонецЕсли;
			
		ТекущийУзелДереваПрефиксов = ДеревоПрефиксов.Строки.Добавить();
		ТекущийУзелДереваПрефиксов.Пометка = СтрокаДереваДокументы.Пометка;
		ТекущийУзелДереваПрефиксов.ДокументПрефикс = СтрокаДереваДокументы.ДокументПрефикс;
			
		Для Каждого СтрокаДереваПрефиксы Из СтрокаДереваДокументы.Строки Цикл
			ТекущийУзелДереваПрефиксовПрефикс = ТекущийУзелДереваПрефиксов.Строки.Добавить();
			ТекущийУзелДереваПрефиксовПрефикс.Пометка = СтрокаДереваПрефиксы.Пометка;
			ТекущийУзелДереваПрефиксовПрефикс.ДокументПрефикс = СтрокаДереваПрефиксы.ДокументПрефикс;
			Если СтрокаДереваПрефиксы.Пометка Тогда
				КоличествоЗапросов = КоличествоЗапросов + 1;
			КонецЕсли;	
		КонецЦИкла;
	КонецЦикла;		
	ПеренумерацияДокументов(ДеревоПрефиксов);
				
	//Перенесем ссылки на документы обратно
	Для Каждого СтрокаДереваПрефиксовДокументы Из ДеревоПрефиксов.Строки Цикл
		ТекущийУзелОсновногоДерева = ДеревоДокументов.Строки.Найти(СтрокаДереваПрефиксовДокументы.ДокументПрефикс,"ДокументПрефикс",Ложь);
		Если ТекущийУзелОсновногоДерева <> Неопределено Тогда
			Для Каждого СтрокаДереваПрефиксовПрефиксы Из СтрокаДереваПрефиксовДокументы.Строки Цикл
				ТекущийУзелПрефиксаОсновногоДерева = ТекущийУзелОсновногоДерева.Строки.Найти(СтрокаДереваПрефиксовПрефиксы.ДокументПрефикс,"ДокументПрефикс",Ложь);
				Если ТекущийУзелПрефиксаОсновногоДерева <> Неопределено Тогда
					//Нашли нужный узел
					ТекущийУзелПрефиксаОсновногоДерева.СозданныйДокумент = СтрокаДереваПрефиксовПрефиксы.СозданныйДокумент;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) Экспорт
	
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущен этап 'Обновление номеров документов', обработка " + ЭтаФорма.Заголовок);
	
	ДатаФормированияДокументов = ТекущаяДата();
	ОбновитьДеревоДокументов();
	ВыполнитьОбработкуПоДереву();
	
	СообщитьОСостоянииОбновления("","Обновление номеров документов завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнен этап 'Обновление номеров документов', обработка " + ЭтаФорма.Заголовок);
КонецПроцедуры	

 Процедура ОсновныеДействияФормыОбновитьДерево(Кнопка)

	ОбновитьДеревоДокументов();

 КонецПроцедуры

 Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
