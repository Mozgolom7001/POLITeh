
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьОбработку(истина);

КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) Экспорт
	
    СообщитьОСостоянииОбновления("Управление заказами","Заполнение нового реквизита «Внутренний заказ» табличных частей документов «Перемещение товаров»");

	ЗаполнениеРеквизитаВнутреннийЗаказВДокументеПеремещениеТоваров();
	СообщитьОСостоянииОбновления("Управление заказами","Заполнение нового реквизита «Внутренний заказ» табличных частей документов «Перемещение товаров» завершено");
	
	
 КонецПроцедуры


Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
КонецПроцедуры

// Возвращает список документов Перемещение товаров, для которых нужно заполнять реквизит табличной части ВнутреннийЗаказ.
//
Функция ПолучитьДокументовПеремещениеТоваров_ВнутренниеЗаказы(СсылкаНач)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|
	|	(ВЫБРАТЬ
	|		ДокТовары.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПеремещениеТоваров.Товары КАК ДокТовары
	|	ГДЕ
	|		ДокТовары.ВнутреннийЗаказ = &ПустойВнутреннийЗаказ И ДокТовары.Ссылка.ВнутреннийЗаказ <> &ПустойВнутреннийЗаказ"
	+?(СсылкаНач = Неопределено,""," И ДокТовары.Ссылка > &СсылкаНач ")+"
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ДокТара.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПеремещениеТоваров.ВозвратнаяТара КАК ДокТара
	|	ГДЕ
	|		ДокТара.ВнутреннийЗаказ = &ПустойВнутреннийЗаказ И ДокТара.Ссылка.ВнутреннийЗаказ <> &ПустойВнутреннийЗаказ"
	+?(СсылкаНач = Неопределено,""," И ДокТара.Ссылка > &СсылкаНач ")+"

	|  ) КАК Док
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СсылкаНач", 				СсылкаНач);
	Запрос.УстановитьПараметр("ПустойВнутреннийЗаказ",	Документы.ВнутреннийЗаказ.ПустаяСсылка());

	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции // ПолучитьДокументовПеремещениеТоваров_ВнутренниеЗаказы()


//Процедура заполняет новый реквизит ВнутреннийЗаказ в табличных частях документа "ПеремещениеТоваров"
//Запускается при обновлении на 1.2.5
Процедура ЗаполнениеРеквизитаВнутреннийЗаказВДокументеПеремещениеТоваров() Экспорт
	Если не УправлениеЗаказами.ИспользоватьВнутренниеЗаказы() Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьДокументы = Истина;
	ДокументСсылка = Неопределено;
	ТипВнутреннийЗаказ = Тип("ДокументСсылка.ВнутреннийЗаказ");
	Пока ЕстьДокументы Цикл
			
		ВыборкаДокументов = ПолучитьДокументовПеремещениеТоваров_ВнутренниеЗаказы(ДокументСсылка);
			
		ЕстьДокументы = ВыборкаДокументов.Количество()>0;
			
		Пока ВыборкаДокументов.Следующий() цикл
	
			ДокументСсылка = ВыборкаДокументов.ссылка;
			ДокументОбъект = ВыборкаДокументов.ссылка.ПолучитьОбъект();	
			текВнутреннийЗаказ = ДокументСсылка.ВнутреннийЗаказ;
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.Товары Цикл
				СтрокаТабличнойЧасти.ВнутреннийЗаказ = текВнутреннийЗаказ;
				Если СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.СоСклада Тогда
					СтрокаТабличнойЧасти.ДокументРезерва = "";
				ИначеЕсли СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.ИзРезерва И 
					СтрокаТабличнойЧасти.ДокументРезерва<>текВнутреннийЗаказ Тогда
					СтрокаТабличнойЧасти.ДокументРезерва = текВнутреннийЗаказ;
				КонецЕсли;
			КонецЦикла;
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.ВозвратнаяТара Цикл
				СтрокаТабличнойЧасти.ВнутреннийЗаказ = текВнутреннийЗаказ;
				Если СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.СоСклада Тогда
					СтрокаТабличнойЧасти.ДокументРезерва = "";
				ИначеЕсли СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.ИзРезерва И 
					СтрокаТабличнойЧасти.ДокументРезерва<>текВнутреннийЗаказ Тогда
					СтрокаТабличнойЧасти.ДокументРезерва = текВнутреннийЗаказ;
				КонецЕсли;
			КонецЦикла;

			Если ДокументОбъект.Модифицированность() Тогда
				ДокументОбъект.Записать();
			Конецесли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры
