
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьОбработку(истина);

КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) Экспорт
	
    СообщитьОСостоянииОбновления("Управление заказами","Заполнение нового реквизита «Внутренний заказ» документов «Требование-накладная»");

	ЗаполнениеРеквизитаВнутреннийЗаказВДокументеТребованиеНакладная();
	СообщитьОСостоянииОбновления("Управление заказами","Заполнение нового реквизита «Внутренний заказ» документов «Требование-накладная» завершено");
	
	
 КонецПроцедуры


Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
КонецПроцедуры

// Возвращает список документов, для которых нужно обновлять реквизиты.
//
Функция ПолучитьТаблицуДокументовТребованиеНакладная_ВнутренниеЗаказыИСписаниеИзРезерва(СсылкаНач)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокМатериалы.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ТребованиеНакладная.Материалы КАК ДокМатериалы
	|	ГДЕ
	|		(ДокМатериалы.Заказ ССЫЛКА Документ.ВнутреннийЗаказ И ДокМатериалы.ВнутреннийЗаказ = &ПустойВнутреннийЗаказ)  
	|	ИЛИ (ДокМатериалы.УдалитьСпособСписанияОстаткаТоваров=&ИзРезерва И ДокМатериалы.ЗаказРезерв=неопределено)"
	+?(СсылкаНач = Неопределено,""," И ДокМатериалы.Ссылка > &СсылкаНач ")+"
	|  ) КАК Док
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СсылкаНач", 				СсылкаНач);
	Запрос.УстановитьПараметр("ПустойВнутреннийЗаказ",	Документы.ВнутреннийЗаказ.ПустаяСсылка());
    Запрос.УстановитьПараметр("ИзРезерва", 				Перечисления.СпособыСписанияОстаткаТоваров.ИзРезерва);

	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции // ПолучитьТаблицуПроизводственныхДокументовТребованиеНакладная_Заказы()


//Процедура заполняет новый реквизит ВнутреннийЗаказ в документе "Требование-накладная"
//Запускается при обновлении на 1.2.5
Процедура ЗаполнениеРеквизитаВнутреннийЗаказВДокументеТребованиеНакладная() Экспорт
	
	ЕстьДокументы = Истина;
	ДокументСсылка = Неопределено;
	ТипВнутреннийЗаказ = Тип("ДокументСсылка.ВнутреннийЗаказ");
	Пока ЕстьДокументы Цикл
			
		ВыборкаДокументов = ПолучитьТаблицуДокументовТребованиеНакладная_ВнутренниеЗаказыИСписаниеИзРезерва(ДокументСсылка);
			
		ЕстьДокументы = ВыборкаДокументов.Количество()>0;
			
		Пока ВыборкаДокументов.Следующий() цикл
	
			ДокументСсылка = ВыборкаДокументов.ссылка;
			ДокументОбъект = ВыборкаДокументов.ссылка.ПолучитьОбъект();		
			Для Каждого СтрокаТабличнойЧасти Из ДокументОбъект.Материалы Цикл
				Если СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.СоСклада Тогда
					СтрокаТабличнойЧасти.ЗаказРезерв = "";
				ИначеЕсли СтрокаТабличнойЧасти.УдалитьСпособСписанияОстаткаТоваров = Перечисления.СпособыСписанияОстаткаТоваров.ИзРезерва И  СтрокаТабличнойЧасти.ЗаказРезерв=неопределено Тогда
					СтрокаТабличнойЧасти.ЗаказРезерв = СтрокаТабличнойЧасти.Заказ;
				КонецЕсли;

				Если ТипЗнч(СтрокаТабличнойЧасти.Заказ) = ТипВнутреннийЗаказ и ЗначениеЗаполнено(СтрокаТабличнойЧасти.Заказ) Тогда
					СтрокаТабличнойЧасти.ВнутреннийЗаказ = СтрокаТабличнойЧасти.Заказ;
					СтрокаТабличнойЧасти.Заказ = "";
				КонецЕсли;
			КонецЦикла;
			
			Если ДокументОбъект.Модифицированность() Тогда
				ДокументОбъект.Записать();
			Конецесли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры
