
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() Экспорт
	
    СообщитьОСостоянииОбновления("Учет НДС","Заполнение нового реквизита «НДС в стоимости товаров» в документах «Перемещение товаров» и «Комплектация номенклатуры»");

	ЗаполнениеРеквизитаНДСВСтоимостиТоваров();
	
	СообщитьОСостоянииОбновления("Учет НДС","Заполнение нового реквизита «НДС в стоимости товаров» в документах «Перемещение товаров» и «Комплектация номенклатуры» завершено");
	
	
 КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = Не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

//Процедура заполняет новый реквизит "НДС в стоимости товаров" в документах "Перемещение товаров" и "Комплектация номенклатуры"
//Запускается при обновлении на 10.3.1
Процедура ЗаполнениеРеквизитаНДСВСтоимостиТоваров() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПеремещениеТоваров.Ссылка
	               |ИЗ
	               |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	               |ГДЕ
	               |	ПеремещениеТоваров.НДСвСтоимостиТоваров = ЗНАЧЕНИЕ(Перечисление.ДействиеНДСВСтоимостиТоваров.ПустаяСсылка)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	КомплектацияНоменклатуры.Ссылка
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК КомплектацияНоменклатуры
	               |ГДЕ
	               |	КомплектацияНоменклатуры.НДСвСтоимостиТоваров = ЗНАЧЕНИЕ(Перечисление.ДействиеНДСВСтоимостиТоваров.ПустаяСсылка)";
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.НДСВСтоимостиТоваров = Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);

	КонецЦикла;
	
КонецПроцедуры
