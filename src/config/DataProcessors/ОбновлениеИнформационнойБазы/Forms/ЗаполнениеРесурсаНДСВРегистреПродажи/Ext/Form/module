
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() Экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	ВыполнитьЗаполнениеРесурсаНДСВРегистреПродажи();
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры

Процедура ВыполнитьЗаполнениеРесурсаНДСВРегистреПродажи()

	ТаблицаОбрабатываемыхДокументов = новый ТаблицаЗначений;
	ТаблицаОбрабатываемыхДокументов.Колонки.Добавить("ИмяДокумента");
	ТаблицаОбрабатываемыхДокументов.Колонки.Добавить("СписокТЧ");

	нстр = ТаблицаОбрабатываемыхДокументов.Добавить();
	нстр.ИмяДОкумента = "ВозвратТоваровОтПокупателя";
	Список = новый СписокЗначений;
	Список.Добавить("Товары");
	нстр.СписокТЧ = Список;
	
	нстр = ТаблицаОбрабатываемыхДокументов.Добавить();
	нстр.ИмяДОкумента = "ОтчетКомиссионераОПродажах";
	Список = новый СписокЗначений;
	Список.Добавить("Товары");
	нстр.СписокТЧ = Список;
	
	нстр = ТаблицаОбрабатываемыхДокументов.Добавить();
	нстр.ИмяДОкумента = "ОтчетОРозничныхПродажах";
	Список = новый СписокЗначений;
	Список.Добавить("Товары");
	нстр.СписокТЧ = Список;


	нстр = ТаблицаОбрабатываемыхДокументов.Добавить();
	нстр.ИмяДОкумента = "ОтчетКомитентуОПродажах";
	Список = новый СписокЗначений;
	Список.Добавить("Товары");
	нстр.СписокТЧ = Список;

	нстр = ТаблицаОбрабатываемыхДокументов.Добавить();
	нстр.ИмяДОкумента = "РеализацияТоваровУслуг";
	Список = новый СписокЗначений;
	Список.Добавить("Товары");
	Список.Добавить("Услуги");
	нстр.СписокТЧ = Список;

	НаборЗаписей = РегистрыНакопления.Продажи.СоздатьНаборЗаписей();


	Для каждого СтрокаТаблицы Из ТаблицаОбрабатываемыхДокументов цикл
		ИмяДокумента = СтрокаТаблицы.ИмяДокумента;
		СписокТЧ = СтрокаТаблицы.СписокТЧ;
		ЕстьДокументы = Истина;
		ДокументСсылка = Неопределено;
		
		Пока ЕстьДокументы Цикл
            РезультатЗапроса = ПолучитьТаблицуДокументовПродажи(ДокументСсылка, ИмяДокумента, СписокТЧ);
			
			ЕстьДокументы = НЕ РезультатЗапроса.Пустой();
			
			//обход по документам
			ВыборкаПоДокументам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоДокументам.Следующий() Цикл
				
				ДокументСсылка = ВыборкаПоДокументам.Регистратор;
				
				ВыборкаПоНоменклатуре = ВыборкаПоДокументам.Выбрать();
				
			    //создание набора записей в регистр
				НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
                НаборЗаписей.Прочитать();

				флИзмененыДвижения = ложь;
				Для каждого Запись из НаборЗаписей цикл
					Если Запись.НДС<>0 Тогда Продолжить; КонецЕсли;
					//т.к. по одной номенклатуре может быть несколько записей, надо каждый раз обновлять выборку
					ВыборкаПоНоменклатуре = ВыборкаПоДокументам.Выбрать();

					текНоменклатура = Запись.Номенклатура;
					//в данном документе ставка НДС определяется в шапке.
					//	Запрос построен так, что результат содержит одну строку с Номенклатура=неопределено
					Если  ИмяДокумента = "ОтчетКомитентуОПродажах" Тогда
						текНоменклатура = неопределено;
					КонецЕсли;
					
					Если ВыборкаПоНоменклатуре.НайтиСледующий(текНоменклатура,"Номенклатура") Тогда
						ставкаНДС = УчетНДС.ПолучитьСтавкуНДС(ВыборкаПоНоменклатуре.СтавкаНДС);
						Если СтавкаНДС<>0 Тогда
							флИзмененыДвижения = истина;
							Запись.НДС = Запись.Стоимость * ставкаНДС / (100+ставкаНДС);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				Если флИзмененыДвижения Тогда
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьТаблицуДокументовПродажи(СсылкаНач, ИмяДокумента, СписокТЧ)
	//получим список документов для обработки
	//выполняем это отдельным запросом для того, чтобы можно было корректно ограничить
	//	количество обрабатываемых в один прием документов
	
	ТекстЗапроса = " ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	ДокументРег.Ссылка КАК Регистратор
	|ИЗ 
	|	Документ."+ИмяДокумента+" КАК ДокументРег
	|ГДЕ
	|	ДокументРег.Проведен = истина "+
		?(СсылкаНач = Неопределено,"","
	|	И ДокументРег.Ссылка > &СсылкаНач")+"
	|Упорядочить по Регистратор"; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СсылкаНач", 				СсылкаНач);

	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	СписокДокументов = ТаблицаДокументов.ВыгрузитьКолонку("Регистратор");
	
	ТекстЗапроса = "";
	Если СписокТЧ.Количество()=1 Тогда
		Если ИмяДокумента = "ОтчетКомитентуОПродажах" Тогда
			ТекстЗапроса="ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка КАК Регистратор,
			|неопределено КАК Номенклатура, 
			|СтавкаНДСВознаграждения КАК СтавкаНДС
			|ИЗ Документ."+ИмяДокумента+"
			|ГДЕ Ссылка в (&СписокДокументов)
			|Итоги по Регистратор";
		Иначе	
			ТекстЗапроса="ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка КАК Регистратор,
			|Номенклатура, 
			|СтавкаНДС
			|ИЗ Документ."+ИмяДокумента+"."+СписокТЧ[0].Значение+"
			|ГДЕ Ссылка в (&СписокДокументов)
			|Итоги по Регистратор";
		КонецЕсли;	
	Иначе
		Для каждого элементТЧ из СписокТЧ цикл
			Если ТекстЗапроса<>"" Тогда
				ТекстЗапроса = ТекстЗапроса + " ОБЪЕДИНИТЬ ВСЕ ";
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка, Номенклатура, СтавкаНДС
			|ИЗ Документ."+ИмяДокумента+"."+элементТЧ.Значение+"
			|ГДЕ Ссылка в (&СписокДокументов)";
		Конеццикла;
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка КАК Регистратор,
		|Номенклатура, СтавкаНДС
		|ИЗ ("+ТекстЗапроса+") КАК ВложенныйЗапрос
		|Итоги по Регистратор";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	
	Возврат Запрос.Выполнить();

КонецФункции

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры


