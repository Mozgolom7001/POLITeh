
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
    ВыполнитьОбработку();

КонецПроцедуры

Процедура ВыполнитьОбработку() экспорт
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок);

	ОбновлениеРеквизитовДокументаСчетФактураВыданный();
	СообщитьОСостоянииОбновления("",ЭтаФорма.Заголовок+" завершено");
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

КонецПроцедуры


// Обновление реквизитов документа СчетФактураВыданный 
// Заполнение ТЧ ДокументыОснования по реквизиту шапки документа
// Обновление реквизита СуммаДокумента для счетов-фактур на аванс
//
Процедура ОбновлениеРеквизитовДокументаСчетФактураВыданный()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка,
		|	СчетФактураВыданный.ДокументОснование,
		|	ВЫБОР
		|		КОГДА СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НаАванс,
		|	СчетФактураВыданный.СформированПриВводеНачальныхОстатковНДС,
		|	СчетФактураВыданный.НомерПлатежноРасчетногоДокумента,
		|	СчетФактураВыданный.ДатаПлатежноРасчетногоДокумента
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
		|		ПО (СчетФактураВыданныйДокументыОснования.Ссылка = СчетФактураВыданный.Ссылка)
		|ГДЕ
		|	СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураВыданный.Дата";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	    СФОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если СФОбъект.ДокументыОснования.Найти(Выборка.ДокументОснование) = неопределено Тогда
			СФОбъект.ДокументыОснования.Добавить().ДокументОснование = Выборка.ДокументОснование;
		КонецЕсли; 
		Если Выборка.НаАванс Тогда
			СФОбъект.СуммаДокумента = СФОбъект.УдаленВалютнаяСумма;
		КонецЕсли; 
		
		Если СФОбъект.ДатаНомерДокументовОплаты.Количество()= 0 Тогда
			НоваяСтрокаПРД = СФОбъект.ДатаНомерДокументовОплаты.Добавить();
			НоваяСтрокаПРД.ДатаПлатежноРасчетногоДокумента	= Выборка.ДатаПлатежноРасчетногоДокумента;
			НоваяСтрокаПРД.НомерПлатежноРасчетногоДокумента	= Выборка.НомерПлатежноРасчетногоДокумента;
		КонецЕсли;

		Если СФОБъект.СформированПриВводеНачальныхОстатковНДС Тогда
			СФОБъект.ОпределениеПараметровСчетаФактуры(СФОБъект);
		КонецЕсли; 
		
		Попытка
			СФОбъект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщить("Ошибка при записи документа «"+Выборка.Ссылка+"». Реквизиты не обновлены.");
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры
