
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ТекстВопроса = 
	"Выполнение данной обработки может занять продолжительное время
	|и является необратимой операцией. Настоятельно рекомендуется предварительно ознакомиться
	|cо справочной информацией. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

    ВыполнитьОбработку(истина);

КонецПроцедуры

Процедура ВыполнитьОбработку(флЗапускИзФормы=ложь) Экспорт
	Если не флЗапускИзФормы Тогда
		флОбновитьАдресаКонтактнойИнформации = истина;
		флКорректировкаСправочникаСерииНоменклатуры = истина;
		флКорректировкаСправочникаНоменклатура = истина;
		флКорректировкаРегистраГражданствоФизЛиц = истина;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Запущена обработка " + ЭтаФорма.Заголовок);

	Если флОбновитьАдресаКонтактнойИнформации Тогда
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Запущен этап 'Обновить адреса контактной информации', обработка " + ЭтаФорма.Заголовок);

		СообщитьОСостоянииОбновления("","Обновление адресов контактной информации");

		ОбновитьАдресаКонтактнойИнформации();
		СообщитьОСостоянииОбновления("","Обновление адресов контактной информации завершено");
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Выполнен этап 'Обновить адреса контактной информации', обработка " + ЭтаФорма.Заголовок);

	КонецЕсли;
	Если флКорректировкаСправочникаСерииНоменклатуры Тогда
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Запущен этап 'Корректировка справочника Серии номенклатуры', обработка " + ЭтаФорма.Заголовок);

		СообщитьОСостоянииОбновления("","Корректировка справочника Серии номенклатуры");

		КорректировкаСправочникаСерииНоменклатурыРоссия();
		СообщитьОСостоянииОбновления("","Корректировка справочника Серии номенклатуры завершено");
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Выполнен этап 'Корректировка справочника Серии номенклатуры', обработка " + ЭтаФорма.Заголовок);

	КонецЕсли;
	Если флКорректировкаСправочникаНоменклатура Тогда
		ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Запущен этап 'Корректировка справочника Номенклатура', обработка " + ЭтаФорма.Заголовок);

		СообщитьОСостоянииОбновления("","Корректировка справочника Номенклатура");
		КорректировкаСправочникаНоменклатураРоссия();
		СообщитьОСостоянииОбновления("","Корректировка справочника Номенклатура завершено");
        ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
		"Выполнен этап 'Корректировка справочника Номенклатура', обработка " + ЭтаФорма.Заголовок);

	КонецЕсли;
    ЗаписьЖурналаРегистрации("ИнформационнаяБаза.ОбновлениеИБ", УровеньЖурналаРегистрации.Информация, ЭтотОбъект.Метаданные(), ,
	"Выполнена обработка " + ЭтаФорма.Заголовок);

Конецпроцедуры

//Процедуря для регистра сведений КИ обновляет все строки у которых тип - Адрес
//Для всех Адресов не из России в Представление добавляется Страна
Процедура ОбновитьАдресаКонтактнойИнформации()
	
	НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	
	НаборЗаписейКИ.Отбор.Тип.Значение = Перечисления.ТипыКонтактнойИнформации.Адрес;
	НаборЗаписейКИ.Отбор.Тип.Использование = Истина;
	НаборЗаписейКИ.Отбор.Тип.ВидСравнения = ВидСравнения.Равно;
	
	СообщитьОСостоянииОбновления("","Изменение регистра Контактная информация ");
	
	Состояние("Читаются данные из ИБ ...");

	НаборЗаписейКИ.Прочитать();
	
	ИмяРоссии = Справочники.КлассификаторСтранМира.Россия.Наименование;
	
	Состояние("Обрабатываются данные из ИБ ...");
	Для Каждого ЗаписьКИ Из НаборЗаписейКИ Цикл
		
		СформированноеПредставление = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдреса(ЗаписьКИ);
		Если СформированноеПредставление <> ЗаписьКИ.Представление Тогда
			Если ЗначениеЗаполнено(ЗаписьКИ.Поле1) И ЗаписьКИ.Поле1 <> ИмяРоссии Тогда
				
				//Ищем в самом начале представления наименование страны
				ИмяСтраны = ЗаписьКИ.Поле1;
				ПозицияСтраныВАдресе = Найти(ЗаписьКИ.Представление, ИмяСтраны);
				// добавляем страну в самое начало представления
				Если ПозицияСтраныВАдресе <> 1 Тогда
					НовоеПредставление = ЗаписьКИ.Поле1 + ", " + ЗаписьКИ.Представление;
					Состояние("Представление """ + ЗаписьКИ.Представление + """ изменено на """ + НовоеПредставление + "");
					ЗаписьКИ.Представление = НовоеПредставление;
				КонецЕсли
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Состояние("Записываются данные в ИБ ...");
	НаборЗаписейКИ.Записать();
	
	СообщитьОСостоянииОбновления("","Изменение регистра Контактная информация завершено");

КонецПроцедуры

// Корректировка справочника "СерииНоменклатуры".
//
Процедура КорректировкаСправочникаСерииНоменклатурыРоссия()

	Россия = Справочники.КлассификаторСтранМира.Россия;

	// Обработка справочника "СерииНоменклатуры".
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	(СерииНоменклатуры.СтранаПроисхождения.Код = ""643""
	|	ИЛИ СерииНоменклатуры.СтранаПроисхождения.Наименование = ""РОССИЯ"")
	|	И НЕ СерииНоменклатуры.СтранаПроисхождения.Предопределенный
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СерияОбъект                       = Выборка.Ссылка.ПолучитьОбъект();
		СерияОбъект.ОбменДанными.Загрузка = Истина;
		СерияОбъект.СтранаПроисхождения   = Россия;
		СерияОбъект.Записать();
	КонецЦикла;

КонецПроцедуры // КорректировкаСправочникаСерииНоменклатурыРоссия()

// Корректировка справочника "Номенклатура".
//
Процедура КорректировкаСправочникаНоменклатураРоссия()

	Россия = Справочники.КлассификаторСтранМира.Россия;

	// Обработка справочника "Номенклатура".
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	(Номенклатура.СтранаПроисхождения.Код = ""643""
	|	ИЛИ Номенклатура.СтранаПроисхождения.Наименование = ""РОССИЯ"")
	|	И НЕ Номенклатура.СтранаПроисхождения.Предопределенный
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		НоменклатураОбъект                     = Выборка.Ссылка.ПолучитьОбъект();
		НоменклатураОбъект.СтранаПроисхождения = Россия;
		НоменклатураОбъект.Записать();
	КонецЦикла;

КонецПроцедуры // КорректировкаСправочникаНоменклатураРоссия()

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = не ПолучитьРазрешениеНаОткрытиеФормы();
	
КонецПроцедуры

Процедура ПриОткрытии()
		флОбновитьАдресаКонтактнойИнформации = истина;
		флКорректировкаСправочникаСерииНоменклатуры = истина;
		флКорректировкаСправочникаНоменклатура = истина;
		флКорректировкаРегистраГражданствоФизЛиц = истина;

КонецПроцедуры



