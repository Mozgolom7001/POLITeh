
Процедура КнопкаВыполнитьНажатие(Кнопка)

	НаборЗаписей = РегистрыСведений.Штрихкоды.СоздатьНаборЗаписей();

	КоличествоСтрок = Товары.Количество() - 1;
	Для Тмп = 0 По КоличествоСтрок Цикл
		СтрокаТЧ = Товары[КоличествоСтрок - Тмп];

		Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура)
		   И ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения)
		   И ЗначениеЗаполнено(СтрокаТЧ.Качество)
		   И ЗначениеЗаполнено(СтрокаТЧ.ТипШтрихкода)Тогда
			НаборЗаписей.Очистить();

			НоваяСтрока = НаборЗаписей.Добавить();
			НоваяСтрока.ТипШтрихкода               = СтрокаТЧ.ТипШтрихкода;
			НоваяСтрока.Штрихкод                   = СтрокаТЧ.ШтрихКод;
			НоваяСтрока.ПредставлениеШтрихкода     = СтрокаТЧ.ПредставлениеШтрихкода;
			НоваяСтрока.Владелец                   = СтрокаТЧ.Номенклатура;
			НоваяСтрока.ЕдиницаИзмерения           = СтрокаТЧ.ЕдиницаИзмерения;
			НоваяСтрока.СерияНоменклатуры          = СтрокаТЧ.СерияНоменклатуры;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТЧ.ХарактеристикаНоменклатуры;
			НоваяСтрока.Качество                   = СтрокаТЧ.Качество;

			НаборЗаписей.Отбор.ТипШтрихкода.Установить(НоваяСтрока.ТипШтрихкода);
			НаборЗаписей.Отбор.Штрихкод.Установить(НоваяСтрока.Штрихкод);
			НаборЗаписей.Отбор.Владелец.Установить(НоваяСтрока.Владелец);
			НаборЗаписей.Отбор.ЕдиницаИзмерения.Установить(НоваяСтрока.ЕдиницаИзмерения);
			НаборЗаписей.Отбор.СерияНоменклатуры.Установить(НоваяСтрока.СерияНоменклатуры);
			НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Установить(НоваяСтрока.ХарактеристикаНоменклатуры);
			НаборЗаписей.Отбор.Качество.Установить(НоваяСтрока.Качество);

			Попытка
				НаборЗаписей.Записать();

				ПолучитьСерверТО().ОбработатьВведенныйШтрихкод(СтрокаТЧ.Штрихкод, СтрокаТЧ.ФормаВладелец);

				Товары.Удалить(СтрокаТЧ);
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПриОткрытии()

	Если Товары.Колонки.Найти("ФормаВладелец") = Неопределено Тогда
		Товары.Колонки.Добавить("ФормаВладелец");

		Если Не глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры") Тогда
			Колонка = ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры;
			Колонка.Видимость         = Ложь;
			Колонка.ИзменятьВидимость = Ложь;
		КонецЕсли;

		Если Не глЗначениеПеременной("ИспользоватьСерииНоменклатуры") Тогда
			Колонка = ЭлементыФормы.Товары.Колонки.СерияНоменклатуры;
			Колонка.Видимость         = Ложь;
			Колонка.ИзменятьВидимость = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ТоварыНоменклатураПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	Если ТекущаяСтрока.ЕдиницаИзмерения.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.ЕдиницаИзмерения = ТекущаяСтрока.Номенклатура.ЕдиницаХраненияОстатков;
	КонецЕсли;

	Если ТекущаяСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;

	Если ТекущаяСтрока.СерияНоменклатуры.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.СерияНоменклатуры = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Качество) Тогда
		ТекущаяСтрока.Качество = Справочники.Качество.Новый;
	КонецЕсли;

КонецПроцедуры

// Процедура
//
// Параметры:
//  
//
Процедура ДобавитьНеизвестныйШтрихкод(ТипШтрихкода, Штрихкод, Количество, ФормаВладелец) Экспорт

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Штрихкод", Штрихкод);
	СтруктураПоиска.Вставить("ФормаВладелец", ФормаВладелец);
	
	ТекущаяСтрока = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(Товары, СтруктураПоиска);
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = Товары.Добавить();
		ТекущаяСтрока.ТипШтрихкода  = ТипШтрихкода;
		ТекущаяСтрока.Штрихкод      = Штрихкод;
		Если ТипШтрихкода = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN128 Тогда
			ТекущаяСтрока.ПредставлениеШтрихкода = Штрихкод;
		КонецЕсли;
		ТекущаяСтрока.Количество    = 0;
		ТекущаяСтрока.ФормаВладелец = ФормаВладелец;
	КонецЕсли;

	ТекущаяСтрока.Количество = ТекущаяСтрока.Количество + Количество;

КонецПроцедуры // ДобавитьНеизвестныйШтрихкод()

Процедура ПриЗакрытии()

	КоличествоСтрок = Товары.Количество() - 1;
	Для Тмп = 0 По КоличествоСтрок Цикл
		СтрокаТЧ = Товары[КоличествоСтрок - Тмп];

		Если Не СтрокаТЧ.ФормаВладелец.Открыта() Тогда
			Товары.Удалить(СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ТоварыТипШтрихкодаПриИзменении(Элемент)

	Если Элемент.Значение = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN128
	   И ЭлементыФормы.Товары.ТекущиеДанные.ПредставлениеШтрихкода <> ЭлементыФормы.Товары.ТекущиеДанные.Штрихкод Тогда
		Если ПустаяСтрока(ЭлементыФормы.Товары.ТекущиеДанные.ПредставлениеШтрихкода) Тогда
			ЭлементыФормы.Товары.ТекущиеДанные.ПредставлениеШтрихкода = ЭлементыФормы.Товары.ТекущиеДанные.Штрихкод;
		Иначе
			Ответ = Вопрос("Представление штрихкода не пустое.
			|Изменить представление штрихкода на значение штрихкода?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ЭлементыФормы.Товары.ТекущиеДанные.ПредставлениеШтрихкода = ЭлементыФормы.Товары.ТекущиеДанные.Штрихкод;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

