Перем мСчитывателиRFID;
Перем мСессияАктивна;
Перем мИспользоватьСерии;

Процедура КнопкаВыполнитьНажатие(Кнопка)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	Если ТекущаяСтрока = Неопределено Тогда
		Предупреждение("Выберите строку, данные которой необходимо записать в RFID метки.");
		Возврат;
	ИначеЕсли ТекущаяСтрока.Записано Тогда
		Ответ = Вопрос("Данные выбранной строки уже были записаны."
		   + Символы.ПС + "Повторить запись?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Обнаружено = 0;
	Записано = 0;

	Результат = ПолучитьСерверТО().ПолучитьКоличествоМетокRFID(ТекущийСчитывательRFID, Обнаружено);

	Если Не ЗначениеЗаполнено(Результат) Тогда
		Если Обнаружено <> ТекущаяСтрока.Количество Тогда
			Ответ = Вопрос("Количество обнаруженных меток не совпадает с запрашиваемым количеством."
			   + Символы.ПС + "Для записи требуется " + СокрЛП(ТекущаяСтрока.Количество)
			   + " меток. Обнаружено " + СокрЛП(Обнаружено) + " меток."
			   + Символы.ПС + "Продолжить запись?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Ошибка = ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Результат);
		Предупреждение(Ошибка);
	КонецЕсли; 

	ПолучитьСерверТО().ЗаписатьДанныеВМеткиRFID(ТекущийСчитывательRFID, ТекущаяСтрока.Количество, ТекущаяСтрока.ШтрихКод, Записано);

	Если Записано <> ТекущаяСтрока.Количество И Не ТекущаяСтрока.Записано Тогда
		Ответ = Вопрос("Количество записанных меток не совпадает с запрашиваемым количеством"
		   + Символы.ПС + "Для записи требуется " + СокрЛП(ТекущаяСтрока.Количество)
		   + " меток. Записано " + СокрЛП(Обнаружено) + " меток."
		   + Символы.ПС + "Установить для строки признак записанных данных?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ТекущаяСтрока.Записано = Истина;

	Для Каждого СтрокаТЧ Из Товары Цикл
		Если Не СтрокаТЧ.Записано Тогда
			ЭлементыФормы.Товары.ТекущаяСтрока = СтрокаТЧ;
			Прервать;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // КнопкаВыполнитьНажатие()

Процедура ПриОткрытии()

	ПолучитьСерверТО().ПодключитьКлиента(ЭтаФорма);

	ЭлементыФормы.ТекущийСчитывательRFID.СписокВыбора = РаботаСТорговымОборудованием.ПолучитьСписокУстройствТОДляВыбора(мСчитывателиRFID);

	Если мСчитывателиRFID.Количество() = 0 Тогда
	Иначе
		ТекущийСчитывательRFID = мСчитывателиRFID[0];
	КонецЕсли;

	Если Не глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры") Тогда
		Колонка = ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры;
		Колонка.Видимость         = Ложь;
		Колонка.ИзменятьВидимость = Ложь;
	КонецЕсли;

	Если Не мИспользоватьСерии Тогда
		Колонка = ЭлементыФормы.Товары.Колонки.СерияНоменклатуры;
		Колонка.Видимость         = Ложь;
		Колонка.ИзменятьВидимость = Ложь;
	КонецЕсли;

	КоличествоМеток = 0;
	Если Не ЗначениеЗаполнено(ПолучитьСерверТО().ЗакрытьСессиюRFID(ТекущийСчитывательRFID, Истина, КоличествоМеток)) Тогда
		мСессияАктивна = ЗначениеЗаполнено(ПолучитьСерверТО().УстановитьРежимДрайвераRFID(ТекущийСчитывательRFID, Ложь));
	Иначе
		мСессияАктивна = Ложь;
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

// Функция возвращает признак того, что клиент поддерживает работу с видом ТО,
// переданным в качестве параметра.
//
// Параметры:
//  Вид      - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//           - Вид торгового оборудования, информация о поддержке
//             которого запрашивается.
//
// Возвращаемое значение:
//  <Булево> - Признак поддержки указанного класса торгового оборудования.
//
Функция ПоддерживаетсяВидТО(Вид) Экспорт

	Результат = Ложь;

	Если Вид = Перечисления.ВидыТорговогоОборудования.СчитывательRFIDМеток Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПоддерживаетсяВидТО()

Процедура ПриЗакрытии()

	Перем КоличествоМеток;

	Если Не ЗначениеЗаполнено(ПолучитьСерверТО().УстановитьРежимДрайвераRFID(ТекущийСчитывательRFID, Истина)) Тогда
		мСессияАктивна = Не ЗначениеЗаполнено(ПолучитьСерверТО().ОткрытьСессиюRFID(ТекущийСчитывательRFID));
	Иначе
		мСессияАктивна = Ложь;
	КонецЕсли;

	ПолучитьСерверТО().ОтключитьКлиента(ЭтаФорма);

КонецПроцедуры // ПриЗакрытии()

Процедура ТоварыНоменклатураПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	Если ТекущаяСтрока.ЕдиницаИзмерения.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.ЕдиницаИзмерения = ТекущаяСтрока.Номенклатура.ЕдиницаХраненияОстатков;
	КонецЕсли;

	Если ТекущаяСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;

	Если ТекущаяСтрока.СерияНоменклатуры.Владелец <> ТекущаяСтрока.Номенклатура Тогда
		ТекущаяСтрока.СерияНоменклатуры = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Качество) Тогда
		ТекущаяСтрока.Качество = Справочники.Качество.Новый;
	КонецЕсли;

КонецПроцедуры

Процедура ОбновлениеОтображения()

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		ЭлементыФормы.НадписьИнформация.Заголовок = "Выберите строку, данные которой необходимо записать в RFID метки.";
	Иначе
		ЭлементыФормы.НадписьИнформация.Заголовок = "Для записи штрихкода номенклатуры "
		   + СокрЛП(ТекущаяСтрока.Номенклатура) + " ("
		   + СокрЛП(ТекущаяСтрока.ЕдиницаИзмерения) + ", "
		   + ?(ЗначениеЗаполнено(ТекущаяСтрока.ХарактеристикаНоменклатуры), СокрЛП(ТекущаяСтрока.ХарактеристикаНоменклатуры) + ", ", "")
		   + ?(ЗначениеЗаполнено(ТекущаяСтрока.СерияНоменклатуры), СокрЛП(ТекущаяСтрока.СерияНоменклатуры) + ", ", "")
		   + СокрЛП(ТекущаяСтрока.Качество) + ")"
		   + " поместите в поле считывателя " + СокрЛП(ТекущаяСтрока.Количество)
		   + " меток и нажмите кнопку ""Записать данные текущей строки в RFID метки"".";
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанельТоварыУдалитьЗаписанныеСтроки(Кнопка)

	Если Товары.Найти(Истина, "Записано") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Ответ = Вопрос("Строки, у которых установлен признак ""Записано"" будут удалены."
	   + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	КоличествоСтрок = Товары.Количество() - 1;
	Для Тмп = 0 По КоличествоСтрок Цикл
		ТекущаяСтрока = Товары[КоличествоСтрок - Тмп];

		Если ТекущаяСтрока.Записано Тогда
			Товары.Удалить(ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

мСчитывателиRFID   = ПолучитьСерверТО().ПолучитьСписокУстройств(
   Перечисления.ВидыТорговогоОборудования.СчитывательRFIDМеток);
мСессияАктивна     = Ложь;
мИспользоватьСерии = глЗначениеПеременной("ИспользоватьСерииНоменклатуры");
