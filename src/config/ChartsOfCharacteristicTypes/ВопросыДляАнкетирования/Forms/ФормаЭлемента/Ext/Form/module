////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мТекущееОсновноеИзображение;
Перем мПустаяКартинка;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура устанавливает в поле картинки основное изображение вопроса
//
Процедура ПоказатьОсновноеИзображение() Экспорт
	
	Если мТекущееОсновноеИзображение = Неопределено Тогда
		ЭлементыФормы.ОсновноеИзображение.Картинка = мПустаяКартинка;
		
	Иначе
		ЭлементыФормы.ОсновноеИзображение.Картинка = мТекущееОсновноеИзображение;
		
	КонецЕсли;
	
КонецПроцедуры // ПоказатьОсновноеИзображение()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ОтветВВидеПриИзменении(ТипОтветаНаВопрос);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);	
	
	Если ЭтоНовый() Тогда
		КоличествоСтрокТаблицы	= 4;
		Длина					= 60;
		ТипОтветаНаВопрос		= Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета;
	КонецЕсли;
	
	ЭлементыФормы.ТабличноеПолеВариантыОтветов.Значение.Порядок.Очистить();
	ЭлементыФормы.ТабличноеПолеВариантыОтветов.Значение.Порядок.Установить("Код Возр");
	
	мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
	ПоказатьОсновноеИзображение();
	
	СписокДоступныхТиповОтвета = Новый СписокЗначений;
	Для Каждого ЭлементПеречисления Из Перечисления.ТипыОтветаНаВопросАнкеты Цикл
		ЗначениеПеречисления = ОбщегоНазначения.ПолучитьИмяЭлементаПеречисленияПоЗначению(ЭлементПеречисления);
		
		Если Найти(ЗначениеПеречисления, "Удалить") = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		СписокДоступныхТиповОтвета.Добавить(ЭлементПеречисления);
	КонецЦикла;
	ЭлементыФормы.ТипОтветаНаВопрос.ДоступныеЗначения = СписокДоступныхТиповОтвета;
	
	Если Не ВидКонтактнойИнформации.Пустая() Тогда
		ВидОбъектаКонтактнойИнформации	= ВидКонтактнойИнформации.ВидОбъектаКонтактнойИнформации;
		ТипКонтактнойИнформации			= ВидКонтактнойИнформации.Тип;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный И КоличествоСтрокТаблицы < 1 Тогда
			Сообщить("Табличный вопрос может иметь одну и более строк. Отредактируйте количество строк вопроса.");
			Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ОбработкаОповещения формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФорму" и Источник = Ссылка Тогда
		ИмяОбновляемогоЭлемента = Параметр.ИмяЭлемента;
		
		Если ИмяОбновляемогоЭлемента = "ОсновноеИзображение" Тогда
			// обновляем картинку на первой странице
			Если мТекущееОсновноеИзображение <> ОсновноеИзображение.Хранилище.Получить() Тогда
				мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
				ПоказатьОсновноеИзображение();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ОбработкаВыбора формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
		
	Если ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.ХранилищеДополнительнойИнформации") Тогда
		Если НЕ ОсновноеИзображение = ЗначениеВыбора Тогда
			ОсновноеИзображение = ЗначениеВыбора;
		КонецЕсли;
		
		мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
		ПоказатьОсновноеИзображение();
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОтветВВидеПриИзменении(Значение)
	
	// длина строки
	ЭлементыФормы.Длина.Видимость		= Ложь;
	ЭлементыФормы.Надпись10.Видимость	= Ложь;
	ЭлементыФормы.Надпись11.Видимость	= Ложь;
	
	ЭлементыФормы.ТипОтветаНаВопрос.ТолькоПросмотр		= Предопределенный;
	ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница	= ЭлементыФормы.ПанельВариантыОтвета.Страницы.Пустая;
	
	// Описание типов
	КЧ = Новый КвалификаторыЧисла(15, 2);
	КС = Новый КвалификаторыСтроки(1000);
	КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
	
	ОписаниеТиповЧисло	= Новый ОписаниеТипов("Число", КЧ);
	ОписаниеТиповСтрока	= Новый ОписаниеТипов("Строка", , КС);
	ОписаниеТиповДата	= Новый ОписаниеТипов("Дата", , , КД);
	ОписаниеТиповБулево	= Новый ОписаниеТипов("Булево");
	ОписаниеТиповВОТ	= Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов");
	
	Если Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Число Тогда
		ЭлементыФормы.Длина.Видимость		= Истина;
		ЭлементыФормы.Надпись10.Видимость	= Истина;
		ЭлементыФормы.Надпись11.Видимость	= Истина;
		
		Если ТипЗначения <> ОписаниеТиповЧисло Тогда
			ТипЗначения = ОписаниеТиповЧисло;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Строка Тогда
		ЭлементыФормы.Длина.Видимость		= Истина;
		ЭлементыФормы.Надпись10.Видимость	= Истина;
		ЭлементыФормы.Надпись11.Видимость	= Истина;
		
		Если ТипЗначения <> ОписаниеТиповСтрока Тогда
			ТипЗначения = ОписаниеТиповСтрока;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Текст Тогда
		
		Если ТипЗначения <> ОписаниеТиповСтрока Тогда
			ТипЗначения = ОписаниеТиповСтрока;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Дата Тогда
		
		Если ТипЗначения <> ОписаниеТиповДата Тогда
			ТипЗначения = ОписаниеТиповДата;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Булево Тогда
		
		Если ТипЗначения <> ОписаниеТиповБулево Тогда
			ТипЗначения = ОписаниеТиповБулево;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
		
		// табличный вопрос
		Если ТипЗначения <> ОписаниеТиповСтрока Тогда
			ТипЗначения = ОписаниеТиповСтрока;
		КонецЕсли;
		
		Если Предопределенный Тогда
			ЭлементыФормы.КолонкиТаблицы.ТолькоПросмотр	= Истина;
		КонецЕсли;
		
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.ТабличныйВопрос;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета
		  ИЛИ Значение = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета Тогда

		ЭлементыФормы.РамкаГруппы1.Заголовок = "Варианты ответов";
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.ВариантыОтветов;
		
		// вариант ответа
		Если Значение = Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета Тогда
			
			Если ТипЗначения <> ОписаниеТиповВОТ Тогда
				ТипЗначения = ОписаниеТиповВОТ;
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементыФормы.ТабличноеПолеВариантыОтветов.Видимость = Истина;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация Тогда
		// это контактная информация
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.КонтактнаяИнформация;
		
		ЭлементыФормы.ВидОбъектаКонтактнойИнформации.ТолькоПросмотр	= Предопределенный;
		ЭлементыФормы.ВидКонтактнойИнформации.ТолькоПросмотр		= Предопределенный;
		ЭлементыФормы.ТипКонтактнойИнформации.ТолькоПросмотр		= Предопределенный;
		
		Если ТипЗначения <> ОписаниеТиповСтрока Тогда
			ТипЗначения = ОписаниеТиповСтрока;
		КонецЕсли;
		
	ИначеЕсли Значение = Перечисления.ТипыОтветаНаВопросАнкеты.Ссылка Тогда
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.ОбъектИБ;
		
		ЭлементыФормы.ТипЗначения.ТолькоПросмотр	= Предопределенный;

	КонецЕсли;
	
КонецПроцедуры // ОтветВВидеПриИзменении()

Процедура ТипОтветаНаВопросПриИзменении(Элемент)
	
	ОтветВВидеПриИзменении(Элемент.Значение);
	
КонецПроцедуры

Процедура ВидКонтактнойИнформацииНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ВидОбъектаКонтактнойИнформации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Укажите вид объекта контактной информации.");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипКонтактнойИнформации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Укажите тип контактной информации.");
	КонецЕсли;
	
	УправлениеКонтактнойИнформацией.ОткрытьФормуВыбораВидаКИ(Истина, Элемент, ТипКонтактнойИнформации, ВидОбъектаКонтактнойИнформации);
	
КонецПроцедуры

Процедура НаименованиеПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ПолнаяФормулировка) Тогда
		ПолнаяФормулировка = Элемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновноеИзображениеНажатие(Элемент)
	
	Если ЭтоНовый() Тогда
		
		ОтветНаВопрос = Вопрос("Для добавления изображения необходимо записать элемент." + Символы.ПС + "Записать?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Записать();
			
		Иначе
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	РаботаСФайлами.ОткрытьФормуИзображения(ЭтаФорма, ОсновноеИзображение, Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ВариантыОтветов

// проверяем, записан ли вопрос
// иначе предлагаем записать - с тем, чтобы можно было 
// вводить записи подчиненного справочника
Процедура ТабличноеПолеВариантыОтветовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Если ЭтоНовый() Тогда
		ТекстВопроса = "Вопрос еще не записан! Записать?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Отказ = Ответ <> КодВозвратаДиалога.Да;
		
		Если Не Отказ Тогда
			ЗаписатьВФорме();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеВариантыОтветовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ВариантыОтветов = Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов");
	
	Если ТипЗначения <> ВариантыОтветов Тогда
		ТипЗначения = ВариантыОтветов;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мПустаяКартинка = Новый Картинка;

